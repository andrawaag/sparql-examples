@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-0e9a7c6286eda53c0fafacc75e64aacd> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Request_a_query/Archive/2024/02>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:genid-4e694113159d4e3db4a1a913894a81d850828-wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/>;
  sh:select """PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX bd: <http://www.bigdata.com/rdf#>
SELECT DISTINCT ?itemLabel 
                (sample(?image_) as ?image) 
                (group_concat(distinct ?instanceOfTypeLabel_;separator=\"; \" ) as ?instanceOfTypeLabel)  
                ?location 
                ?inception 
                (group_concat(distinct ?adminEntityLabel_;separator=\"; \" ) as ?adminEntityLabel)  
                ?historicCountyLabel 
WHERE {
  
  SERVICE bd:slice {
    ?item wdt:P1435 wd:Q15700834.
    bd:serviceParam bd:slice.offset 0 . # Start at item number (not to be confused with QID)
    bd:serviceParam bd:slice.limit 50000 . # List this many items
  }

    ?item wdt:P18 ?image_.
    ?item wdt:P31 ?instanceOfType.
    ?item wdt:P625 ?location.
    OPTIONAL { ?item wdt:P571 ?inception . }
    OPTIONAL { ?item wdt:P131 ?adminEntity . }
    OPTIONAL { ?item wdt:P7959 ?historicCounty . }

    SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". 
                ?item rdfs:label ?itemLabel . 
                ?instanceOfType rdfs:label ?instanceOfTypeLabel_ . 
                ?adminEntity rdfs:label ?adminEntityLabel_ .
                ?historicCounty rdfs:label ?historicCountyLabel .}
} 
GROUP BY ?itemLabel ?location ?inception ?historicCountyLabel""" .
