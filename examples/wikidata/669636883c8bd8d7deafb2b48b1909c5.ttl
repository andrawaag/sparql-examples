@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-669636883c8bd8d7deafb2b48b1909c5> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  rdfs:comment """ Contents 1 Coordinates by SIMC place ID 2 Idref Ids extraction 3 Music band label 4 With entities selected, how to show all values of a list of properties 5 Wikipedia:WikiProject_Women_in_Red/Missing_articles_by_occupation/Cartoonists 6 List of article sizes 7 Cities in germany that are at least 40,000 and at most 60,000 in population and those in at most 10 km to them 8 Extract all the products with labels from Wikidata 9 Things that are situated in problematic countries 10 Combining multiple \"any subclass of\" \"instance of\"s into a single search? 11 Different colors on map 12 Currently active rock metal bands and their hometowns 13 Getting number of cases/deaths/recoveries to mark most recent value as preferred 14 Query Optimization for bypassing the TimeOut Exception 15 Interlaced queries, e.g. works of a certain author; editions of those works; the publisher of those editions etc. 15.1 Online edition / series of creative works 15.1.1 Hyperlinked output (ISBN, OPAC) 15.2 published in (P1433) 15.3 Listeria bot 16 Query for multiple GND claims 17 Norwegian persons on norwegian wikipedia not having a reference for their birth-date or death-date 18 Strange timeout 18.1 Readaptation 19 Graph asked : a line per year with months 20 Listado de las organizaciones cubanas 21 Image grid based on multiple image properties 22 Town that have changed of name in the past 23 Show graph elements by property 24 Berlin is not appears in the list of capitals 25 Q207656 not found when querying for some instances 26 Things named after people involved in the slave trade 27 Find string in description 28 candidates 29 Query about international border (Q12413618) and closed border (Q87443571) 30 Released film/games with a pending rating 31 Select all properties with labels (and also knowns) and values of properties 32 Search in Wikipedia category 33 TimeOut Query 34 Foreign war graveyards in Norway 35 Munzinger person ID but GND missing 36 Similar references for the same value 37 wikidata-qnr property 38 List of countries in 1754 39 Query for usage of instances of inverse property label item (Q65932995) as objects in items 40 Has \"Formation\" and \"U.S.A.\" in title, without a subject 41 List of all current municipalities in Switzerland 42 List of articles of certain category with interwiki to a certain language 43 Generating a list of oldest living US Presidents 44 Error in Query? 45 Find any kind of religious building in a region 46 Most frequent date \"happened-on\" 47 Number of uses of a list of properties 48 Query error Coordinates by SIMC place ID [edit] Hello, I'm from Poland and for my wiki I need a query that gets exact coordinates of (mainly) villages by providing SIMC place IDs and these places' names. I wonder if you could help me, please :) Montanit (talk) 07:12, 1 June 2020 (UTC)[reply] This query makes list of all items with SIMC place ID with names and coordinates. It can also show the result as a map view. SELECT ?item ?itemLabel ?SIMC_place_ID ?coord
WHERE
{
  ?item wdt:P4046 ?SIMC_place_ID.
  OPTIONAL { ?item wdt:P625 ?coord. }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],pl\". }
}
 Try it! --Dipsacus fullonum (talk) 08:01, 1 June 2020 (UTC)[reply] Thanks a lot! :) Montanit (talk) 08:14, 1 June 2020 (UTC)[reply] Idref Ids extraction [edit] Hello, I'm failing with this extremly simple query aimed to extract the Idref Id of an author from Wikidata. Could you help me?SELECT ?idref WHERE { ?idref wdt:P269 wd:Q318391. } Hi, I think you exchanged object and subject. Try SELECT ?idref WHERE { wd:Q318391 wdt:P269 ?idref. }
 Try it! --Dipsacus fullonum (talk) 11:06, 1 June 2020 (UTC)[reply] Thanks a lot! Music band label [edit] Hi, I try to query all the music bands publishing their work on the \"Sarah records\" label (Q3473424). I find bands but my query does not seems to work.A band which match : https://www.wikidata.org/wiki/Q536403 I try : SELECT DISTINCT ?band ?bandLabel
WHERE
{
	?band  wdt:P31 wd:Q5741069 .
        ?band rdfs:label ?bandLabel .
	?band wdt:P264 wd:Q3473424 .
}
 Try it! --Le-sens-commun (talk) 16:19, 1 June 2020 (UTC) Finally I got it :[reply] SELECT DISTINCT ?band ?bandLabel WHERE {
  ?band rdfs:label ?bandLabel;
    wdt:P264 wd:Q3473424.
} LIMIT 206
 Try it! @Le-sens-commun: You may want to have each band only once in the result instead of once for each label. \"DISTINCT\" doesn't help for this as the labels are treated as being different because they have different language tags. I suggest this: SELECT ?band ?bandLabel WHERE {
  ?band  wdt:P264 wd:Q3473424.
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Dipsacus fullonum (talk) 17:36, 1 June 2020 (UTC)[reply] With entities selected, how to show all values of a list of properties [edit] I am choosing all the films that have an award: SELECT ?film ?filmLabel ?award ?awardLabel WHERE { ?film wdt:P31 wd:Q11424.
 ?film wdt:P166 ?award.
 SERVICE wikibase:label {
     bd:serviceParam wikibase:language \"en\" .
    } }But I wish to also see what language(s) the films are in, what language(s) the films support etc.I have a list of such desirable properties involving languages: http://www.wikidata.org/entity/P407 http://www.wikidata.org/entity/P1412 ... ...How do I select all of these properties as well as the films?I tried with the first two properties in the list:SELECT ?film ?filmLabel ?award ?awardLabel ?lang ?langLabel WHERE { ?film wdt:P31 wd:Q11424.
 ?film wdt:P166 ?award.
 ?film wdt:P407 ?lang.
 ?film wdt:P1412 ?lang.
 SERVICE wikibase:label {
     bd:serviceParam wikibase:language \"en\" .
    } }But even with that, I am not returning anything. Hi. 3 comments of what is wrong: When you use the same variable ?lang for two different properties, it means that you require the two properties to have the same value which is hardly what you intended. I have changed them to ?lang1 and ?lang2. When you want to see a value for a property if it present but still have a result for the film if the property isn't used for the film, you have to use the OPTIONAL keyword. languages spoken, written or signed (P1412) is intended for persons and should not be used for film. Therefore the column for P1412 is empty in the query below. That gives this rewritten query: SELECT ?film ?filmLabel ?award ?awardLabel ?lang1 ?lang1Label ?lang2 ?lang2Label WHERE {

 ?film wdt:P31 wd:Q11424.
 ?film wdt:P166 ?award.
 OPTIONAL { ?film wdt:P407 ?lang1. }
 OPTIONAL { ?film wdt:P1412 ?lang2. }
 SERVICE wikibase:label {
     bd:serviceParam wikibase:language \"en\" .
    }
}
 Try it! --Dipsacus fullonum (talk) 08:54, 2 June 2020 (UTC)[reply] Wikipedia:WikiProject_Women_in_Red/Missing_articles_by_occupation/Cartoonists [edit]is a long-standing query that several editors worked together to create in 2018. It has worked in the past but it currently times out regardless of how I tweak it. Basically it needs to capture the following people who don't have an en.wp article:All women who have the occupation Q1114448 or Q715301 and all subclasses like Q191633. All women who have an entry in the Lambiek Comiclopedia (Property:P5035) but might not have one of the above occupations on their items. Thank you. Gamaliel (talk) 16:45, 2 June 2020 (UTC)[reply] @Gamaliel: It seems that when either part of the UNION in that query is excluded, the query runs. So I split both criteria into separate subqueries (with some other modifications) and it seems to work: SELECT DISTINCT ?item ?linkcount ?lambiekurl
with {
  select ?occ WHERE {
    ?list wdt:P31 wd:Q65181907 .                     # find the WD item for the list
    ?article schema:name ?sitelink_list ; schema:about ?list ; schema:isPartOf <https://en.wikipedia.org/> .
    filter(str(?sitelink_list)=\"Wikipedia:WikiProject Women in Red/Missing articles by occupation/Cartoonists\")
    ?list p:P360 [ pq:P106 ?occ1 ].                    # get its occupations
    ?occ wdt:P279* ?occ1 .
  }
} as %i
with { SELECT ?item ?linkcount { 
  include %i.
    {?item wdt:P106 ?occ .}                            # item has this occupation
  MINUS { ?item wdt:P5035 ?lambiek } # those in the Comiclopedia omitted here, since it will be returned in the other subquery
  MINUS {?wen schema:about ?item ; schema:isPartOf <https://en.wikipedia.org/> . } # has no en.wikipedia sitelink
  ?item wdt:P21 wd:Q6581072 .                      # gender is female
  ?item wdt:P31 wd:Q5 .                            # instance is human
  ?item wikibase:sitelinks ?linkcount .            # number of sitelinks for the item
  }
} as %a
with { SELECT ?item ?linkcount ?lambiekurl { 
  include %i.
  ?item wdt:P5035 ?lambiek.  BIND ((CONCAT (\"[https://www.lambiek.net/artists/\", ?lambiek, \".htm entry]\")) as ?lambiekurl) . # is in the Lambiek Comiclopedia
  MINUS {?wen schema:about ?item ; schema:isPartOf <https://en.wikipedia.org/> . } # has no en.wikipedia sitelink
  ?item wdt:P21 wd:Q6581072 .                      # gender is female
  ?item wdt:P31 wd:Q5 .                            # instance is human
  ?item wikibase:sitelinks ?linkcount .            # number of sitelinks for the item
  }
} as %b
where
{
  {include %a} UNION {include %b}
}
 Try it! Mahir256 (talk) 17:21, 2 June 2020 (UTC)[reply] @Mahir256: The query works, but it only returns 797 items, while the last time Listeria successfully ran the query, it returned 3331 items. Gamaliel (talk) 17:28, 2 June 2020 (UTC)[reply] @Gamaliel: It seems, then, that some occupations previously returned in the \"%i\" subquery got lost (that part I did not change, and all that subquery returns are cartoonist (Q1114448), editorial cartoonist (Q3024627), and art director (Q24204895)). Would you happen to know how such de-linkages might have occurred? Mahir256 (talk) 17:34, 2 June 2020 (UTC)[reply] I don't know unfortunately. Maybe Tagishsimon might have some insight. Gamaliel (talk) 17:36, 2 June 2020 (UTC)[reply] @Mahir256: It looks like the list is pulling the occupation from Wikipedia:WikiProject Women in Red/Missing articles by occupation/Cartoonists (Q65183699). I added comics artist (Q715301) to that item but it didn't change the results. Is there a way to get the query to pull multiple occupations from that qualifier? Gamaliel (talk) 17:44, 2 June 2020 (UTC)[reply] @Gamaliel: It is easy to forget that query service lag exists. Mahir256 (talk) 17:51, 2 June 2020 (UTC)[reply] SELECT DISTINCT ?item ?linkcount ?lambiekurl
with {
  select ?occ WHERE { VALUES ?occ {
    wd:Q715301  # comics artists
    wd:Q1114448 # cartoonists 
  }
  }
} as %i
with { SELECT ?item ?linkcount { 
  include %i.
    {?item wdt:P106/wdt:P279* ?occ .}                            # item has this occupation
  MINUS { ?item wdt:P5035 ?lambiek } # those in the Comiclopedia omitted here, since it will be returned in the other subquery
  MINUS {?wen schema:about ?item ; schema:isPartOf <https://en.wikipedia.org/> . } # has no en.wikipedia sitelink
  ?item wdt:P21 wd:Q6581072 .                      # gender is female
  ?item wdt:P31 wd:Q5 .                            # instance is human
  ?item wikibase:sitelinks ?linkcount .            # number of sitelinks for the item
  }
} as %a
with { SELECT ?item ?linkcount ?lambiekurl { 
  include %i.
  ?item wdt:P5035 ?lambiek.  BIND ((CONCAT (\"[https://www.lambiek.net/artists/\", ?lambiek, \".htm entry]\")) as ?lambiekurl) . # is in the Lambiek Comiclopedia
  MINUS {?wen schema:about ?item ; schema:isPartOf <https://en.wikipedia.org/> . } # has no en.wikipedia sitelink
  ?item wdt:P21 wd:Q6581072 .                      # gender is female
  ?item wdt:P31 wd:Q5 .                            # instance is human
  ?item wikibase:sitelinks ?linkcount .            # number of sitelinks for the item
  }
} as %b
where
{
  {include %a} UNION {include %b}
}
 Try it! @Mahir256: Since the problem seemed to be with Wikipedia:WikiProject Women in Red/Missing articles by occupation/Cartoonists (Q65183699), I removed that entirely and put the occupations directly in the query you constructed. Listeria has returned 3496 items. Thank you for your help! Gamaliel (talk) 18:16, 2 June 2020 (UTC)[reply] @Gamaliel: (The comment about query lag was merely intended to suggest that if you just waited until all the query servers caught up, the occupations that were subclasses of \"comics artist\" would get returned, since \"comics artist\" was now a qualifier of the is a list of (P360) statement. Indeed, if you try my original query again, those 3496 (3497?) items would get returned.) Mahir256 (talk) 18:20, 2 June 2020 (UTC)[reply] List of article sizes [edit] Hi! I did not find anything helpful in the archives. My request: I need a list of the sizes of all articles sitelinked with Nibal Thawabteh (Q18207978). The item has (with commons) 11 sitelinks. And I need a list of all the 11 sitelinks. A feature but not necessary were the result in kByte instead of Byte. Thank you a lot, Doc Taxon (talk) 20:29, 2 June 2020 (UTC)[reply] @Doc Taxon: I added some extra variables to select to help me debug. Feel free to remove what you don't need. SELECT ?sitelink ?site ?endpoint ?title ?size ?size_kbyte
WHERE
{
  ?sitelink schema:about wd:Q18207978.
  ?sitelink schema:isPartOf ?site.
  ?sitelink schema:name ?title.
  BIND (STRBEFORE(STRAFTER(STR(?site), \"https://\"), \"/\") AS ?endpoint)
  BIND (STR(?title) AS ?title_nolang)
  SERVICE wikibase:mwapi
  {
    bd:serviceParam wikibase:endpoint ?endpoint.
    bd:serviceParam wikibase:api \"Generator\".
    bd:serviceParam mwapi:generator \"revisions\".
    bd:serviceParam mwapi:titles ?title_nolang.
    bd:serviceParam mwapi:prop \"revisions\".
    bd:serviceParam mwapi:rvprop \"size\".
    bd:serviceParam mwapi:grvdir \"older\".
    bd:serviceParam mwapi:grvlimit \"max\".
    ?size wikibase:apiOutput \"revisions/rev[last()]/@size\".
  }
  BIND (ROUND(xsd:integer(?size)/1024) AS ?size_kbyte)
}
 Try it! --Dipsacus fullonum (talk) 22:18, 2 June 2020 (UTC)[reply] Wow, cool! And is it possible to order it size descending? Doc Taxon (talk) 04:11, 3 June 2020 (UTC)[reply] @Doc Taxon: Yes, add ORDER BY DESC(?size) as the last line after the closing } of the WHERE clasue. --Dipsacus fullonum (talk) 06:12, 3 June 2020 (UTC)[reply] PS: You may get wrong or truncated results or maybe timeouts if you try this query with items that have sitelinks with many revisions. The reason for this is discussed at Wikidata:Contact the development team/Query Service and search#Wish for MW API access from WDQS: bd:serviceParam wikibase:limit value to not use continuations but still make more API calls. --Dipsacus fullonum (talk) 07:50, 3 June 2020 (UTC)[reply] Cities in germany that are at least 40,000 and at most 60,000 in population and those in at most 10 km to them [edit] I am looking for Cities in germany that are at least 40,000 and at most 60,000 in population and those cities and villages that are at most 10 km away from them shown as map and list of city/village names with distance to the city that is at least 40,000 and at most 60,000 in population Which Query does that? I found 261 German cities with 40,000-60,000 inhabitants, and 175,990 items located at most 10 km from these cities. Most of the nearby items are not other cities but other things like streets and houses. However I cannot make a query to find which of the 175,990 nearby items are cities that can run in less 60 seconds so it doesn't timeout. There are simply too many to handle. This query list the 175,990 items: SELECT ?city ?citizens ?city_coord ?place ?place_coord ?distance_from_city 
WHERE
{
  ?city wdt:P31 / wdt:P279 * wd:Q42744322 .
  ?city wdt:P1082 ?citizens .
  FILTER (?citizens >= 40000 && ?citizens <= 60000)
  ?city wdt:P625 ?city_coord .
  SERVICE wikibase:around
  { 
    ?place wdt:P625 ?place_coord . 
    bd:serviceParam wikibase:center ?city_coord . 
    bd:serviceParam wikibase:radius \"10\" . 
    bd:serviceParam wikibase:distance ?distance_from_city.
  }
}
 Try it! Your wanted query would probably be possible if you reduced the area, for instance to only one Bundesland per query. --Dipsacus fullonum (talk) 14:39, 3 June 2020 (UTC)[reply] Nice. I tried with municipalities and got some 1000. Obviously, it times out even more quickly. --- Jura 14:45, 3 June 2020 (UTC)[reply] Extract all the products with labels from Wikidata [edit] Hello, I tried to extract all the products embedded in Wikidata, but I faced an exception that is TimeOut Exception. So, as I understand from this exception, I think that I have to use some strategies of SPARQL Optimization in order to successfully execute this query. My query is just a simple triple pattern but as I said it needs applying some Optimisation services: SELECT ?Products where {?Products wdt:P279* wd:Q2424752.} The optimization is this case is a GAS program (GAS stands for Gather-Apply-Scatter, documentation is at https://github.com/blazegraph/database/wiki/RDF_GAS_API). It finds 972,898 products with a breath-first search with the last found product at search depth 19. I could not get it to always run. Try lower values for gas:maxIterations if it times out. There is no hope at all to find labels for nearly a million items in a query. SELECT ?product ?depth
WHERE
{
  SERVICE gas:service
  {
    gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.BFS\" .
    gas:program gas:linkType wdt:P279 .
    gas:program gas:traversalDirection \"Reverse\" .
    gas:program gas:in wd:Q2424752 .
    gas:program gas:out ?product .
    gas:program gas:out1 ?depth .
    gas:program gas:maxIterations 20 .
  }
}
ORDER BY ASC(?depth)
 Try it! --Dipsacus fullonum (talk) 16:27, 3 June 2020 (UTC)[reply] }} --Thanks a lot Dipsacus fullonum Things that are situated in problematic countries [edit] Hello, I'd like a query list for the current things (things that aren't dissolved/destroyed) being said in country X (country (P17) with only one country X and not matching that country X from located in the administrative territorial entity (P131) such as this problem Bački Breg (Q779764). 13:45, 3 June 2020 (UTC)Bouzinac (talk) The query will timeout if I try to search for all countries at once. This query will find items located in Serbia (Q403) but also located in an administrative entity which aren't in the country. SELECT ?item ?itemLabel ?administrative_entity ?administrative_entityLabel ?administrative_entity_country ?administrative_entity_countryLabel
WHERE
{
  VALUES ?country { wd:Q403 }
  ?item wdt:P17 ?country .
  OPTIONAL { ?item wdt:P576 ?dissolved . }
  FILTER (! BOUND(?dissolved))
  ?item wdt:P131 ?administrative_entity .
  MINUS
  {
    ?administrative_entity wdt:P17 ?country .
  }
  OPTIONAL { ?administrative_entity wdt:P17 ?administrative_entity_country . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Dipsacus fullonum (talk) 17:21, 3 June 2020 (UTC)[reply] Nice try Dipsacus fullonum (talk • contribs • logs), I think it should be filtered out from things that a related to any border (which can be multiple countries). It should also filter out things that are abstracts/studies/etc and not concrete as buildings, churches, cities, etc (like this one Filgotinib Alone and in Combination With Methotrexate (MTX) in Adults With Moderately to Severely Active Rheumatoid Arthritis Who Are Naive to MTX Therapy (Q63813246)) Bouzinac (talk) 19:26, 3 June 2020 (UTC)[reply] Combining multiple \"any subclass of\" \"instance of\"s into a single search? [edit] Hi folks!In the search below I'm looking for anything that is a subclass of cultural institution (Q5193377) on the line: (?library (wdt:P31/(wdt:P279*)) wd:Q5193377.)Unfortunately, cultural institution (Q5193377) by itself doesn't return all the items I'm looking for, so I need to also look for any subclass of cultural heritage (Q210272), tourist attraction (Q570116), and performing arts building (Q57660343). Is there any way to combine these in one search so that I'm searching for any subclass of these four items? SELECT DISTINCT ?library ?libraryLabel ?located_at_street_address ?coordinate_location ?phone_number ?e_mail_address ?countyLabel
WHERE {
  VALUES (?located_in_the_administrative_territorial_entity ?countyLabel)
  {
    (wd:Q491547 \"Union County\") (wd:Q503538 \"Towns County\") (wd:Q503546 \"Rabun County\")
    (wd:Q492040 \"Lumpkin County\") (wd:Q389365 \"White County\") (wd:Q501096 \"Habersham County\")
    (wd:Q498362 \"Stephens County\") (wd:Q492012 \"Hall County\") (wd:Q488201 \"Banks County\")
    (wd:Q385931 \"Franklin County\") (wd:Q491301 \"Hart County\") (wd:Q486838 \"Barrow County\")
    (wd:Q486137 \"Jackson County\") (wd:Q156387 \"Madison County\") (wd:Q492016 \"Elbert County\")
    (wd:Q498312 \"Walton County\") (wd:Q492026 \"Oconee County\") (wd:Q112061 \"Clarke County\")
    (wd:Q491525 \"Oglethorpe County\") (wd:Q491759 \"Wilkes County\") (wd:Q491519 \"Lincoln County\")
    (wd:Q501101 \"Newton County\") (wd:Q493083 \"Morgan County\") (wd:Q486765 \"Greene County\")
  }
  FILTER NOT EXISTS { ?library wdt:P576 [] }
  ?library (wdt:P31/(wdt:P279*)) wd:Q5193377.
  ?library wdt:P131 ?located_in_the_administrative_territorial_entity. hint:Prior hint:runFirst true.
  OPTIONAL { ?library wdt:P6375 ?located_at_street_address. }
  OPTIONAL { ?library wdt:P625 ?coordinate_location. }
  OPTIONAL { ?library wdt:P1329 ?phone_number. }
  OPTIONAL { ?library wdt:P968 ?e_mail_address. }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }
}
ORDER BY ?library
LIMIT 10000
 Try it! Thanks in advance! Clifflandis (talk) 19:53, 3 June 2020 (UTC)[reply] @Clifflandis: It seems that adding cultural heritage (Q210272) gives 8 more results to your query, but neither adding tourist attraction (Q570116) or performing arts building (Q57660343) gives any more results. You can add them using VALUES. Instead of   ?library (wdt:P31/(wdt:P279*)) wd:Q5193377.
 use   VALUES ?class { wd:Q5193377 wd:Q210272 wd:Q570116 wd:Q57660343 }
  ?library (wdt:P31/(wdt:P279*)) ?class.
 --Dipsacus fullonum (talk) 06:38, 4 June 2020 (UTC)[reply] Different colors on map [edit] Hi all! Starting from the following query #Biblioteche, musei e chiese a Roma
#defaultView:Map
SELECT DISTINCT ?luogo ?luogoLabel ?coordinate ?layer
WHERE {
   BIND(wd:Q7075 AS ?biblioteca).
   BIND(wd:Q33506 AS ?museo).
   BIND(wd:Q16970 AS ?chiesa).
  ?luogo wdt:P131 wd:Q220 .
  OPTIONAL { ?luogo wdt:P625 ?coordinate . }
  { ?luogo wdt:P31/wdt:P279* ?biblioteca . }
  UNION
  { ?luogo wdt:P31/wdt:P279* ?museo . }
  UNION
  { ?luogo wdt:P31/wdt:P279* ?chiesa . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"it,en\". }
}
 Try it! would it be possible to show in three different colors \"biblioteca\", \"museo\" and \"chiesa\" in the map? Thank you very much, --Epìdosis 10:04, 4 June 2020 (UTC)[reply] @Epìdosis: Sure. You already selected the variable ?layer, so I just added 3 different bindings for it: #Biblioteche, musei e chiese a Roma
#defaultView:Map
SELECT DISTINCT ?luogo ?luogoLabel ?coordinate ?layer
WHERE {
   BIND(wd:Q7075 AS ?biblioteca).
   BIND(wd:Q33506 AS ?museo).
   BIND(wd:Q16970 AS ?chiesa).
  ?luogo wdt:P131 wd:Q220 .
  OPTIONAL { ?luogo wdt:P625 ?coordinate . }
  { ?luogo wdt:P31/wdt:P279* ?biblioteca . BIND(1 AS ?layer) }
  UNION
  { ?luogo wdt:P31/wdt:P279* ?museo . BIND(2 AS ?layer) }
  UNION
  { ?luogo wdt:P31/wdt:P279* ?chiesa . BIND(3 AS ?layer) }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"it,en\". }
}
 Try it! --Dipsacus fullonum (talk) 10:10, 4 June 2020 (UTC)[reply] @Epìdosis: Here is a simplified version where one VALUES definition replaces the UNIONs: #Biblioteche, musei e chiese a Roma
#defaultView:Map
SELECT DISTINCT ?luogo ?luogoLabel ?coordinate ?layer
WHERE {
  VALUES ?layer { wd:Q7075 wd:Q33506 wd:Q16970 } # biblioteca, museo e chiesa
  ?luogo wdt:P131 wd:Q220 .
  OPTIONAL { ?luogo wdt:P625 ?coordinate . }
  ?luogo wdt:P31/wdt:P279* ?layer.
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"it,en\". }
}
 Try it! --Dipsacus fullonum (talk) 10:22, 4 June 2020 (UTC)[reply] You can use ?layerLabel in SELECT, instead of ?layer to get a more readable one. --- Jura 12:39, 4 June 2020 (UTC)[reply] Correct and a good idea! --Dipsacus fullonum (talk) 12:48, 4 June 2020 (UTC)[reply] #Biblioteche, musei e chiese a Roma
#defaultView:Map{\"hide\":[\"?coordinate\"], \"layer\": \"?typeLabel\"}
SELECT DISTINCT 
  ?luogo ?luogoLabel ?luogoDescription 
  ?coordinate ?typeLabel ?img
WHERE 
{
  VALUES ?type { wd:Q7075 wd:Q33506 wd:Q16970 } # biblioteca, museo e chiesa
  ?luogo wdt:P131/wdt:P131* wd:Q220 .
  ?luogo wdt:P31/wdt:P279* ?type.
  OPTIONAL { ?luogo wdt:P625 ?coordinate . }
  OPTIONAL { ?luogo wdt:P18 ?img }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"it,en\". }
}
 Try it! Above with a few other tweaks. --- Jura 12:43, 4 June 2020 (UTC)[reply] Currently active rock metal bands and their hometowns [edit] Hello everyone I am trying to get a list of currently active bands in the genres stoner rock/metal(Q617240), sludge(Q720959), doom(Q186170), alternative rock(Q11366) and progressive rock(Q49451), along with their location of formation/origin(P740) and accompanying coordinates. Thanks a lot! Here is a query. The results can both be shown as a table and on a map. SELECT ?band ?bandLabel ?genre ?genreLabel ?location_of_formation ?location_of_formationLabel ?coordinates
{
  ?band wdt:P31 wd:Q215380. # ?band is a band
  VALUES ?genre { wd:Q617240 wd:Q720959 wd:Q186170 wd:Q11366 wd:Q49451 }
  ?band wdt:P136 ?genre. # ?band has one of genres in ?genre
  OPTIONAL { ?band wdt:P576 ?dissolved }
  FILTER (! BOUND(?dissolved)) # Band has no dissolve date
  OPTIONAL {
    ?band wdt:P740 ?location_of_formation.
    OPTIONAL { ?location_of_formation wdt:P625 ?coordinates. }
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Dipsacus fullonum (talk) 09:07, 4 June 2020 (UTC)[reply] Mange tak Dipsacus fullonum! How would you go about also selecting the year of latest release? @Andrekotze69: SELECT ?band ?bandLabel ?genre ?genreLabel ?location_of_formation ?location_of_formationLabel ?coordinates
  (MAX(?release_date) AS ?latest_release)
{
  ?band wdt:P31 wd:Q215380. # ?band is a band
  VALUES ?genre { wd:Q617240 wd:Q720959 wd:Q186170 wd:Q11366 wd:Q49451 }
  ?band wdt:P136 ?genre. # ?band has one of genres in ?genre
  OPTIONAL { ?band wdt:P576 ?dissolved }
  FILTER (! BOUND(?dissolved)) # Band has no dissolve date
  OPTIONAL {
    ?band wdt:P740 ?location_of_formation.
    OPTIONAL { ?location_of_formation wdt:P625 ?coordinates. }
  }
  OPTIONAL {
    ?album wdt:P175 ?band.
    ?album wdt:P577 ?release_date.
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
GROUP BY ?band ?bandLabel ?genre ?genreLabel ?location_of_formation ?location_of_formationLabel ?coordinates
 Try it! --Dipsacus fullonum (talk) 15:55, 4 June 2020 (UTC)[reply] Many thanks Dipsacus fullonum! I have tried it with genre \"heavy metal\" (Q38848) and am wondering why it doesn't return the bands Danzig, AC/DC or Ghost? Any help is much appreciated!I figured it out, it was because some artists are \"rock bands\" (Q5741069) and not \"musical groups\". Also some e.g. 1000mods don't have P740 \"location of formation\" but rather P131 \"located in the administrative territorial entity\" or P495 \"country of origin\" @Andrekotze69: You can change ?band wdt:P31 wd:Q215380. to ?band wdt:P31 / wdt:P279* wd:Q215380. to include all subclasses of bands. That will get more bands, but not AC/DC (Q27593) or Danzig (Q649175) because their use of genre (P136) don't include any of the genres in your list. You could get located in the administrative territorial entity (P131) but that will show where they are located now instead of their origin. I would want that in another column as they are different things. I think use of country of origin (P495) for a band is plainly wrong. The property is intended for creative works like films and television series. --Dipsacus fullonum (talk) 19:01, 4 June 2020 (UTC)[reply] Getting number of cases/deaths/recoveries to mark most recent value as preferred [edit] Following up from here, could number of cases (P1603), number of deaths (P1120), and number of recoveries (P8010) be made so that they automatically mark the most recent value as preferred? Sdkb (talk) 09:55, 4 June 2020 (UTC)[reply] @Sdkb: You cannot make a query that will edit Wikidata. You can however use the results from a query to find out what you want to change by using other tools. --Dipsacus fullonum (talk) 16:02, 4 June 2020 (UTC)[reply] @Dipsacus fullonum: Thanks for the reply. I'm new to Wikidata, so I'm just posting here since that's where I was directed. If this isn't the spot to achieve what I'm trying to do, it'd be helpful to have guidance on where to go/what to do. My goal here is to get this page updating with Wikidata data (initial efforts here). Sdkb (talk) 17:24, 4 June 2020 (UTC)[reply] Query Optimization for bypassing the TimeOut Exception [edit] Hello, I wrote the following Query that consists of extracting the products and their brands, but the result that I received is not efficient and generates the TimeOut Exception. Therefore, I need to optimize it and please provide me some relevant references for SPARQL Query Optimization. select DISTINCT ?brand ?brandLabel (GROUP_CONCAT(?itemName; SEPARATOR=\", \") AS ?ProductsItemName) 
where {
    ?CompanyID wdt:P31 wd:Q4830453.
    OPTIONAL{?CompanyID wdt:P1056 ?MaterialProduced.}
    OPTIONAL {?item wdt:P279* ?MaterialProduced.
             ?item wdt:P1716 ?brand.}
  SERVICE wikibase:label{
            bd:serviceParam wikibase:language \"en\".
            ?item rdfs:label ?itemName.
           ?brand rdfs:label ?brandLabel.
        }
}
GROUP BY ?brand ?brandLabel
 Try it! An obvious optimization is to remove the OPTIONAL keywords. You need results from the OPTIONAL clauses to get values for ?brand and ?item, so the aren't really optional. That being said I don't think it will be possible to avoid timeout because there are far too many results. My counting gave 171,070 companies, 11,760 products and over a million items. You will need to limit the query to get a smaller result by adding more restrictive criteria. An general search for all types of products at once is impossible in 60 seconds. --Dipsacus fullonum (talk) 09:34, 6 June 2020 (UTC)[reply] @Dipsacus fullonum, Dipsacus fullonum:: Thank you very much. In fact, I removed the OPTIONAL KeyWord. But I still faced a problem of redundancy. In the following Query, the field ?ProductsItemName contains redundant data and when i used Distinct KeyWord, the query generates an Exception. Help me in eliminating the redundancy. select  ?brand ?brandLabel (GROUP_CONCAT(?itemName; SEPARATOR=\", \") AS ?ProductsItemName) 
where {
  
    ?CompanyID wdt:P31   wd:Q4830453.
    ?CompanyID wdt:P1056 ?MaterialProduced.
     ?item      wdt:P279*  ?MaterialProduced.
    ?item      wdt:P1716 ?brand.
  
  SERVICE wikibase:label{
            bd:serviceParam wikibase:language \"en\".
            ?item rdfs:label ?itemName.
           ?brand rdfs:label ?brandLabel.
        }
}
GROUP BY ?brand ?brandLabel
 Try it! --Bilalox01 (talk) 07:14, 7 June 2020 (UTC)[reply] @Dipsacus fullonum: I found the solution, and I want to share it. It is simple, Just we have to add Distinct KeyWord inside Template:(GROUP CONCAT(?itemName; SEPARATOR=\", \") AS ?ProductsItemName) select  ?brand ?brandLabel (GROUP_CONCAT( Distinct ?itemName; SEPARATOR=\", \") AS ?ProductsItemName) 
where {
  
    ?CompanyID wdt:P31   wd:Q4830453.
    ?CompanyID wdt:P1056 ?MaterialProduced.
     ?item      wdt:P279*  ?MaterialProduced.
    ?item      wdt:P1716 ?brand.
  
  SERVICE wikibase:label{
            bd:serviceParam wikibase:language \"en\".
            ?item rdfs:label ?itemName.
           ?brand rdfs:label ?brandLabel.
        }
}
GROUP BY ?brand ?brandLabel
 Try it! --Bilalox01 (talk) 07:36, 7 June 2020 (UTC) Bilalox01 (talk) 07:36, 7 June 2020 (UTC)Bilalox01[reply] Interlaced queries, e.g. works of a certain author; editions of those works; the publisher of those editions etc. [edit] 1. I created this table. There are listed of 8 volumes of a dictionary.Each of the volume (Q1238720) 2 to 8 has a (digital) edition (Property:P747). All these version, edition or translation (Q3331189) have a value for full work available at URL (P953).volume (Q1238720) 1 has a second printed edition and this second edition (in turn) has a digital edition (as volumen 2 to 7).How can I get the values for full work available at URL (P953) into last column of the table (of course, by other means than copy & paste)?2. Similar problem:I created this two tables: G. Sprigath als Herausgeberin and G. Sprigath als Buch-Autorin This two tables are (more or less) okay for me. I created also a further table - this one: G. Sprigath als Übersetzerin. This is a list of essays, which where translated by Gabriele Sprigath. This essays were published within two editions of a certain book. Among other information I would like to mention the publisher (P123) and the place of publication (P291) of this editions within the table (column 6 and 7) as well. What would I have to add to the code of the SPARQL-query?3. Similar queries would be: car manufactures in a certain town / quantity of workers of each of the manufactures (within that town). political parties within a certains country / the chairpersons of each of this parties / the start time (P580), when this chairpersons came into office. etc. --Nstrc (talk) 10:58, 6 June 2020 (UTC)[reply] Online edition / series of creative works [edit] 1. It could be something likke this: SELECT ?volume ?edition ?available_at ?derivative_work ?derivative_work_edition ?derivative_work_available_at
WHERE
{
  ?volume wdt:P179 wd:Q96024609.
  OPTIONAL
  {
    ?volume wdt:P747 ?edition.
    ?edition wdt:P953 ?available_at.
  }
  OPTIONAL
  {
    ?volume wdt:P4969 ?derivative_work.
    ?derivative_work wdt:P747 ?derivative_work_edition.
    ?derivative_work_edition wdt:P953 ?derivative_work_available_at.
  }
}
 Try it! It would also be possible to reuse the columns for the two cases, but then you wouldn't know when the edition is a derivative_work. --Dipsacus fullonum (talk) 13:45, 6 June 2020 (UTC)[reply]I reworked it in this way. SELECT ?Titel (year(?Erscheinungsdatum) as ?Erscheinungsjahr) ?ISBN ?Umfang ?DigitalisatURL ?Datensatz WHERE {
  { ?Datensatz wdt:P179 wd:Q96024609. }
  OPTIONAL {
    ?Datensatz wdt:P1476 ?Titel.
  }
  OPTIONAL {
    ?Datensatz wdt:P577 ?Erscheinungsdatum.
  }
  OPTIONAL {
    ?Datensatz wdt:P957 ?ISBN. 
  }
    OPTIONAL {
    ?Datensatz wdt:P1104 ?Umfang. 
  }
        OPTIONAL {
    ?Datensatz wdt:P1292 ?DNB. 
  }
        OPTIONAL {
    ?Datensatz wdt:P6721 ?K10plus. 
  }  
  OPTIONAL
  {
    ?Datensatz wdt:P747 ?Ausgabe.
    ?Ausgabe wdt:P953 ?DigitalisatURL.
  }
  OPTIONAL
  {
    ?Datensatz wdt:P4969 ?Bearbeitung.
    ?Bearbeitung wdt:P747 ?derivative_work_edition.
    ?derivative_work_edition wdt:P953 ?DigitalisatURL.
  }
}
 Try it! Volume 1 is displayed only in one line; volume 2 to 8 are displayed each in two lines. Would be it possible, that also volume 2 to 8 would need only one line as well?--Nstrc (talk) 15:53, 6 June 2020 (UTC)[reply] @Nstrc: Volume 2 to 8 are displayed in two lines because they each have two ISBN numbers. That can be fixed grouping the results by everything else than ISBN: SELECT ?Titel (year(?Erscheinungsdatum) as ?Erscheinungsjahr) (GROUP_CONCAT(?ISBN_) AS ?ISBN) ?Umfang ?DigitalisatURL ?Datensatz WHERE {
  ?Datensatz wdt:P179 wd:Q96024609.
  OPTIONAL {
    ?Datensatz wdt:P1476 ?Titel.
  }
  OPTIONAL {
    ?Datensatz wdt:P577 ?Erscheinungsdatum.
  }
  OPTIONAL {
    ?Datensatz wdt:P957 ?ISBN_.
  }
  OPTIONAL {
    ?Datensatz wdt:P1104 ?Umfang. 
  }
  OPTIONAL {
    ?Datensatz wdt:P1292 ?DNB. 
  }
  OPTIONAL {
    ?Datensatz wdt:P6721 ?K10plus. 
  }  
  OPTIONAL
  {
    ?Datensatz wdt:P747 ?Ausgabe.
    ?Ausgabe wdt:P953 ?DigitalisatURL.
  }
  OPTIONAL
  {
    ?Datensatz wdt:P4969 ?Bearbeitung.
    ?Bearbeitung wdt:P747 ?derivative_work_edition.
    ?derivative_work_edition wdt:P953 ?DigitalisatURL.
  }
}
GROUP BY ?Titel ?Erscheinungsdatum ?Umfang ?DigitalisatURL ?Datensatz
 Try it! --Dipsacus fullonum (talk) 16:19, 6 June 2020 (UTC)[reply] Thank your very much. --Nstrc (talk) 16:25, 6 June 2020 (UTC)[reply] Hyperlinked output (ISBN, OPAC) [edit] The output there: https://w.wiki/TH3 is as follows: Titel Erscheinungsjahr Umfang ISBN DNB K10plus DigitalisatURL Datensatz Band 1 Abhängigkeit bis Bund 1983 205 3-88619-033-1 831087498 125332025 <http://www.inkrit.de/neuinkrit/mediadaten/archivkwm/KWM01.pdf> wd:Q96026743 Band 2 Cäsarismus bis Funktionär 1984 197 3-88619-052-8 3-88619-062-5 840398360 125332106 http://www.inkrit.de/neuinkrit/mediadaten/archivkwm/KWM02.pdf> wd:Q96031980 Band 3 Gattung bis Judenfrage 1985 184 3-88619-053-6 3-88619-063-3 850368103 125332157 <http://www.inkrit.de/neuinkrit/mediadaten/archivkwm/KWM03.pdf> wd:Q96032581 Band 4 Kadetten bis Lyssenkismus 1986 220 3-88619-054-4 3-88619-064-1 860515966 125332211 <http://www.inkrit.de/neuinkrit/mediadaten/archivkwm/KWM04.pdf> wd:Q96033164 Band 5 Machismus bis Owenismus 1986 174 3-88619-055-2 3-88619-065-X 861000641 125332254 <http://www.inkrit.de/neuinkrit/mediadaten/archivkwm/KWM05.pdf> wd:Q96033337 Band 6 Pariser Kommune bis Romantik 1987 174 3-88619-056-0 3-88619-066-8 870865528 125332327 <http://www.inkrit.de/neuinkrit/mediadaten/archivkwm/KWM06.pdf> wd:Q96034314 Band 7 Säuberung bis Trotzkismus 1988 168 3-88619-057-9 3-88619-067-6 880402016 12533236X <http://www.inkrit.de/neuinkrit/mediadaten/archivkwm/KWM07.pdf> wd:Q96035295 Band 8 Überbau bis Zusammenbruchstheorie 1989 214 3-88619-058-7 3-88619-068-4 881428779 125332416 <http://www.inkrit.de/neuinkrit/mediadaten/archivkwm/KWM08.pdf> wd:Q96035325 Would it be possible to get the output as well in this way: Titel Erscheinungsjahr Umfang (Seiten) ISBN-10 DNB K10plus Digitalisat-URL Datensatz Band 1 Abhängigkeit bis Bund 1983 205 ISBN 3-88619-033-1 831087498 125332025 KWM01.pdf Q96026743 Band 2 Cäsarismus bis Funktionär 1984 197 ISBN 3-88619-052-8 ISBN 3-88619-062-5 840398360 125332106 KWM02.pdf Q96031980 Band 3 Gattung bis Judenfrage 1985 184 ISBN 3-88619-053-6 ISBN 3-88619-063-3 850368103 125332157 KWM03.pdf Q96032581 Band 4 Kadetten bis Lyssenkismus 1986 220 ISBN 3-88619-054-4 ISBN 3-88619-064-1 860515966 125332211 KWM04.pdf Q96033164 Band 5 Machismus bis Owenismus 1986 174 ISBN 3-88619-055-2 ISBN 3-88619-065-X 861000641 125332254 KWM05.pdf Q96033337 Band 6 Pariser Kommune bis Romantik 1987 174 ISBN 3-88619-056-0 ISBN 3-88619-066-8 870865528 125332327 KWM06.pdf Q96034314 Band 7 Säuberung bis Trotzkismus 1988 168 ISBN 3-88619-057-9 ISBN 3-88619-067-6 880402016 12533236X KWM07.pdf Q96035295 Band 8 Überbau bis Zusammenbruchstheorie 1989 214 ISBN 3-88619-058-7 ISBN 3-88619-068-4 880402016 125332416 KWM08.pdf Q96035325 En passant: Abhängigkeit, Bund, Cäsarismus, Funktionär, Gattung, Judenfrage, Kadetten, Lyssenkismus, Machismus, Owenismus, Pariser Kommune, Romantik, Säuberung, Trotzkismus, Überbau, Zusammenbruchstheorie. DNB = https://d-nb.info/... K10plus = https://kxp.k10plus.de/DB=2.1/PPNSET?PPN... (Digitalisat-URL = http://www.inkrit.de/neuinkrit/mediadaten/archivkwm/...) [[File:DNB.svg|35px]] [[File:Wikidata-logo-en.svg|30px]] --Nstrc (talk) 08:36, 7 June 2020 (UTC)[reply] published in (P1433) [edit] 2. SELECT ?item ?published_in ?publisher ?place_of_publication
WHERE
{
  ?item wdt:P655 wd:Q92759384.
  ?item (wdt:P361?) / (wdt:P361 | wdt:P1433) ?published_in.
  ?published_in wdt:P123 ?publisher.
  ?published_in wdt:P291 ?place_of_publication.
}
 Try it! Here is three variants combined to one by use of a property path when finding ?published_in. --Dipsacus fullonum (talk) 13:45, 6 June 2020 (UTC)[reply] I have reworked it in this way: SELECT ?AutorLabel ?Titel ?Untertitel ?veroeffentlicht_in ?VerlagLabel ?ErscheinungsortLabel (year(?Erscheinungsdatum) as ?Erscheinungsjahr) WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  {
    ?werk wdt:P655 wd:Q92759384.
    ?werk (wdt:P361?) / (wdt:P361| wdt:P1433) ?published_in.
    ?published_in wdt:P1476 ?veroeffentlicht_in.  
    ?published_in wdt:P123 ?Verlag.
    ?published_in wdt:P291 ?ErscheinungsortLabel.
    ?published_in wdt:P577 ?Erscheinungsdatum.}
  OPTIONAL { ?werk wdt:P50 ?Autor. }
  OPTIONAL { ?werk wdt:P2093 ?Autor. }
  OPTIONAL { ?werk wdt:P1476 ?Titel. }
  OPTIONAL { ?werk wdt:P1680 ?Untertitel. }
}
 Try it! I would rather like to get the name of the town / place of publication displayed (not the Q number of the place). What would I have to change?Would it be possible to determine, that in column 1 \"Autor\" instead of \"AutorLabel\" in column 4 \"veröffentlicht in\" instead of \"veroffentlicht_in\" and in column 5 \"Verlag\" instead of \"VerlagLabel\" is displayed? (However, the content of the lines below should not change.)--Nstrc (talk) 16:38, 6 June 2020 (UTC)[reply] Listeria bot [edit] Thank you very much. Can I use it with listeria bot? Or can't this work, because we need - at least - a second term different than \"item\", while listeria works only with \"item\"? (I tried it, and it didn't work. But I'm not sure, whether I made a mistake or it can't work anyway.) --Nstrc (talk) 14:32, 6 June 2020 (UTC)[reply] I suppose that the queries can used with Listeria bot, but here others have to help. --Dipsacus fullonum (talk) 14:39, 6 June 2020 (UTC)[reply] I have recognised now, that Wikidata Query Servive offers at \"</> code\" lisetria code (among other versions). But also using this code, it does not work for me: using the listeria code from https://w.wiki/TDD results in: User:Nstrc/KWM-Tabelle and https://tools.wmflabs.org/listeria/index.php?action=update&lang=en&page=User:Nstrc/KWM-Tabelle (\"Status: no items\"). substituting within that code \"?Datensatz\" by \"?item\" results in: en:User:Nstrc/KWM-Tabelle - 2. Versuch. Finally, using the listeria code from https://w.wiki/TDT results - even worse - in Tabelle. --Nstrc (talk) 18:56, 6 June 2020 (UTC)[reply] Query for multiple GND claims [edit] Hi, I ask for help for a query, that lists items with multiple GNDs: Only items with a link to de.wikipedia at least two GND ID (P227) the ranks of these GND properties Qualifiers if any Raymond (talk) 08:03, 6 June 2020 (UTC)[reply] This is a bit of a complicated request, so let's start with this simplified one: SELECT ?item (COUNT(DISTINCT ?gnd) AS ?gnd_cnt) (GROUP_CONCAT(?gnd; SEPARATOR=', ') AS ?gnds) ?dewiki_sitelink WITH {
  SELECT DISTINCT ?item WHERE {
    ?item wdt:P227 ?gnd .
  } GROUP BY ?item HAVING(COUNT(DISTINCT ?gnd) > 1)
} AS %subquery WHERE {
  INCLUDE %subquery .
  ?item wdt:P227 ?gnd .
  ?dewiki_sitelink schema:about ?item; schema:isPartOf <https://de.wikipedia.org/> .
} GROUP BY ?item ?dewiki_sitelink
 Try it! It only considers \"best rank\" claims, i.e. all \"normal rank\" claims if no \"preferred rank\" claims are present, all \"preferred rank\" claims only in case there are any such claims, and no \"deprecated rank\" claims at all; in other words: the same data which you see when using the {{#property:P227}} or {{#statements:P227}} parser function in Wikipedia templates. Qualifiers are somewhat difficult to add here, since the GND identifiers are aggregated into a string. Starting from here, should we modify the query somehow to gain further insight? —MisterSynergy (talk) 11:30, 6 June 2020 (UTC)[reply] @Raymond, MisterSynergy: Here is a full version with statements of all ranks and a list of qualifiers as wanted: SELECT ?item ?itemLabel
       ?rank1 (GROUP_CONCAT(DISTINCT ?qual1; SEPARATOR=\", \") AS ?qualifiers1)
       ?rank2 (GROUP_CONCAT(DISTINCT ?qual2; SEPARATOR=\", \") AS ?qualifiers2)
WITH
{
  SELECT ?item
  WHERE
  {
    ?item p:P227 [].
  }
  GROUP BY ?item
  HAVING (COUNT(?item) > 1)
} AS %multiple_GND1
WITH
{
  SELECT ?item ?GND_statement1 ?GND_statement2
  WHERE
  {
    INCLUDE %multiple_GND1
    ?item p:P227 ?GND_statement1.
    ?item p:P227 ?GND_statement2.
    FILTER (?GND_statement1 != ?GND_statement2)
    FILTER (STR(?GND_statement1) < STR(?GND_statement2))
  }
} AS %multiple_GND2
WHERE
{
  INCLUDE %multiple_GND2
  ?sitelink schema:about ?item.
  ?sitelink schema:isPartOf <https://de.wikipedia.org/>.
  ?GND_statement1 wikibase:rank ?rank1.
  ?GND_statement2 wikibase:rank ?rank2.
  OPTIONAL { ?GND_statement1 ?qualifier1 []. ?qualifierproperty1 wikibase:qualifier ?qualifier1. }
  OPTIONAL { ?GND_statement2 ?qualifier2 []. ?qualifierproperty2 wikibase:qualifier ?qualifier2. }
  SERVICE wikibase:label
  {
    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],de\".
    ?item rdfs:label ?itemLabel.
    ?qualifierproperty1 rdfs:label ?qual1.
    ?qualifierproperty2 rdfs:label ?qual2.
  }
}
GROUP BY ?item ?itemLabel ?GND_statement1 ?GND_statement2 ?rank1 ?rank2
 Try it!--Dipsacus fullonum (talk) 11:53, 6 June 2020 (UTC)[reply]@Dipsacus fullonum: Thank you very much, that is what I need. May I ask for 2 refinements? 1. Only items with human (Q5) 2. Add the value of the qualifier Raymond (talk) 12:36, 6 June 2020 (UTC)[reply] @Raymond: No problem. New version is here: SELECT ?item ?itemLabel
       ?rank1
       (GROUP_CONCAT(DISTINCT CONCAT(?qual1, \": \", IF(BOUND(?qvalue1Label), ?qvalue1Label, STR(?qvalue1))); SEPARATOR=\"; \") AS ?qualifiers1)
       ?rank2
       (GROUP_CONCAT(DISTINCT CONCAT(?qual2, \": \", IF(BOUND(?qvalue2Label), ?qvalue2Label, STR(?qvalue2))); SEPARATOR=\"; \") AS ?qualifiers2)
WITH
{
  SELECT ?item
  WHERE
  {
    ?item p:P227 [].
  }
  GROUP BY ?item
  HAVING (COUNT(?item) > 1)
} AS %multiple_GND1
WITH
{
  SELECT ?item ?GND_statement1 ?GND_statement2
  WHERE
  {
    INCLUDE %multiple_GND1
    ?item wdt:P31 wd:Q5. # only humans
    ?item p:P227 ?GND_statement1.
    ?item p:P227 ?GND_statement2.
    FILTER (?GND_statement1 != ?GND_statement2)
    FILTER (STR(?GND_statement1) < STR(?GND_statement2))
  }
} AS %multiple_GND2
WHERE
{
  INCLUDE %multiple_GND2
  ?sitelink schema:about ?item.
  ?sitelink schema:isPartOf <https://de.wikipedia.org/>.
  ?GND_statement1 wikibase:rank ?rank1.
  ?GND_statement2 wikibase:rank ?rank2.
  OPTIONAL { ?GND_statement1 ?qualifier1 ?qvalue1. ?qualifierproperty1 wikibase:qualifier ?qualifier1. }
  OPTIONAL { ?GND_statement2 ?qualifier2 ?qvalue2. ?qualifierproperty2 wikibase:qualifier ?qualifier2. }
  SERVICE wikibase:label
  {
    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],de\".
    ?item rdfs:label ?itemLabel.
    ?qualifierproperty1 rdfs:label ?qual1.
    ?qualifierproperty2 rdfs:label ?qual2.
    ?qvalue1 rdfs:label ?qvalue1Label.
    ?qvalue2 rdfs:label ?qvalue2Label.
  }
}
GROUP BY ?item ?itemLabel ?GND_statement1 ?GND_statement2 ?rank1 ?rank2
 Try it! --Dipsacus fullonum (talk) 14:26, 6 June 2020 (UTC)[reply] Thank you again, Dipsacus fullonum. Works fine. Raymond (talk) 18:42, 7 June 2020 (UTC)[reply] Norwegian persons on norwegian wikipedia not having a reference for their birth-date or death-date [edit] Is it possible to get a list for all norwegians on Norwegian wikipedia not having a reference for their birth or death date? Pmt (talk) 13:59, 7 June 2020 (UTC)[reply] SELECT DISTINCT ?item ?itemLabel ?no 
{ 
  ?item wdt:P27 wd:Q20 ; wdt:P31 wd:Q5 .
  ?no schema:about ?item ; schema:isPartOf <https://no.wikipedia.org/> .
  ?item p:P569 ?claim . 
  OPTIONAL { ?claim prov:wasDerivedFrom ?source . ?source ?p ?v . 
  FILTER NOT EXISTS { ?v wdt:P31 wd:Q10876391 } } 
  FILTER(!bound(?source)) 
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! Above one for birth date. --- Jura 14:08, 7 June 2020 (UTC)[reply] @Jura1: That's a really interesting query! Playing with variants throws up some interesting stats - 41300 items in total, of which 22400 (54%) have no citations for P569 at all, 12600 (31%) have a citation to something other than WP, and the remaining 6300 (15%) presumably are those cited to WP and nothing else. Andrew Gray (talk) 14:34, 7 June 2020 (UTC)[reply] @Jura1: Thank you very much, an very usefull Query. Also @Andrew Gray:, there have been \"Heavy discussions\" included a voting on norwegian Wikipedia where it have been decided that references and other info in Infobox:Biography and other infoboxes shall be taken from Wikidata (max 4), so the hope is that Wikidata birth and death dates will be filled references. in addition occupation (Q12737077) (created by Jura1) will be used together with ISCO-88 occupation class (P952) and Template:P8283 to filter down professions to be used in infoboxes. Breg Pmt (talk) 15:14, 7 June 2020 (UTC)[reply] Strange timeout [edit] Hi all! I've written the following query #URL contains
SELECT ?p ?db
WHERE {
  ?p p:P569 [ps:P569 ?db; prov:wasDerivedFrom [pr:P854 ?site] ].
  MINUS { ?p wdt:P570 ?dd . }
  FILTER(CONTAINS(STR(?site),\"viaf.org/viaf/\"))
}
LIMIT 10
 Try it! which, despite looking simple to me, always times out. Any idea about how to get some result? Thank you very much, --Epìdosis 12:47, 5 June 2020 (UTC)[reply] @Epìdosis: It looks simple, but searches in not-indexed texts are slow, so I'm not particularly surprised that it times out. If you start by selecting items which have links to VIAF by using an API call, you can get some results before the query times out: SELECT ?p ?db
WHERE
{
  SERVICE wikibase:mwapi
  {
    bd:serviceParam wikibase:endpoint \"www.wikidata.org\" .
    bd:serviceParam wikibase:api \"Generator\" .
    bd:serviceParam mwapi:generator \"exturlusage\" .
    bd:serviceParam mwapi:geuprop \"title\" .
    bd:serviceParam mwapi:geunamespace \"0\" .
    bd:serviceParam mwapi:geuprotocol \"https\" .
    bd:serviceParam mwapi:geuquery \"viaf.org/viaf/\" .
    bd:serviceParam mwapi:geulimit \"max\" .
    ?p wikibase:apiOutputItem mwapi:title .
  }
  hint:Prior hint:runFirst \"true\".
  
  ?p p:P569 [ps:P569 ?db; prov:wasDerivedFrom [pr:P854 ?site] ].
  MINUS { ?p wdt:P570 ?dd . }
  FILTER(CONTAINS(STR(?site),\"viaf.org/viaf/\"))
}
 Try it! The API call finds items which contains external https links containing \"viaf.org/viaf/\", but it has a built-in limit to max. 10,000 items. Then these 10,000 items are checked for the other conditions, but because of the limit of 10,000 results from the MWAPI call this query will not give a complete list. --Dipsacus fullonum (talk) 15:44, 5 June 2020 (UTC)[reply] Readaptation [edit] @Dipsacus fullonum: I've readapted your excellent query in this: SELECT ?p ?db
WHERE
{
  SERVICE wikibase:mwapi
  {
    bd:serviceParam wikibase:endpoint \"www.wikidata.org\" .
    bd:serviceParam wikibase:api \"Generator\" .
    bd:serviceParam mwapi:generator \"exturlusage\" .
    bd:serviceParam mwapi:geuprop \"title\" .
    bd:serviceParam mwapi:geunamespace \"0\" .
    bd:serviceParam mwapi:geuprotocol \"https\" .
    bd:serviceParam mwapi:geuquery \"viaf.org/viaf/\" .
    bd:serviceParam mwapi:geulimit \"max\" .
    ?p wikibase:apiOutputItem mwapi:title .
  }
  hint:Prior hint:runFirst \"true\".
  
  ?p p:P569 [ps:P569 ?db ; prov:wasDerivedFrom [pr:P854 ?site] ].
  FILTER(\"1950-00-00\"^^xsd:dateTime = ?db)
  FILTER(CONTAINS(STR(?site),\"viaf.org/viaf/\"))
}
 Try it! Now I would need two further readapaptations: include all the dates of birth having at least a reference containing reference URL (P854) - I think it suffices removing the last FILTER include not only people born in 1950, but all people born in years ending with 50 (e.g. 450 BCE, 50 BCE, 50 AD, 150 AD, 1450 AD ...) - I don't know how to do this, could you help me? Thank you very much as always, --Epìdosis 08:43, 8 June 2020 (UTC)[reply] Here is a query for all years ending in 50. I also added the precision for the date (11=date, 10=month, 9=year, 8=decade, 7=century). The list still is a sample because of the limit of results for use of MWAPI. SELECT ?p ?db ?precision
WHERE
{
  SERVICE wikibase:mwapi
  {
    bd:serviceParam wikibase:endpoint \"www.wikidata.org\" .
    bd:serviceParam wikibase:api \"Generator\" .
    bd:serviceParam mwapi:generator \"exturlusage\" .
    bd:serviceParam mwapi:geuprop \"title\" .
    bd:serviceParam mwapi:geunamespace \"0\" .
    bd:serviceParam mwapi:geuprotocol \"https\" .
    bd:serviceParam mwapi:geuquery \"viaf.org/viaf/\" .
    bd:serviceParam mwapi:geulimit \"max\" .
    ?p wikibase:apiOutputItem mwapi:title .
  }
  hint:Prior hint:runFirst \"true\".
  
  ?p p:P569 [psv:P569 ?dbv ; prov:wasDerivedFrom [pr:P854 ?site] ].
  FILTER CONTAINS(STR(?site),\"viaf.org/viaf/\")
  ?dbv wikibase:timeValue ?db; wikibase:timePrecision ?precision.
  BIND (YEAR(?db) AS ?year)
  FILTER IF(?year > 0,
            ?year - FLOOR(?year / 100) * 100 = 50, # year is AD
            ?year - FLOOR(?year / 100) * 100 = 51) # year is BC, 1 BC is encoded as \"0\", 2 BC as \"-1\" etc.
}
 Try it!--Dipsacus fullonum (talk) 12:25, 8 June 2020 (UTC)[reply]Graph asked : a line per year with months [edit] Hello, with Tokyo International Airport (Q204853), I'd like to see the 2018,2019,2020 patronage (P3872) monthly statistics, all these 12 months being put into one colored line per year. Would it be possible ? Thanks ! Bouzinac (talk) 14:10, 6 June 2020 (UTC)[reply] Manually update chart WDQS | PetScan | TABernacle | Find images | Recent changes Graphs are temporarily unavailable due to technical issues. End of automatically generated list. @Bouzinac: as above? --- Jura 10:02, 7 June 2020 (UTC)[reply] Salut Jura1, hum, je ne sais pas trop car là tu as montré un graph annuel. Il y a en fait deux soucis: Les valeurs annuelles devraient correspondre à par exemple 85 millions pour l'année 2017 (comme il y a de multiples sources, il ne faut pas faire de sommes par années)==> https://w.wiki/THi . S'il était possible d'avoir cette dernière requête en graph visuel affichable côté wikipédia:fr, ce serait super! Les valeurs mensuelles que je voulais dans ma demande étaient trois courbes, une 2018, une 2019, une 2020, avec janvier, février, mars etc. L'idée était de voir visuellement la chute du trafic aérien sur mars 2020. Bouzinac (talk) 12:25, 7 June 2020 (UTC)[reply] @Bouzinac: j'ai modifié. Pour d'autres aéroports, il faudrait probablement plus de vérifications pour s'assurer que l'on a qu'une donnée par mois. ça devrait fonctionner également sur frwiki, si on y fait les mêmes modifications que j'ai faites à Module:Graph pour les variables en minuscules (problème de Listeria). --- Jura 13:31, 7 June 2020 (UTC)[reply]  Graphs are temporarily unavailable due to technical issues. Oh. peut-être plutôt ainsi. --- Jura 14:03, 7 June 2020 (UTC)[reply] @Bouzinac, Jura1: I suppose it may also be possible to make a similar linechart using a SPARQL query. But the documentation for that at Wikidata:SPARQL query service/Wikidata Query Help/Result Views is so sparse that I haven't been able to do it. My best try is: #defaultView:LineChart
SELECT DISTINCT ?month ?patronage ?year 
WHERE
{
  wd:Q204853 p:P3872 ?statement.
  ?statement ps:P3872 ?patronage.
  ?statement pqv:P585 ?timevalue.
  ?timevalue wikibase:timeValue ?time.
  ?timevalue wikibase:timePrecision 10 . # Precsion month (10)
  BIND (STR(MONTH(?time)) AS ?month)
  BIND (STR(YEAR(?time)) AS ?year)
}
 Try it! but the graph has the months in a wrong sequence and I haven't been able to find out how to change it. --Dipsacus fullonum (talk) 14:14, 7 June 2020 (UTC)[reply] I think there is problem if it isn't string or one is using a browser different from the one of the developer. --- Jura 14:16, 7 June 2020 (UTC)[reply] Yeah, nice try with the hard-coded values, seems to work. However still trouble with direct query. There is a current [task] about this graph problem (they are removing the graphoid (static image) to enable direct Vega rendering graph. Bouzinac (talk) 20:08, 7 June 2020 (UTC)[reply] https://www.wikidata.org/wiki/Module:Graph#Test_2-values_with_direct_query_at_Wikidata – The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] • contribs).  Manually update chart WDQS | PetScan | TABernacle | Find images | Recent changes Graphs are temporarily unavailable due to technical issues. End of automatically generated list. Here a dynamic version. Template_talk:Graph:Chart has more samples. --- Jura 05:16, 8 June 2020 (UTC)[reply] Listado de las organizaciones cubanas [edit] Cómo obtener un listado de las organizaciones cubanas con los campos principales, incluyendo los identificadores persistentes q esten registrados This query finds all Cuban organizations in Wikidata which can be tricky to do effectively. I don't know which information you want displayed for each organization so just a label is displayed now. More can be added. SELECT ?item ?itemLabel
WHERE
{
  {
    SELECT DISTINCT ?item
    WHERE
    {
      ?item wdt:P17 wd:Q241 . # ?item is Cuban
      hint:Prior hint:runFirst \"true\". # There are much fewer Cuban items than organizations, but the optimizer cannot see that.

      ?item wdt:P31 ?instance.
      ?instance wdt:P279* wd:Q43229 . # ?item is an organisation
      hint:Prior hint:gearing \"forward\". # Check if Cuban items are organizations rather than 
                                         # finding all organizations and check if they are Cuban
    }
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],es,en\". }
}
 Try it! --Dipsacus fullonum (talk) 20:23, 8 June 2020 (UTC)[reply]  Image grid based on multiple image properties [edit] Items like Nigeria (Q1033) have multiple image-related properties, and I am trying to build an image grid based on that, for which I have the following query: The following query uses these:Items: Nigeria (Q1033)   , Wikidata property to link to Commons (Q18610173)   , Wikidata property for linking to a representative image (Q26940804)   Properties: instance of (P31)    Features: ImageGrid (Q24515278)  #defaultView:ImageGrid
SELECT * WHERE {
  VALUES ?item {wd:Q1033}
  ?property wdt:P31 wd:Q18610173 .
  ?property wdt:P31 wd:Q26940804 .
  
  ?property a wikibase:Property;
              wikibase:statementValue ?psv ;
              wikibase:claim ?p.

  ?item ?p ?Image1 .

#  ?item ?property ?Image2 . # no results
#  ?item [wdt:P31 wd:Q18610173] ?Image3 . # syntax error
#  ?item ?psv ?Image4 . # no results
#  ?Image1 ?psv ?Image5 . # no results
} Try it! This does not actually show the image grid, although it gives the expected number of results in Table view (except perhaps for flag image (P41), where I would prefer to get only the current image). Some of the other things I tried are commented out at the bottom. I'd welcome any pointers on what I am missing. --Daniel Mietchen (talk) 21:34, 8 June 2020 (UTC)[reply] @Daniel Mietchen: ?Image1 is a wrong type. It is a statement where it should have been a Commons Media. Change wikibase:claim to wikibase:directClaim. --Dipsacus fullonum (talk) 22:00, 8 June 2020 (UTC)[reply] @Dipsacus fullonum: Thanks — yes, that was it! --Daniel Mietchen (talk) 22:28, 8 June 2020 (UTC)[reply] Town that have changed of name in the past [edit] Hello, I'd like to list towns/cities that have changed at least 3 times in the past, with their dates ? See for instance La Roche-sur-Yon (Q190118) with its official name (P1448) Bouzinac (talk) 20:56, 7 June 2020 (UTC)[reply] @Bouzinac: This query will list cities etc. with more than 3 officiel names. It doesn't distinguish between names at different times and names in different languages at the same time. SELECT ?item ?name ?language_tag ?rank ?start ?end
WITH
{
  # Select items with more than 3 official names, i.e. it has changed at least 3 times.
  SELECT ?item
  WHERE
  {
    ?item p:P1448 []. # Officiel name
  }
  GROUP BY ?item
  HAVING (COUNT(?item) > 3)
} AS %multiple_names
WHERE
{
  INCLUDE %multiple_names
  ?item wdt:P31 / wdt:P279 wd:Q486972. # ?item is a settelement
  ?item p:P1448 ?name_statement.
  ?name_statement wikibase:rank ?rank.
  ?name_statement ps:P1448 ?name.
  BIND (LANG(?name) AS ?language_tag).
  OPTIONAL { ?name_statement pq:P580 ?start. }
  OPTIONAL { ?name_statement pq:P582 ?end. }
}
ORDER By ?item ?start
 Try it!--Dipsacus fullonum (talk) 07:12, 8 June 2020 (UTC)[reply]Nice, thanks! How would I filter only those written in lang:fr ? Bouzinac (talk) 08:38, 8 June 2020 (UTC)[reply] @Bouzinac: Insert FILTER(?language_tag = \"fr\") in the query for filter for \"fr\" as language tag. Insert FILTER LANGMATCHES(?language_tag, \"fr\") to filter for French language. The latter will include language tags with subtags. I don't think French tags with subtags are used on Wikidata, but there are for other languages. --Dipsacus fullonum (talk) 09:48, 8 June 2020 (UTC)[reply] Well it gives false positives such as Liège (Q3992) which has never changed of name (but does have many international names) https://w.wiki/TNa Bouzinac (talk) 11:16, 8 June 2020 (UTC)[reply] @Bouzinac: This version only includes cities which have 4 or more French names. It also includes values for the qualifiers point in time (P585) and epoch (P6259). SELECT ?item ?name ?language_tag ?rank ?epochLabel ?start ?point_of_time ?end
WITH
{
  # Select items with more than 3 official names, i.e. it has changed at least 3 times.
  SELECT ?item
  WHERE
  {
    ?item p:P1448 []. # Official name
  }
  GROUP BY ?item
  HAVING (COUNT(?item) > 3)
} AS %multiple_names
WITH
{
  SELECT ?item
  WHERE
  {
    INCLUDE %multiple_names
    ?item p:P1448 / ps:P1448 ?name.
    FILTER LANGMATCHES(LANG(?name), \"fr\")
  }
  GROUP BY ?item
  HAVING (COUNT(?item) > 3)
} AS %multiple_French_names
WHERE
{
  INCLUDE %multiple_French_names
  ?item wdt:P31 / wdt:P279 wd:Q486972. # ?item is a settelement
  ?item p:P1448 ?name_statement.
  ?name_statement wikibase:rank ?rank.
  ?name_statement ps:P1448 ?name.
  BIND (LANG(?name) AS ?language_tag).
  OPTIONAL { ?name_statement pq:P6259 ?epoch. }
  OPTIONAL { ?name_statement pq:P580 ?start. }
  OPTIONAL { ?name_statement pq:P585 ?point_of_time. }
  OPTIONAL { ?name_statement pq:P582 ?end. }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr,en\". }
}
ORDER By ?item ?start ?point_of_time ?end
 Try it! --Dipsacus fullonum (talk) 11:40, 8 June 2020 (UTC)[reply] Please use common class for towns and cities - city or town (Q7930989). --Infovarius (talk) 19:18, 9 June 2020 (UTC)[reply] Hi Dipsacus fullonum, I've updated some French old city names, especially those from French 1789 revolution. I've got two questions for you in the having count clauses here, could you answer their specific use ? https://w.wiki/TfP Thanks ! Bouzinac (talk) 19:32, 10 June 2020 (UTC)[reply] @Bouzinac: \"HAVING\" is a keyword for a filter that operates over a grouped solution set. The first subquery named \"%multiple_names\" finds all entities which have more than 3 statements with official name (P1448) as predicate. It works by finding all such statements, group the statements by the their subject, count the number of equal subjects in each group, and then apply the filter so only entities with a count greater than 3 is preserved. The purpose is to drastically reduce the result set, so the next part of the query can work faster by having a much smaller set of entities to work on. It would not alter the result to remove that subquery, but the query would be slower. It would be faster to change the filter condition to \"HAVING (COUNT(?item) > 8)\" so the result set is reduced even more. The second subquery named \"%multiple_French_names\" select the entities which have more than 8 statements with P1448 with a value in French. It works by finding all statements with P1448 and a value in French, group these statements by their subject, count the number of equal subjects in each group and then apply the filter so only entities with a count greater than 8 is preserved. It would do the same if \"INCLUDE %multiple_names\" and the first subquery were omitted, but work slower because it would have to find the language of all values of P1448 instead of using the limited result set of the first subquery. I hope that answers your questions. --Dipsacus fullonum (talk) 22:29, 11 June 2020 (UTC)[reply] Show graph elements by property [edit] I would like to show all bridges across a river in a graph *in the order in which they appear* on the river (order by upstream/downstream). Right now I have the following query #All bridges on Thur with upstream/downstream bridge
#defaultView:Graph
PREFIX gas: <http://www.bigdata.com/rdf/gas#>
SELECT ?item ?itemLabel ?linkTo ?pic ?depth
WHERE 
{
    SERVICE gas:service {
    gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.SSSP\" ;
                gas:in wd:Q96108174 ; ## This is my work-around
                #gas:in wdt:P177 wd:Q14339 ; ## This is what I would want!
                gas:traversalDirection \"Forward\" ;
                gas:out ?item ;
                gas:maxVisited 150;
                gas:out1 ?depth ;
                gas:linkType wdt:P2674
  }
  OPTIONAL { ?item wdt:P2674 ?linkTo }
  OPTIONAL { ?item wdt:P18 ?pic }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
ORDER BY ?depth
 Try it! however I have two issues here: this requires me to know the starting point (here Chlostobelstrasse-Brücke (Q96108174) but I would much prefer to only write wdt:P177 wd:Q14339 (all bridges that cross my river of interest). This is because I may not know the start/end point in advance. Is that possible or do I *need* to specify the starting point? Eg is it possible to have the engine choose an arbitrary starting point and then walk bi-directionally? [It may be possible to formulate a sub-query to \"walk to the end\" and then once the end point is found, use that in the real query as start point] can I make the graph view default to \"hierarchical layout top-down\" instead of the non-hierarchical layout that is currently chosen by default? Finally I have some issues to display the images on the graph nodes, often I just see empty nodes, it seems to work sometimes but not reliably - does somebody know what is going on? let me know if you have any other improvements/ideasBest --Hannes Röst (talk) 21:02, 9 June 2020 (UTC)[reply] Hi Hannes Röst. If all bridges which crosses Thur had statements for next crossing upstream (P2673) and next crossing downstream (P2674) you could find the most upstream bridge as the only one with a novalue special value for P2673 (or maybe without any statement for P2673 at all if novalue wasn't used). But that wouldn't help with the GAS program because GAS programs cannot have variables as input, only constants. Another point is that there are 14 bridges which crosses Thur without statements for P2673 and P2674. Where would you place them in the graph? --Dipsacus fullonum (talk) 08:13, 10 June 2020 (UTC)[reply] Thanks for the suggestion, this may indeed work under some circumstances but not always since the last bridge on the river Thur is still downstream from other bridges of the two river that flow together to make the river Thur (Wildhauser Thur (Q89288705) and Säntisthur (Q69099248)) so this will not always work unless the river is one that goes all the way up to the source. Of course one could argue that the relationship upstream/downstream should not be set since these are rivers with different names but in the general usage of the term the one bridge would still be downstream from the other. --Hannes Röst (talk) 13:27, 10 June 2020 (UTC)[reply] Maybe linear reference (P6710) could be set? --- Jura 08:24, 10 June 2020 (UTC)[reply]What’s wrong with#All bridges on Thur with upstream/downstream bridge
#defaultView:Graph
PREFIX gas: <http://www.bigdata.com/rdf/gas#>
SELECT ?item ?itemLabel ?linkTo ?pic ?depth
WHERE 
{
  OPTIONAL { ?item wdt:P2674 ?linkTo }
  OPTIONAL { ?item wdt:P18 ?pic }
  ?item wdt:P177 wd:Q14339 .
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it!? Does not need a starting point. author TomT0m / talk page 09:30, 10 June 2020 (UTC)[reply]Is there are way to get it into table form (for Listeria)? (e.g. as when clicking on the icon \"hierarchical layout up to down\") --- Jura 10:25, 10 June 2020 (UTC)[reply] #defaultView:Graph
SELECT ?item ?itemLabel ?pic ?count ?coor ?linkTo
WITH
{
  SELECT ?item (COUNT(DISTINCT ?down) as ?count)
  {  ?item wdt:P177 wd:Q14339 ; wdt:P2674* ?down } 
  GROUP BY ?item
  ORDER BY DESC(?count) ?item
} as %ct
WHERE 
{
  INCLUDE %ct
  ?item wdt:P177 wd:Q14339 .
  OPTIONAL { ?item wdt:P18 ?pic }
  OPTIONAL { ?item wdt:P625 ?coor }
  OPTIONAL { ?item wdt:P2674 ?linkTo }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
ORDER BY DESC(?count) ?item
 Try it! Something like the above maybe --- Jura 11:43, 10 June 2020 (UTC)[reply] Great! I added the \"?linkTo\" and now it works both as graph and as table. Now its quite easy to find issues in wikidata (missing bridges, self-links etc). Great, this helps a lot! --Hannes Röst (talk) 14:42, 10 June 2020 (UTC)[reply] Items used: River Thames (Q19686)   , river source (Q7376362)   , London Bridge (Q1868889)   Properties used: crosses (P177)   , next crossing downstream (P2674)   , image (P18)   , coordinate location (P625)   , applies to part (P518)   """@en;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Request_a_query/Archive/2020/06>,
    <https://www.wikidata.org//wiki/Wikidata:WikiProject_Rivers>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:genid-4e694113159d4e3db4a1a913894a81d861901-wikidata_prefixes, _:genid-4e694113159d4e3db4a1a913894a81d861901-566A42A9642A4A79E887A91E95D2BC8E;
  schema:target <https://query.wikidata.org/sparql/>;
  sh:select """PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX ps: <http://www.wikidata.org/prop/statement/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX bd: <http://www.bigdata.com/rdf#>
SELECT ?item ?itemLabel ?pic ?count ?coor ?dist (ROUND(?dist/10) as ?layer)
WITH
{
  SELECT ?item (COUNT(DISTINCT ?down) as ?count)
  {  ?item wdt:P177 wd:Q19686 ; wdt:P2674* ?down } 
  GROUP BY ?item
  ORDER BY DESC(?count) ?item
} as %ct
WHERE 
{
  INCLUDE %ct
  ?item wdt:P177 wd:Q19686 .
  OPTIONAL { ?item wdt:P18 ?pic }
  wd:Q19686 p:P625 [ ps:P625 ?coor_orig ; pq:P518 wd:Q7376362 ] .
  OPTIONAL { ?item wdt:P625 ?coor }
  FILTER ( ?item != wd:Q1868889 )
  BIND(ROUND(geof:distance(?coor_orig, ?coor)) as ?dist)
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
ORDER BY DESC(?count) DESC(?dist) ?item""" .
