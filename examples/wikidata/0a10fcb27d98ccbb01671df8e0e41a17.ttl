@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-0a10fcb27d98ccbb01671df8e0e41a17> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Request_a_query/Archive/2020/05>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:genid-4e694113159d4e3db4a1a913894a81d817867-wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/>;
  sh:select """PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX bd: <http://www.bigdata.com/rdf#>
SELECT ?occupation ?occupationLabel ?count ?males ?females

WHERE
{
  {
  SELECT ?occupation (COUNT(?item) AS ?count) (SUM(?male) AS ?males) (SUM(?female) AS ?females)
  WHERE
  {
    ?item wdt:P31 wd:Q5. # item is human
    ?item wdt:P106 ?occupation.
    ?occupation wdt:P279* wd:Q901. # occupation is subclass of scientist
    OPTIONAL { ?item wdt:P570 ?date_of_death. }
    FILTER (! BOUND(?date_of_death)) # No date of death meaning only living people
    OPTIONAL { ?item wdt:P21 ?gender. }
    BIND (IF (?gender = wd:Q6581097, 1, 0) AS ?male)
    BIND (IF (?gender = wd:Q6581072, 1, 0) AS ?female)
  }
  GROUP BY ?occupation
}
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}""";
  <http://example.org/bigdata_select> """SELECT ?occupation ?occupationLabel ?count ?males ?females
WITH
{
  SELECT ?occupation (COUNT(?item) AS ?count) (SUM(?male) AS ?males) (SUM(?female) AS ?females)
  WHERE
  {
    ?item wdt:P31 wd:Q5. # item is human
    ?item wdt:P106 ?occupation.
    ?occupation wdt:P279* wd:Q901. # occupation is subclass of scientist
    OPTIONAL { ?item wdt:P570 ?date_of_death. }
    FILTER (! BOUND(?date_of_death)) # No date of death meaning only living people
    OPTIONAL { ?item wdt:P21 ?gender. }
    BIND (IF (?gender = wd:Q6581097, 1, 0) AS ?male)
    BIND (IF (?gender = wd:Q6581072, 1, 0) AS ?female)
  }
  GROUP BY ?occupation
} AS %query
WHERE
{
  INCLUDE %query
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}""" .
