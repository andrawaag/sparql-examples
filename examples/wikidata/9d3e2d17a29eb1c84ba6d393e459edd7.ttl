@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-9d3e2d17a29eb1c84ba6d393e459edd7> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Request_a_query/Archive/2022/08>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:genid-4e694113159d4e3db4a1a913894a81d86095-wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/>;
  sh:select """PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX bd: <http://www.bigdata.com/rdf#>
SELECT ?item ?itemLabel ?coord ?item2 ?item2Label ?coord2 ?dist
WITH
{
  SELECT DISTINCT ?item (SAMPLE(?coord_) AS ?coord)
  {
    ?item wdt:P31 / wdt:P279 * wd:Q12323 . # Instance of Dam or subclass
    ?item wdt:P17 wd:Q17 . # In Japan
    ?item wdt:P625 ?coord_ .
    BIND (geof:longitude(?coord_) AS ?long)
    FILTER (?long > 136)
    FILTER (?long < 138)
  }
  GROUP BY ?item
} AS %items
WITH
{
  SELECT ?item ?coord ?item2 ?coord2 ?dist
  {
    INCLUDE %items
    {
      SELECT (?item AS ?item2) (?coord AS ?coord2) { INCLUDE %items }
    }
    FILTER (STR(?item) < STR(?item2))
    BIND(geof:distance(?coord, ?coord2) as ?dist)
    FILTER (?dist <= 10)
  }
} AS %dists
{
  INCLUDE %dists
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,ja\" }
}
ORDER BY ?dist""" .
