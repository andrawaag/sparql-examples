@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-20fcdb3a33e6d89ae38cad9ba16705bc> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  rdfs:comment """ This page is an archive. Please do not modify it. Use the current page, even to continue an old discussion. Contents 1 Query multiple given names 2 Searching for p31=Q13442814 with title that begin with \"Erratum\" that have no P31=Q1348305 3 How to get data from wikidata using the QID 4 How to add subject to this query 5 A list of OSM wiki pages without links to Wikidata 6 timeout 7 License 8 Find an author who created an item 9 Finding items that does not have a frwiki 10 Most statements, instance of (P31) = human (Q5), no en wiki 11 Nigerian women 12 Combined age of podium F1 13 Ordering items in dimensions view 14 How to include Reference URL for Wikidata SPARQL queries? 15 Beautify this query 16 Persons from a certain time period and country 17 SPARQL query to get wikipedia sitelink to article by wikipedia item id in Wikidata 18 I want to exclude items from a query if an 'instance of' claim is included in a VALUES list. 19 Help for freeing data from wikipedia to wikidata 20 Searching for people with a residence within X km from a given location 21 Bilingual place name sign (P1766) 22 Male tennis players who won a Grand Slam singles title 23 Timing out 24 Wikimedia Commons Query Service 25 Is there a way to get just the QIDs and not their respective URL? 26 Erdos number 27 Get Wikipedia URLs (sitelinks) in Wikidata SPARQL query 28 Is it possible to have an array or list of values for a column? 29 Scholarly articles with images on Commons 30 Several query requests in one 31 Last names for Norwegian citizens 32 Get all qualifiers and unit of a property 33 Request to get a list of all the paintings and all the properties of the painting. 34 Last wovel for Italians 35 SPARQL request leads to JSONDecodeError once the requested timeframe is expanded 36 IMDB people with no English label 37 querying for information about a given item 38 Male artists active in the 20th century 39 Cebuano filter for Wikishootme 40 Getting all statements pointing to a specific item 41 Breadcrumbs for a class 42 Check for duplicates of evolutionary lines 43 Consistency of evolutionary lines 44 All sources referenced as \"stated in\" for \"cites work\" property 45 cited articles that have been retracted 46 cited articles that have an erratum 47 Mountains in M√∏re og Romsdal 48 Pok√©list 49 Biodiversity quantification 50 Items having both country (P17) : Republic of Artsakh (Q244165) and Azerbaijan (Q227) 51 List of conflicts in Canada 52 One time list needed 53 Commons Query Service-Missing Labels and descriptions 54 All settlement names and wikipedia articles in Hebrew and Yiddish 55 Shortest and longest wars Query multiple given names Hi there! I am new to SPARQL. So I am sorry for the case, I am asking a stupid question: How do I query multiple givenNames of a person? I tried something like this:SELECT ?human ?humanLabel ?familyNameLabel ?givenNameLabel ?givenName2Label ?birthName ?countryOfCitizenShipLabel WHERE { ?human wdt:P31 wd:Q5;
 OPTIONAL { ?human wdt:P734 ?familyName. }
 OPTIONAL { ?human wdt:P735 ?givenName. }
 OPTIONAL { ?human wdt:P735 ?givenName2. }
 OPTIONAL { ?human wdt:P1477 ?birthName. }
 OPTIONAL { ?human wdt:P27 ?countryOfCitizenShip. }
 SERVICE wikibase:label { bd:serviceParam wikibase:language \"de\". } }Do you have an idea, how to select a second or third given name? Or even better: How to query for any given name. Thanks in advance! You don't need to use ?givenName2, you'll get all given names in ?givenName. Or do you mean series ordinal (P1545)? If you want specific name, just use wd:Qnnn instead of variable. --Infovarius (talk) 21:04, 1 September 2020 (UTC) Searching for p31=Q13442814 with title that begin with \"Erratum\" that have no P31=Q1348305 I hope to run a search for instance of (P31)=scholarly article (Q13442814) with that have (en) title (P1476) that begin with the word \"Erratum\" but also have no instance of (P31)=erratum (Q1348305). Hope this isn't a difficult one (I've never been disappointed with the support found here. Thank you all, Trilotat (talk) 23:30, 31 August 2020 (UTC) @Trilotat: This query works for items with a label equal to the title of the article. That is the case in all the cases that I havd seen. SELECT DISTINCT ?item ?title
WHERE
{
  SERVICE wikibase:mwapi
  {
    bd:serviceParam wikibase:endpoint \"www.wikidata.org\".
    bd:serviceParam wikibase:api \"Generator\".
    bd:serviceParam mwapi:generator \"search\".
    bd:serviceParam mwapi:gsrsearch \"haswbstatement:P31=Q13442814 -haswbstatement:P31=Q1348305 Erratum\".
    bd:serviceParam mwapi:gsrlimit \"max\".
    ?item wikibase:apiOutputItem mwapi:title.
  }
  ?item wdt:P1476 ?title.
  FILTER STRSTARTS(?title, \"Erratum\")
}
 Try it! --Dipsacus fullonum (talk) 00:41, 1 September 2020 (UTC) Thanks! This has given me more to do. That's a good thing, particularly now. I appreciate it. Trilotat (talk) 14:28, 1 September 2020 (UTC) How to get data from wikidata using the QID I wanted to know how can I get data using QID. I have some names, I use falcon 2.0 entity linker curl command( change it into python script) to get information of its QID. Now I want to use that QID to get the persons gender( male or female) or alias or other information. Can someone give an idea how can it be done. The code to get QID is given below. the link to falcon 2.0 is https://github.com/SDM-TIB/Falcon2.0.import requests import jsonresponse_list=[] person_names=[]if __name__ == '__main__': limit=100
   with open(filename, 'r') as in_file:
       in_reader = in_file.readlines()
       for data in in_reader:
           if limit > 0:
               person_names.append(data.rstrip()) 
               limit -=1
           else¬†:
               break
               
               
   \"\"\"
   Url of post request and header of type json create linking against each line of text.
   \"\"\"
   
   
   url=\"https://labs.tib.eu/falcon/falcon2/api?mode=long\"
   headers = {'Content-type': 'application/json'}
   for name in person_names:
       data = {\"text\":name }
       data_json = json.dumps(data)
       response = requests.post(url, data=data_json, headers=headers)
       print(response.content) It gives output as http://www.wikidata.org/entity/Q42493 for the entity. ‚Äì The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] ‚Ä¢ contribs). Maybe Wikidata:Data_access would be a nice intro for you. --- Jura 17:56, 1 September 2020 (UTC) How to add subject to this query I like this query. I don't remember who was kind enough to create it for me. I'd like to use it to search if appropriate main subject (P921) has been added to the items. How do I add subject in a way that returns results? Thank you. Trilotat (talk) 18:15, 1 September 2020 (UTC) SELECT DISTINCT ?item ?title ?doi
WHERE {
  hint:Query hint:optimizer \"None\".
{  SERVICE wikibase:mwapi {
    bd:serviceParam wikibase:api \"Search\";
                    wikibase:endpoint \"www.wikidata.org\";
                    mwapi:srsearch \"\\\"california\\\" haswbstatement:P31=Q13442814\".
    ?page_title wikibase:apiOutput mwapi:title.
  } }
  
  BIND(IRI(CONCAT(STR(wd:), ?page_title)) AS ?item)
  
  ?item wdt:P31 wd:Q13442814;
        wdt:P1476 ?title;
        wdt:P356 ?doi.
  FILTER CONTAINS(LCASE(?title), \"california\").
  ?item wdt:P356 ?doi . FILTER(STRSTARTS(?doi,'10.1785'))
}
 Try it! Hi Trilotat. The only real change is that I added OPTIONAL { ?item wdt:P921 ?subject. } and the label service for ?subjectLabel, but besides that I also did several optimizations: Get the item directly from the MWAPI call instead of first ?page_title and then doing a manual conversion to ?item Don't get ?doi twice Remove the triple ?item wdt:P31 wd:Q13442814 from the SPARQL code because the MWAPI search already checks that condition. (Well, it doesn't check for rank. If that is important, reinsert the triple.) Don't turn the built-in optimizer off but instead move the MWAPI call to a named subquery to ensure that it is executed first SELECT  ?item ?title ?subject ?subjectLabel ?doi
WITH
{
  SELECT ?item
  WHERE
  {
    SERVICE wikibase:mwapi {
      bd:serviceParam wikibase:api \"Search\";
                      wikibase:endpoint \"www.wikidata.org\";
                      mwapi:srsearch \"\\\"california\\\" haswbstatement:P31=Q13442814\".
      ?item wikibase:apiOutputItem mwapi:title.
    }
  }
} AS %mwapi
WHERE {
  INCLUDE %mwapi
  ?item wdt:P1476 ?title;
        wdt:P356 ?doi.
  FILTER CONTAINS(LCASE(?title), \"california\").
  FILTER STRSTARTS(?doi,'10.1785')
  OPTIONAL { ?item wdt:P921 ?subject. }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Dipsacus fullonum (talk) 19:20, 1 September 2020 (UTC) Awesome. I didn't understand half of what you said. Okay, less than half. Thanks, though! A list of OSM wiki pages without links to Wikidata I want to create a mix'n'match catalog with OpenStreetMap Wiki (Q18635431) pages for tags, keys and relations that are still not yet connected to Wikidata. Using Sophox (Q55840137) instead of WDQS, I need, in this order: Entry ID (OSM wiki page URL title) Entry name (OSM wikibase item label) Entry description (OSM wikibase item description) At an earlier stage, @Yurik suggested this query, but I'm having trouble adapting it to what I need for mix'n'match. I apologize in advance for asking this here as I know Sophox is slightly different from usual pure WDQS queries, but I have few other options at this time. NMaia (talk) 01:19, 28 August 2020 (UTC) For future reference, this was done, courtesy of @CamelCaseNick. Query (and query without items that have duplicates). NMaia (talk) 11:52, 3 September 2020 (UTC) timeout Dear fellows, I keep receiving timeout. Is there any solution? Items used: human (Q5) ‚ÄØ Properties used: ORCID iD (P496) ‚ÄØ , occupation (P106) ‚ÄØ , instance of (P31) ‚ÄØ SELECT ?item 
WHERE
{
  ?item wdt:P496 ?reseachID .
  FILTER NOT EXISTS { ?item wdt:P106 [] }
  ?item wdt:P31 wd:Q5. 
}
LIMIT 100 Try it!Luckyz (talk) 09:48, 30 August 2020 (UTC) Would a search query be a solution for you? --Matƒõj Such√°nek (talk) 10:02, 2 September 2020 (UTC) On another look, I didn't receive a timeout with MINUS { ... }. --Matƒõj Such√°nek (talk) 10:04, 2 September 2020 (UTC) What do you mean with minus? Sorry for being newbie. Luckyz (talk) 10:47, 2 September 2020 (UTC) Items used: human (Q5) ‚ÄØ Properties used: ORCID iD (P496) ‚ÄØ , occupation (P106) ‚ÄØ , instance of (P31) ‚ÄØ SELECT ?item 
WHERE
{
  ?item wdt:P496 ?reseachID .
  MINUS{ ?item wdt:P106 [] }
  ?item wdt:P31 wd:Q5. 
}
LIMIT 100 Try it!? Luckyz (talk) 10:47, 2 September 2020 (UTC) Exactly. Sorry for being too brief. --Matƒõj Such√°nek (talk) 12:56, 3 September 2020 (UTC) License Could someone here make a query of instances/subclasses of video games that have multiple copyright license (P275) with one of them being either freeware (Q178285) or free-to-play (Q1414510)? Thanks! --Trade (talk) 07:34, 3 September 2020 (UTC) @Trade: SELECT ?item ?itemLabel ?free_license ?free_licenseLabel ?other_license ?other_licenseLabel
WHERE
{
  VALUES ?free_license { wd:Q178285 wd:Q1414510 }
  ?item wdt:P31 / wdt:P279 * wd:Q7889 . # ?item is instance of video game
  ?item wdt:P275 ?free_license .
  ?item wdt:P275 ?other_license .
  FILTER (?free_license != ?other_license)
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Dipsacus fullonum (talk) 12:03, 3 September 2020 (UTC) Find an author who created an item Hello everyone,Is it possible to find an author who created a Wikidata item with a SPARQL query?Thanks in advance! Yes: SELECT ?item ?creator ?timestamp ?edit_comment
WHERE
{
  VALUES ?titles { \"Q42\" \"Q2013\" \"Q1000000\" }
  SERVICE wikibase:mwapi
  {
    bd:serviceParam wikibase:endpoint \"www.wikidata.org\" .
    bd:serviceParam wikibase:api \"Generator\" .
    bd:serviceParam mwapi:generator \"allpages\" .
    bd:serviceParam mwapi:gapfrom ?titles .
    bd:serviceParam mwapi:gapto ?titles .
    bd:serviceParam mwapi:prop \"revisions\" .
    bd:serviceParam mwapi:rvprop \"user|timestamp|comment\" .
    bd:serviceParam mwapi:rvdir \"newer\" .
    bd:serviceParam mwapi:rvlimit \"1\" .
    ?item wikibase:apiOutputItem mwapi:title .
    ?creator wikibase:apiOutput \"revisions/rev/@user\" .
    ?timestamp wikibase:apiOutput \"revisions/rev/@timestamp\" .
    ?edit_comment wikibase:apiOutput \"revisions/rev/@comment\" .
    bd:serviceParam wikibase:limit \"once\" .
  }
}
 Try it! --Dipsacus fullonum (talk) 15:13, 2 September 2020 (UTC) Thank you Dipsacus fullonum! And what I need change to find items by author name, e.g. Sartle.wiki.bot? I don't think that it is possible to query for items created by a given user using a SPARQL query. You can get a list of a user's contributions with an action=query&list=usercontribs type API call, but these aren't available via the MWAPI service in SPARQL. --Dipsacus fullonum (talk) 17:19, 3 September 2020 (UTC) Finding items that does not have a frwiki Hello, I wanted to have any airport, ranked by patronage, that does not have a frwiki. That query times out, any thoughts ? SELECT ?item 
(sample(?number) as ?number)
WHERE {
  ?item wdt:P31/wdt:P279* wd:Q1248784.
?item p:P3872 ?statement.
?statement pq:P585 ?time.
?statement ps:P3872 ?number.
bind (YEAR(?time) AS ?year)
FILTER(YEAR(?time) =2017) .
   FILTER NOT EXISTS {
   ?article schema:about ?item .
   ?article schema:isPartOf <https://fr.wikipedia.org/> . #Targeting Wikipedia language where subjects has no article.
 }
} group by ?item 
order by desc(?number ) limit 2
 Try it! Bouzinac‚ÄØüí¨‚óè‚úíÔ∏è‚óèüíõ 15:05, 3 September 2020 (UTC)@Bouzinac: You can do by finding the airports first in a named subquery before using frwiki filter. Note that the construct (sample(?number) as ?number) is illegal SPARQL code. The names of new aggregate variables may not already be in scope where they are created. SELECT ?item ?any_number
WITH
{
  SELECT ?item (SAMPLE(?number) AS ?any_number)
  WHERE
  {
    ?item wdt:P31/wdt:P279* wd:Q1248784.
    ?item p:P3872 ?statement.
    ?statement pq:P585 ?time.
    ?statement ps:P3872 ?number.
    BIND (YEAR(?time) AS ?year)
    FILTER (YEAR(?time) = 2017)
  }
  GROUP BY ?item
} AS %get_items
WHERE
{
  INCLUDE %get_items
  FILTER NOT EXISTS
  {
    ?article schema:about ?item .
    ?article schema:isPartOf <https://fr.wikipedia.org/> . #Targeting Wikipedia language where subjects has no article.
  }
}
ORDER BY DESC(?any_number)
 Try it! --Dipsacus fullonum (talk) 17:38, 3 September 2020 (UTC) Most statements, instance of (P31) = human (Q5), no en wiki I'm looking to contribute to 500 Women Wiki Scientists and thought I might start with Wikidata items with the most statements, instance of (P31) = human (Q5), sex or gender (P21)=female (Q6581072) with no en wiki. Would someone be kind enough to generate such a query? Many thanks, Trilotat (talk) 21:40, 4 September 2020 (UTC) @Trilotat: It seems that the list is topped by female chess players with a statement for Elo rating for every month for many years. SELECT ?item ?itemLabel ?occupationLabel ?statements ?sitelinks
WHERE
{
  ?item wdt:P31 wd:Q5 .
  ?item wdt:P21 wd:Q6581072 .
  ?item wikibase:statements ?statements . hint:Prior hint:rangeSafe true .
  FILTER (?statements > 160)
  ?item wikibase:sitelinks ?sitelinks .
  MINUS
  {
    ?article schema:about ?item .
    ?article schema:isPartOf <https://en.wikipedia.org/> .
  }
  OPTIONAL { ?item wdt:P106 ?occupation . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
ORDER BY DESC(?statements)
 Try it! --Dipsacus fullonum (talk) 07:20, 5 September 2020 (UTC) Nigerian women Hello,Please i will like to write a query for Nigerian women missing on English Wikipedia stating their occupation, state of origin/residence and description.Thanks Kaizenify (talk) 08:51, 5 September 2020 (UTC) Items used: Nigeria (Q1033) ‚ÄØ , female (Q6581072) ‚ÄØ Properties used: country of citizenship (P27) ‚ÄØ , place of birth (P19) ‚ÄØ , country (P17) ‚ÄØ , place of death (P20) ‚ÄØ , residence (P551) ‚ÄØ , work location (P937) ‚ÄØ , educated at (P69) ‚ÄØ , sex or gender (P21) ‚ÄØ , occupation (P106) ‚ÄØ SELECT DISTINCT ?item ?itemLabel ?itemDescription ?occLabel ?natLabel ?eduLabel ?pobLabel ?podLabel ?worklocLabel ?resLabel
{
    { ?item wdt:P27 wd:Q1033 } 
    UNION { ?item wdt:P19/wdt:P17 wd:Q1033 } 
    UNION { ?item wdt:P20/wdt:P17 wd:Q1033 } 
    UNION { ?item wdt:P551/wdt:P17 wd:Q1033 } 
    UNION { ?item wdt:P937/wdt:P17 wd:Q1033 } 
    UNION { ?item wdt:P69/wdt:P17 wd:Q1033 } 

    ?item wdt:P21 wd:Q6581072 .
    FILTER NOT EXISTS { [] schema:about ?item ; schema:isPartOf <https://en.wikipedia.org/> }

    OPTIONAL { ?item wdt:P106 ?occ }
    OPTIONAL { ?item wdt:P27 ?nat }
    OPTIONAL { ?item wdt:P19 ?pob }
    OPTIONAL { ?item wdt:P20 ?pod }
    OPTIONAL { ?item wdt:P551 ?res }
    OPTIONAL { ?item wdt:P69 ?edu }
    OPTIONAL { ?item wdt:P937 ?workloc  }

    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
} Try it!Gives some of it. You can remove elements you don't want to include.BTW we had Wikidata:Request_a_query/Archive/2020/03#Nigeria_women_biographies_without_photos_on_Wikipedia earlier. --- Jura 09:54, 5 September 2020 (UTC) Combined age of podium F1 Hey there, I'm trying to get a query to get the age of the podiumplaces (1st, 2nd and 3rd) together for each race in formula 1 season 2020 2020 Formula One World Championship (Q41174436) at that moment in time. I started with one race, to implement the calculations so I could then easily extract it to all the races in the season and optionally to expanding to more seasons: Items used: 2020 Austrian Grand Prix (Q67123914) ‚ÄØ Properties used: point in time (P585) ‚ÄØ , date of birth (P569) ‚ÄØ , participant (P710) ‚ÄØ , ranking (P1352) ‚ÄØ SELECT ?item ?itemLabel ?rank ?dob ?eventtime ?age WHERE{
  wd:Q67123914 p:P710 [ps:P710 ?item ; pq:P1352 ?rank].
  wd:Q67123914 wdt:P585 ?eventtime.
  ?item wdt:P569 ?dob.    
  (?eventtime - ?dob) = ?age  # how to calculate this? because this is giving an error.
  Filter ( ?rank in (1, 2, 3)).       
 # and then finally sum the three ages together
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
} Try it! Thanks in advance! Q.Zanden questions? 13:17, 8 September 2020 (UTC) @QZanden: You need to use BIND(a - b as c) for this, as follows: SELECT ?item ?itemLabel ?rank ?dob ?eventtime ?age WHERE{
  wd:Q67123914 p:P710 [ps:P710 ?item ; pq:P1352 ?rank].
  wd:Q67123914 wdt:P585 ?eventtime.
  ?item wdt:P569 ?dob. 
  bind((?eventtime - ?dob)/365.25 as ?age)  # how to calculate this? because this is giving an error.
  Filter ( ?rank in (1, 2, 3)).       
 # and then finally sum the three ages together
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! SELECT ?item ?itemLabel ?rank ?dob ?eventtime ?age WHERE{
  wd:Q67123914 p:P710 [ps:P710 ?item ; pq:P1352 ?rank].
  wd:Q67123914 wdt:P585 ?eventtime.
  ?item wdt:P569 ?dob. 
  bind(floor((?eventtime - ?dob)/365.25) as ?age)  # how to calculate this? because this is giving an error.
  Filter ( ?rank in (1, 2, 3)).       
 # and then finally sum the three ages together
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! Times are normally recorded in days, which can get a bit confusing when their age is reported as eleven thousand! In the first version I've divided the age by 365.25 to give it in years, and in the second I've explicitly rounded it down to the whole year (so Bottas is 30, not 30.85). This might be more useful for adding up, as it's the normal way we report ages. Note that if we only have a year of birth for someone, it will count as 1 January. Andrew Gray (talk) 15:20, 8 September 2020 (UTC) @QZanden: It seems only the Austrian Grand Prix item at the moment has statements with participant (P710), but when the data are available this code (building on both yours and Andrew Gray's code) will hopefully work: SELECT ?event ?eventLabel ?eventtime (SUM(?age) AS ?age_sum)
WHERE
{
  ?event wdt:P361 wd:Q41174436 .
  ?event p:P710 [ps:P710 ?item ; pq:P1352 ?rank] .
  ?event wdt:P585 ?eventtime .
  ?item wdt:P569 ?dob .
  BIND(xsd:integer(FLOOR((?eventtime - ?dob)/365.25)) AS ?age)
  FILTER ( ?rank in (1, 2, 3) )
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" . }
}
GROUP BY ?event ?eventLabel ?eventtime
 Try it! --Dipsacus fullonum (talk) 20:58, 8 September 2020 (UTC) @Andrew Gray, Dipsacus fullonum: Thanks for your help! I did'nt notice the other GPs don't have any information on participant (P710), but now also looking back at 2019 and 2018, they also don't have any data about participant (P710). Q.Zanden questions? 21:10, 8 September 2020 (UTC) Ordering items in dimensions view I'm trying to order the items in a dimensions graph output so that the lines minimally cross. #defaultView:Dimensions
SELECT ?geneLabel ?proteinLabel ?interacts_withLabel WHERE {
  ?gene wdt:P703 wd:Q82069695 ; wdt:P31 wd:Q7187 .
  ?gene wdt:P688 ?protein .
  OPTIONAL {?protein wdt:P129 ?interacts_with.}
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }
  }
LIMIT 10000
 Try it! For example ORF1ab polyprotein gene in the left hand column encodes two proteins in the middle column (ORF1a and orf1ab) that should ideally be placed next to each other. Any ideas on how to do this? Any ideas appreciated! T.Shafee(evo&evo) (talk) 02:12, 9 September 2020 (UTC) How to include Reference URL for Wikidata SPARQL queries? Brand new user to Wikidata.I have this query: https://w.wiki/bdjI would like to include the referenceUrl on my website https://rembrandt-nono.netlify.app/ so when users click on the more button, they will open a new tab to either a wikipedia page or other source that will have more details on each item.I looked at the wikidata page for some of the paintings and there some(not all) Reference Url for that item. I haven't been able to figure out how to extract that data in my query. Also if there is a better way to extract the references pls let me know.Thanks in advance. N Hi. Note that reference URL (P854) is not used at the item level, but as a part of the references for an item's individual statements. If you e.g. want to see the values of P854 when used in references to statements with the predicate instance of (P31), you can add OPTIONAL { ?painting p:P31 / prov:wasDerivedFrom / pr:P854 ?reference_URL. }
 to your query. You can see a description of Wikidata's data model at mw:Wikibase/Indexing/RDF Dump Format. --Dipsacus fullonum (talk) 07:27, 9 September 2020 (UTC) Thank you. Beautify this query Hello! I'm playing a little bit with twinned cities and I have built a query to show all twinned cities of Basque municipalities connected with lines. The result is pretty... great and ridiculous. Greatculous! If anyone comes with a better way of visualizing this, it would be awesome! #defaultView:Map{\"hide\":[\"?coord1\", \"?coord2\", \"?line\"]}
SELECT DISTINCT ?item ?itemLabel ?hiri_senidetuak ?hiri_senidetuakLabel ?coord1 ?coord2 ?line WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  { ?item wdt:P31 wd:Q2074737. }
  UNION
  { ?item wdt:P31 wd:Q484170. }
  ?item (wdt:P131/(wdt:P131*)/^wdt:P527) wd:Q47588.
  ?item wdt:P190 ?hiri_senidetuak.
  ?item wdt:P625 ?coord1.
  ?hiri_senidetuak wdt:P625 ?coord2.
  ?item p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord1lon; wikibase:geoLatitude ?coord1lat; ] ].
  ?hiri_senidetuak p:P625 [ ps:P625 [];  psv:P625 [ wikibase:geoLongitude ?coord2lon; wikibase:geoLatitude ?coord2lat; ] ].
  BIND(CONCAT('LINESTRING (', STR(?coord1lon), ' ', STR(?coord1lat), ',', STR(?coord2lon), ' ', STR(?coord2lat), ')') AS ?str) .
  BIND(STRDT(?str, geo:wktLiteral) AS ?line) 
}
 Try it! Thanks! -Theklan (talk) 21:52, 9 September 2020 (UTC) I see the problem. It is impossible to see where in Basque Country a line originates in a map of the whole world. I don't have a solution for that, but I can give a minor tip about SPARQL: instead of wdt:P131/(wdt:P131*) you can just use wdt:P131+ meaning one or more occurrences of wdt:P131. --Dipsacus fullonum (talk) 06:07, 10 September 2020 (UTC) Persons from a certain time period and country I would like to get a list of persons from a specific time range and country: The Sengoku Period (year around 1400-1600) of Japan.I looked at notable figures that is already on Wikidata and found a date of birth field on some of them. How would I query using a date of birth from between 1400-1600 in the country of Japan? And would there be a better way to query this?Thanks in advance N SELECT ?person ?personLabel ?date_of_birth
WHERE
{
  ?person wdt:P31 wd:Q5 . # ?person is human
  ?person wdt:P27 wd:Q17 . # ?person is Japanese
  ?person wdt:P569 ?date_of_birth . hint:Prior hint:rangeSafe true .
  FILTER(\"1400-00-00\"^^xsd:dateTime <= ?date_of_birth &&
         ?date_of_birth < \"1600-00-00\"^^xsd:dateTime)
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" . }
}
 Try it! --Dipsacus fullonum (talk) 07:03, 10 September 2020 (UTC) SPARQL query to get wikipedia sitelink to article by wikipedia item id in Wikidata Hello everyone,Is it possible with SPARQL to get Wikipedia sitelink by Wikipedia ID in wikidata (e.g. https://www.wikidata.org/wiki/Q10000 for Dutch Wikipedia) and article ID in wikidata (e.g. https://www.wikidata.org/wiki/Q5580). Now I getting \"Wikimedia database name\" with sparql SELECT ?wikiname WHERE {values ?wiki {wd:Q10000}.?wiki wdt:P1800 ?wikiname} to get \"nlwiki\" value and then using https://www.wikidata.org/w/api.php?action=wbgetentities&ids=Q5580&props=sitelinks/urls&sitefilter=nlwiki to get the link. Is it possible to do it with one SPARQL query? Or how I can optimize it? Yes: SELECT ?sitelink
WHERE
{
  VALUES ?wiki { wd:Q10000 }
  VALUES ?item { wd:Q5580 }
  ?sitelink schema:about ?item . hint:Prior hint:runFirst true .
  ?sitelink schema:isPartOf ?wiki_website .
  ?wiki wdt:P856 ?wiki_website .
}
 Try it! --Dipsacus fullonum (talk) 16:54, 10 September 2020 (UTC) I want to exclude items from a query if an 'instance of' claim is included in a VALUES list. (The actual list will come from a subquery, so I'd like to avoid using FILTER with a bunch of hard-coded || operators) Below is my stab at such a query where I'm hoping to omit Jane Austen if she is either a person OR a biblical figure. Apparently, the FILTER NOT EXISTS treats ?ignorableTypes as an AND condition rather than an OR, which isn't want I'm hoping for. Items used: human (Q5) ‚ÄØ , human biblical figure (Q20643955) ‚ÄØ , Jane Austen (Q36322) ‚ÄØ Properties used: instance of (P31) ‚ÄØ SELECT ?item ?itemLabel ?typeLabel
WHERE {
  VALUES ?ignorableType {
    wd:Q5
    wd:Q20643955
  }
  
  VALUES ?item {
    wd:Q36322
  }
  
  ?item wdt:P31 ?type .
  
  FILTER NOT EXISTS {?item wdt:P31 ?ignorableType}
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
} Try it! Realworldobject (talk) 19:15, 10 September 2020 (UTC) @Realworldobject: Before the filter is applied you have a result with each value of ?ignorableType for each ?item. The filter do remove results for each match with an ignorable type, but leaves result with unmatched ignorable type. If you know the number of ignorable types, you can count the results for each item and filter out ?item where some results are removed: SELECT ?item ?itemLabel ?typeLabel
WHERE {
  VALUES ?ignorableType {
    wd:Q5
    wd:Q20643955
  }
  
  VALUES ?item {
    wd:Q36322
  }
  
  ?item wdt:P31 ?type .
  
  FILTER NOT EXISTS {?item wdt:P31 ?ignorableType}
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
GROUP BY ?item ?itemLabel ?typeLabel
HAVING (COUNT(?item) = 2) # 2 is the number of ignorable types.
 Try it!--Dipsacus fullonum (talk) 07:55, 11 September 2020 (UTC)Help for freeing data from wikipedia to wikidata Would it be possible to have a list of ?item wdt:P31 wd:Q5. MINUS{ ?item wdt:P106 [] } present in Category w:it:Categoria:Cantanti lirici italiani and its subcats of the Italian wikipedia? many thanks --Goldmund100 (talk) 10:23, 11 September 2020 (UTC) @Goldmund100: Yes, certainly: SELECT ?title ?item ?article_it
WHERE
{
  hint:Query hint:optimizer \"None\".
  SERVICE wikibase:mwapi
  {
    bd:serviceParam wikibase:api \"Search\" .
    bd:serviceParam wikibase:endpoint \"it.wikipedia.org\" .
    bd:serviceParam mwapi:srnamespace \"0\" .
    bd:serviceParam mwapi:srsearch 'deepcat:\"Cantanti lirici italiani\"' .
    ?title wikibase:apiOutput mwapi:title .
  }
  BIND (STRLANG(?title, \"it\") AS ?title_it)
  ?article_it schema:name ?title_it .
  ?article_it schema:about ?item .
  ?article_it schema:isPartOf <https://it.wikipedia.org/> .
  ?item wdt:P31 wd:Q5 .
  MINUS { ?item wdt:P106 [] } 
}
 Try it! --Dipsacus fullonum (talk) 13:50, 13 September 2020 (UTC) Thank you very much. For my better understanding, why if I use as deepcat Compositori I get no results whether if I choose a category below Compositori per nazionalit√† I get 7 results. If I understand, I'm sure I will take the most from this query. Thank you very much. --Goldmund100 (talk) 07:08, 14 September 2020 (UTC) @Goldmund100: It is caused by a bug in the deep category search. The search 'deepcat:\"Compositori\"' in the Italian Wikipedia gives only 3,616 results which are too few. The search deepcat:\"Compositori per nazionalit√†\" gives 4,579 results. The missing results in deep category searching is tracked in task Phab:T246568 in Phabricator. --Dipsacus fullonum (talk) 09:16, 14 September 2020 (UTC) @Goldmund100: PS. Please note that even when this bug is fixed, deep category search will not work if the number of subcategories exceeds 256. And the MWAPI service in SPARQL can as maximum return 5,000 results from the Wikipedia API, so the query will never work with too big categories. --Dipsacus fullonum (talk) 10:10, 14 September 2020 (UTC) Searching for people with a residence within X km from a given location Hi All,I've been using the following query to find African American artists in my museum's collections who have a residence listed in their record: Items used: human (Q5) ‚ÄØ , African Americans (Q49085) ‚ÄØ , Philadelphia Museum of Art (Q510324) ‚ÄØ Properties used: instance of (P31) ‚ÄØ , ethnic group (P172) ‚ÄØ , has works in the collection (P6379) ‚ÄØ , residence (P551) ‚ÄØ SELECT ?item ?itemLabel ?itemDescription ?residence ?residenceLabel WHERE {
  ?item wdt:P31 wd:Q5;
    wdt:P172 wd:Q49085;
    wdt:P6379 wd:Q510324;
        wdt:P551 ?residence
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
} Try it!I'd like to refine the results further by querying artists with a residence within an 80 km radius of Philadelphia. I'm pretty stumped on this one. Thanks in advance for the help! ‚Äì The preceding unsigned comment was added by PMAlibcat (talk ‚Ä¢ contribs). Items used: Philadelphia (Q1345) ‚ÄØ , human (Q5) ‚ÄØ Properties used: coordinate location (P625) ‚ÄØ , residence (P551) ‚ÄØ , instance of (P31) ‚ÄØ Features used: map (Q24515275) ‚ÄØ #defaultView:Map
SELECT DISTINCT 
  ?item ?itemLabel ?itemDescription
  ?p ?pLabel
  ?coords ?distance
WHERE
{
  wd:Q1345 wdt:P625 ?a0 .
  SERVICE wikibase:around {
      ?p wdt:P625 ?coords .
      bd:serviceParam wikibase:center ?a0 .
      bd:serviceParam wikibase:radius \"80\" .
      bd:serviceParam wikibase:distance ?distance .      
  }
  hint:Query hint:optimizer \"None\".
  ?item wdt:P551 ?p .
  ?item wdt:P31 wd:Q5 .
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
ORDER BY ?distance Try it! Maybe the above helps you get there. --- Jura 20:27, 11 September 2020 (UTC)Thank you so much! I was able to incorporate my other search parameters into the query you posted, too. Bilingual place name sign (P1766) Hello, how to list bilinguals place name sign (P1766) ? (and find those obviously bilingual but not designed as bilingual) ? Bouzinac‚ÄØüí¨‚óè‚úíÔ∏è‚óèüíõ 09:24, 14 September 2020 (UTC) @Bouzinac: SELECT ?item ?itemLabel ?place_name_sign ?lang1Label ?lang2Label
WHERE
{
  ?item p:P1766 ?sign_statement .
  ?sign_statement a wikibase:BestRank .
  ?sign_statement ps:P1766 ?place_name_sign .
  ?sign_statement pq:P407 ?lang1 .
  ?sign_statement pq:P407 ?lang2 .
  FILTER (STR(?lang1) < STR(?lang2))
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Dipsacus fullonum (talk) 10:20, 14 September 2020 (UTC) Male tennis players who won a Grand Slam singles title Hi, looking for a list of male (P21=Q6581097) tennis players (P106=Q10833314) who have won a singles title at a Grand Slam tournament sorted by height (P20148) in centimeters with tallest player listed first. Each player should only be listed once. Can this be done? Grand Slam tournaments are Australian Open (Q60874), French Open (Q43605), Wimbledon (Q41520) and US Open (Q123577). --Wolbo (talk) 15:53, 14 September 2020 (UTC) This is one way to do it, there is missing data though, eg this year's winners, not sure if everyone has a height either SELECT DISTINCT ?winner ?winnerLabel ?height_in_cm WHERE {
  ?x wdt:P31 wd:Q46190676 .                  # x is a tennis event
  ?x wdt:P2094 wd:Q16893072 .                # x's competiion class is men's singles
  ?x wdt:P361/wdt:P31/wdt:P361 wd:Q102113  . # x is part of a tournament that is part of a grand slam event
  ?x wdt:P1346 ?winner .                     # x has a winner
  ?winner  p:P2048/psn:P2048 [               # the winner has a height
     wikibase:quantityAmount ?height         # get normalized units (in metres)
  ] .
  BIND (100 * ?height AS ?height_in_cm)      # convert to centimetres
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
ORDER BY DESC (?height_in_cm)                # order by height, tallest first
 Try it! Piecesofuk (talk) 19:20, 14 September 2020 (UTC)My version made simultaneously is the essentially the same but includes players with unknown height: SELECT ?player ?playerLabel ?height_in_cm
WHERE
{
  ?player wdt:P31 wd:Q5 . # ?player is human
  ?player wdt:P21 wd:Q6581097 . # ?player is male
  ?player wdt:P106 wd:Q10833314 . # ?player is tennis player
  OPTIONAL {
    ?player p:P2048 / psn:P2048 / wikibase:quantityAmount ?normalized_height .
     BIND (?normalized_height * 100 AS ?height_in_cm)
  }
  
  wd:Q102113 wdt:P527 ?grand_slam . # ?grand_slam is a tennis grand slam tournament
  ?grand_slam_for_year wdt:P31 ?grand_slam . # ?grand_slam_for_year is a grand slam tourmament for a certain year
  ?event wdt:P361 ?grand_slam_for_year . # event is grand slam event
  ?event wdt:P2094 wd:Q16893072 . # event is a men's singles event
  ?event wdt:P1346 ?player . # ?player won ?event
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
GROUP BY ?player ?playerLabel ?height_in_cm
ORDER BY DESC(?height_in_cm)
 Try it! --Dipsacus fullonum (talk) 19:57, 14 September 2020 (UTC) @Piecesofuk:, @Dipsacus fullonum:, thanks to both of you, seems too work nicely! --Wolbo (talk) 21:52, 14 September 2020 (UTC) Timing out Hello, why is this timing out? I wanted a list of Paris (Q90) streets/roads that havent any place name sign (P1766). SELECT ?item ?itemLabel WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  ?item wdt:P31/wdt:P279* wd:Q83620.
  ?item wdt:P131* wd:Q90.
    MINUS {
    ?item wdt:P1766 []  # exclude if there is an image
  }
}
 Try it! Bouzinac‚ÄØüí¨‚óè‚úíÔ∏è‚óèüíõ 18:58, 15 September 2020 (UTC)Corrected myself in removing one * SELECT ?item ?itemLabel WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  ?item (wdt:P31/(wdt:P279*)) wd:Q79007;
    wdt:P131 wd:Q90.
  MINUS { ?item wdt:P1766 []. }
  MINUS { ?item wdt:P576 []. }          
  MINUS { ?item wdt:P582 []. }  
}
 Try it! @Bouzinac: The first query times out because the SPARQL engine cannot see that the number of items which are a thoroughfare (Q83620) is much higher than the number of items in Paris (Q90). You can fix the timeout by forcing the code ?item wdt:P131* wd:Q90. to be executed first by putting it in a named subquery: SELECT DISTINCT ?item ?itemLabel
WITH
{
  SELECT ?item
  WHERE
  {
    ?item wdt:P131* wd:Q90.
  }
} AS %in_Paris
WHERE
{
  INCLUDE %in_Paris
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  ?item wdt:P31/wdt:P279* wd:Q83620.
  MINUS {
    ?item wdt:P1766 []  # exclude if there is an image
  }
}
 Try it! --Dipsacus fullonum (talk) 06:35, 16 September 2020 (UTC) Wikimedia Commons Query Service This is about the Commons Query Service, however the concept is, I'd think identical. Since there is nobody sticking around at Commons for this type of questions I'd like to ask hereI have the following query: SELECT ?Urheber ?UrheberLabel ?Lizenz ?LizenzLabel WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  ?Urheber wdt:P170 wd:Q15080600.
  OPTIONAL { ?Urheber wdt:P275 ?Lizenz. }
}
LIMIT 100 It returns everything I would like it to but in an unexpected way: Urheber UrheberLabel Lizenz LizenzLabel sdc:M371354 M371354 wd:Q50829104 Q50829104 QueryWhat I would like to see is: Urheber UrheberLabel Lizenz LizenzLabel sdc:M371354 Wiederer√∂ffnung der S-Bahn Berlin am Bahnhof Eichkamp wd:Q50829104 GNU-Lizenz f√ºr freie Dokumentation Version 1.2 oder sp√§ter Just getting the same but unlinked for the label is not useful as you can imagine. It will be great if someone could assist in getting the label instead of the page id. Thanks for your time. --Marbot (talk) 07:25, 15 September 2020 (UTC) @Marbot: The license labels are Wikidata data, so you need to make a federated query to the WDQS endpoint to get them: SELECT ?Urheber ?UrheberLabel ?Lizenz ?LizenzLabel WHERE {
  ?Urheber wdt:P170 wd:Q15080600.
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".
    ?Urheber rdfs:label ?UrheberLabel.
  }
  OPTIONAL { ?Urheber wdt:P275 ?Lizenz. }
  SERVICE <https://query.wikidata.org/sparql> {
    SERVICE wikibase:label {
      bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".
      ?Lizenz rdfs:label ?LizenzLabel.
    }
  }
}
LIMIT 100
 Try it! --Dipsacus fullonum (talk) 13:44, 15 September 2020 (UTC) @Dipsacus fullonum: Thanks a lot for this! Yeah, to make a federated query does make sense. And the results work also, however only for one label. As soon as I try to add more labels the results are visibly incorrect. SELECT ?Urheber ?UrheberLabel ?relevante_Person ?relevante_PersonLabel ?Ort ?OrtLabel ?Koordinaten_des_Standpunktes ?Gr_ndung__Erstellung_bzw__Entstehung_oder_Erbauung ?Lizenz ?LizenzLabel WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  ?Urheber wdt:P170 wd:Q98908523.
  OPTIONAL { ?Urheber wdt:P3342 ?relevante_Person. }
  OPTIONAL { ?Urheber wdt:P276 ?Ort. }
  OPTIONAL { ?Urheber wdt:P1259 ?Koordinaten_des_Standpunktes. }
  OPTIONAL { ?Urheber wdt:P571 ?Gr_ndung__Erstellung_bzw__Entstehung_oder_Erbauung. }
  OPTIONAL { ?Urheber wdt:P275 ?Lizenz. }
    SERVICE <https://query.wikidata.org/sparql> {
    SERVICE wikibase:label {
      bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\".
      ?Lizenz rdfs:label ?LizenzLabel.
      ?relevante_Person rdfs:label ?relevante_PersonLabel.
      ?Ort rdfs:label ?OrtLabel.
    }
  }
}
LIMIT 100
 Try it! The first label detected is just copied over from the top to the bottom. I had hoped that the answer to my short query example would help me adopting the query I originally wanted to create. A complete failure, I am afraid. I am interested in updating the structured data for all the files I uploaded to Commons. Thus I need to see what I added and also I would like to check for consistency. Thus I need a table showing link to file || label of the file || label of relevant person || label of location || coordinates of location || creation date || license label
 I does not seem to be possible to omit the link to the relevant object, e.g. for label of relevant person I also get the link of relevant person, which is not necessary for my purpose. If these links can be omitted it will be cool (did not add them to the expected header). In the end I have the feeling that there will be many more people, e. g. people working on the wiki loves monuments contest to get their data right for the suggested data model by the contest. Thus I think having a cool query here will not just be beneficial for me. Again, thanks for all your assistance. I really appreciate this. Cheers --Marbot (talk) 07:56, 17 September 2020 (UTC) @Marbot: Please note that in my query above, I used the manual mode of the label service for both WCQS and WDQS. (See the label service manual at mw:Wikidata Query Service/User Manual#Label service). In your query, you use the label service for WCQS in automatic mode. I suppose that causes all the variables with names ending with \"Label\" to be bound before making the federated call to WDQS so you don't get any labels from WDQS. --Dipsacus fullonum (talk) 08:21, 17 September 2020 (UTC) @Dipsacus fullonum: Thanks for the note. I was not able to convert it into a workable table showing the columns I indicated above, so this is the end of this story since admittedly I not have time to learn SPARQL queries at the moment. I will now do one item at once, i.e. label, person etc. based on you original suggestion. Hope that at least this will work. Anyhow, thanks a ton for trying to make a difference! Cheers --Marbot (talk) 14:21, 17 September 2020 (UTC) PS In the end I can easily do a VLOOKUP in Calc or Excel and merge all tables into one once all the data was added. From that point I can check consistency. Huu, I'm afraid your query suggestion does not work either. SELECT ?Urheber ?UrheberLabel ?Ort ?OrtLabel WHERE {
  ?Urheber wdt:P170 wd:Q98908523.
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language \"de\".
    ?Urheber rdfs:label ?UrheberLabel.
  }
  OPTIONAL { ?Urheber wdt:P276 ?Ort. }
  SERVICE <https://query.wikidata.org/sparql> {
    SERVICE wikibase:label {
      bd:serviceParam wikibase:language \"de\".
      ?Ort rdfs:label ?OrtLabel.
    }
  }
}
LIMIT 1000
 Try it! If no data was added at all, it copies over the data from the last valid result before instead of leaving a blanc cell. So this is not usable at all. Now the story indeed came to an end since my work-around fell apart. What a pity. --Marbot (talk) 14:36, 17 September 2020 (UTC) PS I just see from the first results that this is not the only issue. What a, what a pity. :( Is there a way to get just the QIDs and not their respective URL? I use Quickstatements and I always have to search and replace the URL in Google sheets to get just the QIDs. Is there a way to get just the QIDs? Nonoumasy (talk) 23:01, 17 September 2020 (UTC) @Nonoumasy: You can convert the URI to text and remove the first part: SELECT ?item ?item_as_text
{
  VALUES ?item { wd:Q1 }
  BIND (STRAFTER(STR(?item), \"http://www.wikidata.org/entity/\") AS ?item_as_text)
}
 Try it! --Dipsacus fullonum (talk) 23:45, 17 September 2020 (UTC) Thanks Dipsacus Erdos number I've tried to build a query to find Erdos number of a person by definition SELECT ?work1Label ?work2Label ?work3Label WHERE {
  VALUES ?pers0 {wd:Q173746 wd:Q15873}
  VALUES ?persx {wd:Q56812165 wd:Q52015163 wd:Q77342071}
  ?work1 wdt:P50 ?pers0;
         wdt:P50 ?pers1.
  ?work2 wdt:P50 ?pers1;
         wdt:P50 ?pers2.
  ?work3 wdt:P50 ?pers2;
         wdt:P50 ?pers3.
  ?work4 wdt:P50 ?pers3;  wdt:P50 ?persx.
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  }
 Try it! but it times out if E.n.>3. I see this example with gas.service and probably it is possible to adapt it for such relation as \"being co-authors of a scientific work\", but I failed. Anyone? --Infovarius (talk) 09:59, 16 September 2020 (UTC) @Infovarius: Like this? SELECT ?item ?itemLabel ?calculated_Erd≈ës_number ?stored_Erd≈ës_number
WITH
{
  SELECT ?item ?depth
  WHERE
  {
    SERVICE gas:service
    {
     gas:program gas:gasClass \"com.bigdata.rdf.graph.analytics.SSSP\" .
     gas:program gas:in wd:Q173746 .
     gas:program gas:out ?item .
     gas:program gas:out1 ?depth .
     gas:program gas:linkType wdt:P50 .
     gas:program gas:traversalDirection \"Undirected\" .
     gas:program gas:maxIterations 6 .
    }
    FILTER (FLOOR(?depth / 2) * 2 = ?depth) # Use only even depths. The odd numbers are the written papers
  }
} AS %get_items
WHERE
{
  INCLUDE %get_items
  BIND (xsd:integer(?depth/2) AS ?calculated_Erd≈ës_number)
  OPTIONAL { ?item wdt:P2021 ?stored_Erd≈ës_number . }
  SERVICE wikibase:label {bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" }              
} 
ORDER BY ?calculated_Erd≈ës_number
 Try it! --Dipsacus fullonum (talk) 20:37, 16 September 2020 (UTC) @Dipsacus fullonum: I love it! I knew I something don't understand in gas-service... Although the query still times out at maxIterations=10 (ErdowNumber=5) but I got the result I wanted. --Infovarius (talk) 20:14, 18 September 2020 (UTC) Get Wikipedia URLs (sitelinks) in Wikidata SPARQL query I would like to get the wikipedia URls of the items from a SPARQL query. Here is my query: https://w.wiki/ccVI tried using this solution to get the wikipedia articles but didn't have much luck: https://opendata.stackexchange.com/questions/6050/get-wikipedia-urls-sitelinks-in-wikidata-sparql-queryThanks.--Nonoumasy (talk) 08:55, 18 September 2020 (UTC)Template:Nonoumasy @Nonoumasy: #Battles, Sieges, Military Campaigns, Wars, Rebellions in the Sengoku Period
#Show label, link to English Wikipedia, year, coordinates, startTime
SELECT ?battle ?battleLabel ?wikipedia_link_en ?year ?coordinates
  (GROUP_CONCAT(DISTINCT ?participantLabel; SEPARATOR = \" // \") AS ?participants)
WHERE {
  { ?battle (wdt:P361|wdt:P2348) wd:Q204023. }
  OPTIONAL { ?battle wdt:P710 ?participant. }
  OPTIONAL { ?battle (wdt:P585|wdt:P580) ?year. }
  OPTIONAL { ?battle wdt:P625 ?coordinates. }
  OPTIONAL { ?wikipedia_link_en schema:about ?battle ; schema:isPartOf <https://en.wikipedia.org/>. }
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,ja\".
    ?battle rdfs:label ?battleLabel.
    ?participant rdfs:label ?participantLabel.
  }
}
GROUP BY ?battle ?battleLabel ?wikipedia_link_en ?year ?typeBattle ?coordinates
ORDER BY (?year)
 Try it! --Dipsacus fullonum (talk) 18:01, 18 September 2020 (UTC) Is it possible to have an array or list of values for a column? I have this query: https://w.wiki/cVCI was wondering if value(s) for a column in a query can be extracted as an array. For example, in a battle, there are multiple participants(wdt:P710) wd:Q39040 Battle of Yamazaki 12 July 1582 Point(135.691333333 34.907055555) Toyotomi Hideyoshi wd:Q39040 Battle of Yamazaki 12 July 1582 Point(135.691333333 34.907055555) Akechi Mitsuhide vs. wd:Q39040 Battle of Yamazaki 12 July 1582 Point(135.691333333 34.907055555) [Toyotomi Hideyoshi, Akechi Mitsuhide] ‚Äì The preceding unsigned comment was added by Nonoumasy (talk ‚Ä¢ contribs). Items used: Sengoku period (Q204023) ‚ÄØ Properties used: part of (P361) ‚ÄØ , time period (P2348) ‚ÄØ , participant (P710) ‚ÄØ , point in time (P585) ‚ÄØ , coordinate location (P625) ‚ÄØ Features used: map (Q24515275) ‚ÄØ #defaultView:Map{\"hide\": \"?coordinates\"}
SELECT ?battle ?battleLabel ?year ?coordinates (GROUP_CONCAT(DISTINCT ?participantLabel; separator=\" // \") as ?participants) 
WHERE 
{
  ?battle ( wdt:P361 | wdt:P2348 ) wd:Q204023.
 
  OPTIONAL { ?battle wdt:P710 ?participant. }
  OPTIONAL { ?battle wdt:P585 ?year. }
  OPTIONAL { ?battle wdt:P625 ?coordinates. }

  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,ja\". 
                         ?battle rdfs:label ?battleLabel .
                         ?participant rdfs:label ?participantLabel .
                         }
}
GROUP BY ?battle ?battleLabel ?year ?coordinates
ORDER BY (?year) Try it!Try the above with GROUP_CONCAT. This requires using the label service in manual mode.BTW, time period (P2348) seems preferable over part of (P361). A bot could move them over for you. --- Jura 10:45, 17 September 2020 (UTC)Thanks @Jura:Yes, I would also prefer time period. I was going to use Quick statements to add a statement of 'time period' of \"sengoku period\" . How does the bot work? Do I do that here? https://www.wikidata.org/wiki/Wikidata:Bot_requests ‚Äì The preceding unsigned comment was added by Nonoumasy (talk ‚Ä¢ contribs). Yes, or [1] should take care of it. It might take a couple of days. --- Jura 10:36, 19 September 2020 (UTC) ThanksThanks @Jura: Scholarly articles with images on Commons Hello, I would want a request to see all the scholarly article (Q13442814) published in (P1433) ZooKeys (Q219980) publication date (P577) within the year 2018, and to see if the items has (or not) a value for Commons category (P373). Christian Ferrer (talk) 18:21, 19 September 2020 (UTC) SELECT ?item ?itemLabel ?date ?commonscat ?commonssitelink WHERE {
  ?item wdt:P1433 wd:Q219980; wdt:P577 ?date; wdt:P31 wd:Q13442814 .
  FILTER(YEAR(?date) = 2018) .
  OPTIONAL { ?item wdt:P373 ?commonscat }
  OPTIONAL { ?commonssitelink schema:about ?item; schema:isPartOf <https://commons.wikimedia.org/> }
  SERVICE wikibase:label { bd:serviceParam wikibase:language 'en' }
} ORDER BY ASC(?date)
 Try it!‚ÄîMisterSynergy (talk) 18:25, 19 September 2020 (UTC)Excelent, thanks you! Christian Ferrer (talk) 18:27, 19 September 2020 (UTC) Several query requests in one Not sure if I should post several query request. Since they are related I thought I would just post one. I am trying to finish my site. https://sengoku.netlify.app/#So, I'm trying to refine the query: https://w.wiki/cjqMany thanks to the help I've gotten so far.These are my request: I would like to get the Battles, sieges, and campaigns of the Sengoku Period. So what I did was query the 'part of' (P361) | 'time period'(P2348) of the Sengoku Period(Q204023). However, some of the results are people from this period. Is there a way to subtract people from the query or should I just use a UNION of 'instance of' for 'battle', 'siege', and 'military campaign'. Can I get Coordinate location(P625) of location (PP276). Get english or Japanese Wikipedia url links Create a column called ‚Äòtype‚Äô for ‚Äòbattle‚Äô, ‚Äòsiege‚Äô, ‚Äòmilitary campaign‚Äô. Use only 1 coordinate location for each item. Get images(optional) for each participant. I know this is query is long. Hopefully its ok. Thanks in advance.--Nonoumasy (talk) 10:34, 19 September 2020 (UTC)Template:Nonoumasy Items used: Sengoku period (Q204023) ‚ÄØ , human (Q5) ‚ÄØ , military operation (Q645883) ‚ÄØ Properties used: part of (P361) ‚ÄØ , time period (P2348) ‚ÄØ , instance of (P31) ‚ÄØ , subclass of (P279) ‚ÄØ , participant (P710) ‚ÄØ , image (P18) ‚ÄØ , point in time (P585) ‚ÄØ , start time (P580) ‚ÄØ , coordinate location (P625) ‚ÄØ , location (P276) ‚ÄØ SELECT ?battle ?battleLabel 
  ?type ?typeLabel 
  ?wikipedia_link_en ?wikipedia_link_ja 
  (YEAR(?year) as ?someyear)
  (SAMPLE(?coordinates) as ?some_coordinates)
  (GROUP_CONCAT(DISTINCT ?participantLabel; SEPARATOR = \" // \") AS ?participants)
  (SAMPLE(?image) as ?some_image)
WHERE
{
  { ?battle (wdt:P361 | wdt:P2348) wd:Q204023. }
  FILTER NOT EXISTS {  ?battle wdt:P31 wd:Q5 } #exclude people
  # ?battle wdt:P31/wdt:P279* wd:Q645883 .     #only military operations or subtypes 
  OPTIONAL { ?battle wdt:P31 ?type }
  OPTIONAL { ?battle wdt:P710 ?participant. 
              OPTIONAL { ?participant wdt:P18 ?image }
           
           }
  ?battle (wdt:P585 | wdt:P580) ?year. 
  OPTIONAL { ?battle wdt:P625 ?coordinates0. }
  OPTIONAL { ?battle wdt:P276 / wdt:P625 ?coordinates1. }
  BIND( COALESCE(?coordinates0, ?coordinates1) as ?coordinates)
  OPTIONAL { ?wikipedia_link_en schema:about ?battle ; schema:isPartOf <https://en.wikipedia.org/>. }
  OPTIONAL { ?wikipedia_link_ja schema:about ?battle ; schema:isPartOf <https://ja.wikipedia.org/>. } 
  SERVICE wikibase:label {
    bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en,ja\".
    ?battle rdfs:label ?battleLabel.
    ?participant rdfs:label ?participantLabel.
    ?type rdfs:label ?typeLabel.
  }
}
GROUP BY ?battle ?battleLabel  ?type ?typeLabel ?wikipedia_link_en ?wikipedia_link_ja ?year ?typeBattle
ORDER BY (?year) Try it! The above does some of it except #6 (images: it still needs some work to build a presentable gallery). Currently I just filter out people but eventually you might want to filter by types, to avoid that some other random things slip in, e.g. Ch≈´goku ≈ågaeshi (Q10873539) which uses different location properties. --- Jura 10:55, 19 September 2020 (UTC) Thanks @Jura:. This is great! I will study and learn from this query. Is there a way to make the Japanese wikipedia link conditional. Show only if english wikipedia not available? I'm just using it as backup so I don't have an empty value. Ideally I have one link for each item. Do I do a BIND here like you did with coordinates? Also, is it possible to get multiple image links for the participant? I'll convert this column into an array in Python and then turn it images in the html. is this where GROUP CONCAT would help? btw url shortener fails --Nonoumasy (talk) 20:56, 19 September 2020 (UTC)Template:Nonoumasy @Nonoumasy: Yes, add BIND( COALESCE(?wikipedia_link_en, ?wikipedia_link_ja) as ?wikipedia_link) and replace ?wikipedia_link_en ?wikipedia_link_ja with ?wikipedia_link in both SELECT and GROUP BY. Yes, replace (SAMPLE(?image) as ?some_image) with (GROUP_CONCAT(DISTINCT ?image; SEPARATOR = \" // \") AS ?images) in SELECT. Yes, it cannot handle long queries. --Dipsacus fullonum (talk) 06:37, 20 September 2020 (UTC) Thanks @Dipsacus fullonum: Template:Nonoumasy I think the solution for #6 needs to be slightly more complex unless you don't mind the person not being identified. --- Jura 06:54, 20 September 2020 (UTC) @Jura: Since the images provided are less than ideal. I think I'll fill those in in Python. Having the participantLabels is a good foundation. --Nonoumasy (talk) 11:22, 20 September 2020 (UTC)Template:Nonoumasy Last names for Norwegian citizens I want to have listed all last names for Norwegian having country of citizenship (P27) Norway (Q20) and who having last names where the family name (P734) also have been said to be the same as (P460). Breg Pmt (talk) 18:37, 20 September 2020 (UTC) Items used: Norway (Q20) ‚ÄØ Properties used: country of citizenship (P27) ‚ÄØ , family name (P734) ‚ÄØ , said to be the same as (P460) ‚ÄØ SELECT
  ?item ?itemLabel ?count
  (GROUP_CONCAT(DISTINCT ?otherLabel; separator=\", \") as ?others) 
WITH
{
  SELECT ?item (COUNT(DISTINCT ?p) as ?count) WHERE { ?p wdt:P27 wd:Q20 ; wdt:P734 ?item } GROUP BY ?item
} as %surnames
WHERE
{
    hint:Query hint:optimizer \"None\".
    INCLUDE %surnames
    OPTIONAL { ?item wdt:P460 ?other }
    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". 
                           ?item rdfs:label ?itemLabel .
                           ?other rdfs:label ?otherLabel .
                           }
}
GROUP BY ?item ?itemLabel ?count
ORDER BY DESC(?count) Try it! @Pmt:. Maybe \"?other\" should be filtered for Norwegians as well, but I couldn't get that to work. --- Jura 18:53, 20 September 2020 (UTC) @Jura1: Not so unexspected the query was timing out, could it be limited in some way, only men in one and women in another for instance? Pmt (talk) 19:40, 20 September 2020 (UTC) Try with the hint I just added. If it times out, retry once or twice. It worked for me, but close to 60s . --- Jura 19:50, 20 September 2020 (UTC) Very nice! It works, then next step would be to have a list only for persons having an ID in Histreg.no Norwegian historical register of persons ID (P4574) Pmt (talk) 20:33, 20 September 2020 (UTC) @Jura1, Pmt: Try to replace COUNT(DISTINCT ?p) with COUNT(*) in the query. The change makes the query to execute in nearly half time (22 seconds instead of 40 seconds for me). It will only give different results if somebody has the same name more than once, which never or almost never happens as far as know, so I don't think that is an issue. --Dipsacus fullonum (talk) 21:45, 20 September 2020 (UTC) Items used: Norway (Q20) ‚ÄØ , female (Q6581072) ‚ÄØ Properties used: country of citizenship (P27) ‚ÄØ , sex or gender (P21) ‚ÄØ , family name (P734) ‚ÄØ , native label (P1705) ‚ÄØ , said to be the same as (P460) ‚ÄØ """@en;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Request_a_query/Archive/2020/09>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:genid-4e694113159d4e3db4a1a913894a81d8935-wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/>;
  sh:select """PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
SELECT DISTINCT ?all ?names
WITH
{
SELECT ?other ?nl (COUNT(DISTINCT ?p ) as ?count)
WHERE 
{
    hint:Query hint:optimizer \"None\".
    ?p wdt:P27 wd:Q20 ; wdt:P21 wd:Q6581072 ; wdt:P734 ?item . 
    ?item wdt:P1705 ?nl . 
    { ?item wdt:P460* ?other } UNION { ?other wdt:P460* ?item } .
} 
GROUP BY ?other ?nl
ORDER BY ?nl
} as %a
WHERE
{
  SELECT ?other (sum(?count) as ?all) (GROUP_CONCAT(DISTINCT CONCAT(?nl, \" (\",str(?count),\"), \")  ) as ?names)
  { INCLUDE %a }
  GROUP bY ?other  
}
ORDER BY DESC(?all)
LIMIT 100""";
  <http://example.org/bigdata_select> """SELECT DISTINCT ?all ?names
WITH
{
SELECT ?other ?nl (COUNT(DISTINCT ?p ) as ?count)
WHERE 
{
    hint:Query hint:optimizer \"None\".
    ?p wdt:P27 wd:Q20 ; wdt:P21 wd:Q6581072 ; wdt:P734 ?item . 
    ?item wdt:P1705 ?nl . 
    { ?item wdt:P460* ?other } UNION { ?other wdt:P460* ?item } .
} 
GROUP BY ?other ?nl
ORDER BY ?nl
} as %a
WHERE
{
  SELECT ?other (sum(?count) as ?all) (GROUP_CONCAT(DISTINCT CONCAT(?nl, \" (\",str(?count),\"), \")  ) as ?names)
  { INCLUDE %a }
  GROUP bY ?other  
}
ORDER BY DESC(?all)
LIMIT 100""" .
