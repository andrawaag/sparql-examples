@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-8e17d5fdc62e14a7c77f419e801036d4> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  sh:select """SELECT ?item ?itemLabel ?nat ?natLabel ?pob ?pobLabel ?coor
{
    hint:Query hint:optimizer \"None\".
    ?item wdt:P19 / wdt:P17 wd:Q142 .
    MINUS { ?item wdt:P27 wd:Q142 } .
    ?item wdt:P27 ?nat .
    ?item wdt:P19 ?pob . 
    ?pob wdt:P17 wd:Q142 . OPTIONAL { ?pob wdt:P625 ?coor }
    ?item wdt:P31 wd:Q5 .
    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],fr,en\". }
}""";
  rdfs:comment """ This page is an archive. Please do not modify it. Use the current page, even to continue an old discussion. Contents 1 Exclude a list of QIDs 2 Edits made by a user 3 Adding labels to aggregated values 4 Url and date 5 News article archives 6 Sitelinks for multiple items 7 I want to list the sitelink to English and German Wikipedia 8 Search for multiple (specific) types via wikibase:api \"EntitySearch\" running into 50 record limit? 9 Get all items that come under a particular category 10 WikiProject every politician 11 WikiData SPARQL query for plants data 12 Query of Species of Trees in different languages 13 chemical substances used in plastics 14 Trouble with nested town/state entities 15 Query to get Fotball Players from different countries by divistion and date of birth 16 DisGeNET and mirbase 17 Common properties that link politicians 18 All eukaryotic proteins 19 Property paths with normalRank 20 office held by head of government 21 Leading zeros 22 Select a point in time and a determination method 23 Change query 24 Order by number - correctly 25 New year challenge 26 Map: Set individual colors for ordinal scale 27 Liste des codes Wikidata des communes franÃ§aises 28 Wikidata:Database reports/items with P569 greater than P570 29 all items with P536 or P597 and not P4544 30 Unwanted instances 31 Group URLs in one cell 32 All norwegians born before 1802 33 Dams 34 Feature arcticle 35 Famous (not french) people born in France 36 People by decade and century 37 Articles on the Hebrew Wikipedia that have coordinates 38 List of films 39 Countings airports 40 how many trans-people in Wikidata? 41 Count people by birth date decade and by gender? 42 Get all values in two column format Exclude a list of QIDs I have a worklist of potential Wikipedia article topics and I want to remove entries that lack enough sourcing to warrant an article (so that a newcomer won't see this list and try to create the article). My understanding is that manual exclusion from the query is my only optionâ€”is this correct? If so, what's the best way to write this such that it's easy to maintain a list of QIDs to exclude from a series of queries? MINUS or NOT EXISTS? When I edit the list of QIDs, is there a way to host the list in one location or would I need to update it in each query? czar 05:58, 1 January 2020 (UTC) I figured it out with MINUS[1] but do {{Ping}} me if you have any suggestions for improvement czar 07:27, 1 January 2020 (UTC) @Czar: There's also NOT IN, which is deployed within FILTER, in the form filter (?item NOT IN (wd:Q516882,wd:Q12345)). I suspect this might be the easiest approach. --Tagishsimon (talk) 14:03, 1 January 2020 (UTC) Edits made by a user Hi all,I don't know if this request is possible or not: I would like to have the list of items (subclasses of structure of worship (Q1370598) in France (Q142)) for which the user Peter17-Bot add the property coordinate location (P625) (e.g. https://www.wikidata.org/w/index.php?title=Q38434124&diff=615373953&oldid=551868164). Thank you very much. Tubezlob (ðŸ™‹) 11:29, 27 December 2019 (UTC) @Tubezlob: AFAIK, not via SPARQL. Quarry probably can - here's a not very dissimilar query - https://quarry.wmflabs.org/query/16335 - presuming that Quarry can filter on the edit summary. (iirc, Quarry cannot see the content of edits, but can see edit metadata.) Sadly successful Quarrying continues to be beyond my paygrade. --Tagishsimon (talk) 06:03, 28 December 2019 (UTC) @Tagishsimon: Thank you, that's a good hint! Is there an expert of Quarry here? ðŸ˜Š Tubezlob (ðŸ™‹) 09:01, 28 December 2019 (UTC) If the edits happend in a relatively short period, I would try SPARQL. Have a look at Wikidata:SPARQL_query_service/queries/examples/advanced#Map_of_newspapers_in_the_United_States_by_presence_of_Infobox_newspaper_in_their_English_Wikipedia_article. I would combine: Regular SPARQL query to get the instance/subclass of structure of worship (Q1370598) in France (Q142) Mediawiki API request to get all the edits of the bot in a certain period. Multichill (talk) 12:48, 2 January 2020 (UTC) @Multichill: Thank you for your answer! I made this SPARQL query: SELECT ?church ?churchLabel ?location
WHERE
{
  ?church wdt:P31/wdt:P279* wd:Q1370598; 
         wdt:P17 wd:Q142;
         wdt:P625 ?location.
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"fr\". }
}
 Try it! For the Mediawiki API, I tried this request (URL): {
	\"action\": \"query\",
	\"format\": \"json\",
	\"list\": \"usercontribs\",
	\"uclimit\": \"max\",
	\"ucstart\": \"2017-12-30T15:00:00.000Z\",
	\"ucend\": \"2017-12-31T15:00:00.000Z\",
	\"ucuser\": \"Peter17-Bot\"
} But I don't get anything. I don't understand how Mediawiki API works (I have no knowledge at all about APIs). How can I have just the list of QIDs? Also, I saw that this query is limited to 500 results. Is it possible to get directly the â‰ˆ 7,500 results?Thank you very much, Tubezlob (ðŸ™‹) 16:23, 2 January 2020 (UTC) I consulted the manual at mw:Wikidata_Query_Service/User_Manual/MWAPI#Supported_services and it only supports so called generators. That's possible for search, but not for usercontributions I'm afraid. Getting this data will involve some better trickery. Multichill (talk) 17:19, 2 January 2020 (UTC) @Multichill: Thank you. So I did the work manually: I copied the modifs summaries obtained with https://www.wikidata.org/w/index.php?title=Special:Contributions&target=Peter17-Bot (â‰ˆ 15 pages of 500 edits) between 2017-12-30 16:31 and 2017-12-31 14:09 and did a regex search to get the QIDs. It was not so long and it works! Tubezlob (ðŸ™‹) 18:29, 2 January 2020 (UTC) Adding labels to aggregated values I wanted to know the most common occupations of people with biographies at dawiki, and wrote this query: # Count biographies at dawiki by occupation 
SELECT ?job ?jobLabel (COUNT(?link) AS ?count)
WHERE {
  ?person wdt:P31 wd:Q5.
  ?person wdt:P106 ?job.
  ?link schema:about ?person;
        schema:isPartOf <https://da.wikipedia.org/>.
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"da,en\" }
}
GROUP BY ?job ?jobLabel
ORDER BY DESC(?count)
limit 10
 Try it! The query times out. However it runs fine if I remove \"?jobLabel\" from SELECT and GROUP BY: # Count biographies at dawiki by occupation 
SELECT ?job (COUNT(?link) AS ?count)
WHERE {
  ?person wdt:P31 wd:Q5.
  ?person wdt:P106 ?job.
  ?link schema:about ?person;
        schema:isPartOf <https://da.wikipedia.org/>.
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"da,en\" }
}
GROUP BY ?job
ORDER BY DESC(?count)
limit 10
 Try it! It cannot take long to find 10 labels after the counting is done, so can the query be rewritten to include the labels more efficiently? --Dipsacus fullonum (talk) 14:17, 2 January 2020 (UTC) @Dipsacus fullonum: Named subqueries are your friend. # Count biographies at dawiki by occupation 
SELECT ?job ?jobLabel ?count with {
SELECT ?job (COUNT(?link) AS ?count)
WHERE {
  ?person wdt:P31 wd:Q5.
  ?person wdt:P106 ?job.
  ?link schema:about ?person;
        schema:isPartOf <https://da.wikipedia.org/>.
} 
  GROUP BY ?job
  ORDER BY DESC(?count)
  limit 10
} as %i where
{
  include %i
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"da,en\" }
}
 Try it! --Tagishsimon (talk) 14:51, 2 January 2020 (UTC) @Tagishsimon: Thank you very much. I also want to know the distribution of sexes for each occupation. I found a method defining variables ?male og ?female as either 0 or 1 and then making sums of these variables for each occupation: # Count biographies at dawiki by occupation and sex
SELECT ?job ?jobLabel ?m ?f ?count with {
SELECT ?job (COUNT(?link) AS ?count) (SUM (?male) as ?m) (SUM (?female) as ?f)
WHERE {
  ?person wdt:P31 wd:Q5.
  ?person wdt:P106 ?job.
  OPTIONAL { ?person wdt:P21 ?sex. }
  BIND(IF(?sex = wd:Q6581097, 1, 0) as ?male) 
  BIND(IF(?sex = wd:Q6581072, 1, 0) as ?female)
  ?link schema:about ?person;
        schema:isPartOf <https://da.wikipedia.org/>.
} 
  GROUP BY ?job
  ORDER BY DESC(?count)
  limit 10
} as %i where
{
  include %i
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"da,en\" }
}
 Try it! It works, but it seems a little weird to use sums. I wonder if there is a more direct and faster way to do this. --Dipsacus fullonum (talk) 16:34, 2 January 2020 (UTC) Url and date Hello. I want to find all items that have a reference with reference URL (P854) -> http://www.mof.gov.cy/mof/cystat/statistics.nsf/All/A2A7A50A241BC682C2257DBF002697D0/$file/GEO_CODES-2015.xls?OpenElement and publication date (P577) with any value. Xaris333 (talk) 01:09, 31 December 2019 (UTC) @Xaris333: SELECT DISTINCT ?item ?itemLabel {
  ?item ?prop ?statement.
  ?statement prov:wasDerivedFrom ?ref.
  ?ref pr:P854 <http://www.mof.gov.cy/mof/cystat/statistics.nsf/All/A2A7A50A241BC682C2257DBF002697D0/$file/GEO_CODES-2015.xls?OpenElement>.
  ?ref pr:P577 [].
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! Vahurzpu (talk) 03:25, 3 January 2020 (UTC) Thanks!! Xaris333 (talk) 22:35, 3 January 2020 (UTC) News article archives I'm looking to get a list of news article (Q5707594) with full work available at URL (P953) but no archive URL (P1065) qualifier (Q80317512 would be an example of that). --Trade (talk) 00:35, 1 January 2020 (UTC) @Trade: Seems to be: SELECT ?item ?itemLabel ?P953
WHERE 
{
  ?item wdt:P31 wd:Q5707594. 
  ?item p:P953 ?stat. 
  ?stat ps:P953 ?P953. 
  filter not exists {?stat prov:wasDerivedFrom/pr:P1065 ?thing .}
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  
}
 Try it! --Tagishsimon (talk) 14:08, 1 January 2020 (UTC) @Tagishsimon:, there seems to be something wrong? I've already found several items that already contains an archive URL (P1065) qualifier. --Trade (talk) 01:03, 2 January 2020 (UTC) @Trade: Could you point me at one; might help in figuring out the error. --Tagishsimon (talk) 01:18, 2 January 2020 (UTC) @Tagishsimon:, Mange mennesker drÃ¦bt i skyderier i moskeer (Q62071687) --Trade (talk) 09:59, 2 January 2020 (UTC) @Trade: The problem here is that for 200 objects, the one you give as an exampel is one of them, the archive URL (P1065) is given as a qualifier instead of a reference in the full work available at URL (P953) claim. Only 15 objects use archive URL (P1065) as a reference. If you want to exclude both types, try the following query, but I really think there is some cleaning up to be done for those objects that use archive URL (P1065) in an incorrect way. I am not sure which way is the correct, but the different uses are really confusing. SELECT ?item ?itemLabel ?P953
WHERE 
{
  ?item wdt:P31 wd:Q5707594. 
  ?item p:P953 ?stat. 
  ?stat ps:P953 ?P953. 
  filter ( not exists {?stat prov:wasDerivedFrom/pr:P1065 ?thing .}  && not exists {?stat pq:P1065 ?thing2 } )
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Larske (talk) 11:28, 2 January 2020 (UTC) New year's resolution is to read the question properly; Trade pointed to qualifiers. My response (and I was probably thinking of Xaris33's preceding question) dealt with references. --Tagishsimon (talk) 12:26, 2 January 2020 (UTC) @Larske:, most of the incorrect uses were made a long time ago by a few users so correcting them should not be a problem. The way i understand there are two ways to use archive url: Either as a qualifier for full work available at URL (P953), official website (P856) and withdrawn identifier value (Q21441764). Or as a property for references. --Trade (talk) 15:29, 3 January 2020 (UTC) Sitelinks for multiple items At https://www.wikidata.org/w/index.php?title=Wikidata:Project_chat&oldid=1083113466#3_conflicted_categories_should_probably_be_reduced_to_2 it was suggested that three items should be made into two because of overlap in content on some wikis. I thought I could use the query service for a list of sitelinks which existed for all three. I used a query for URLs of items, but couldn't see a way to make a list of sites for each item or how many of these items existed on each site, so I used another query for a list of sites and their URLs, and copied the results into a spreadsheet to obtain the answer. Can this be achieved with SPARQL? Peter James (talk) 13:15, 25 December 2019 (UTC) @Peter James: Not sure if I understand what you expect. Maybe some of the queries below takes you a bit forward: SELECT ?item ?itemLabel ?sitelinks {
  VALUES ?item { wd:Q5645580 wd:Q6010682 wd:Q9703849 }
  ?item wikibase:sitelinks ?sitelinks .
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! SELECT ?item ?itemLabel ?family (COUNT(?site) AS ?count) (GROUP_CONCAT(?sitey;SEPARATOR=', ') AS ?sites) {
  VALUES ?item { wd:Q5645580 wd:Q6010682 wd:Q9703849 }
  ?site schema:about ?item . 
  BIND(STRAFTER(STRBEFORE(STR(?site),'.org'),'//') AS ?sitex)
  BIND(STRAFTER(?sitex,'.') AS ?family)
  BIND(STRBEFORE(?sitex,'.') AS ?sitey)
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
GROUP BY ?item ?itemLabel ?sitelinks ?family
ORDER BY ?itemLabel ?family
 Try it! @Larske: concerns the query right above: if there exists no sitelink, the item will not been listed. Am I right? Is it possible to change the query that items without any sitelink will also be listed? Doc Taxon (talk) 22:36, 5 January 2020 (UTC) SELECT ?s ?categories (COUNT(?site1a) AS ?count) (GROUP_CONCAT(?site1b;SEPARATOR=', ') AS ?sites) WITH {
SELECT ?site1a (COUNT(*) AS ?count) (SUM(?x) AS ?s) {
  { ?site1 schema:about wd:Q5645580 . BIND(STRBEFORE(STR(?site1),'.org') AS ?site1a) BIND(1 AS ?x) }
  UNION
  { ?site1 schema:about wd:Q6010682 . BIND(STRBEFORE(STR(?site1),'.org') AS ?site1a) BIND(10 AS ?x) }
  UNION
  { ?site1 schema:about wd:Q9703849  . BIND(STRBEFORE(STR(?site1),'.org') AS ?site1a) BIND(100 AS ?x) }
}
GROUP BY ?site1a
ORDER BY ?site1a
} AS %i WHERE {
  include %i
  BIND(STRAFTER(?site1a,'//') AS ?site1b)
  BIND(IF(?s=111,'All three categories',IF(?s=110,'All but Q5645580',IF(?s=101,'All but Q6010682',IF(?s=11,'All but Q9703849',IF(?s=1,'Only Q5645580',IF(?s=10,'Only Q6010682',IF(?s=100,'Only Q9703849',''))))))) AS ?categories)
}
GROUP BY ?s ?categories
ORDER BY DESC(?s)
 Try it! SELECT ?site1b ?categories WITH {
SELECT ?site1a (COUNT(*) AS ?count) (SUM(?x) AS ?s) {
  { ?site1 schema:about wd:Q5645580 . BIND(STRBEFORE(STR(?site1),'.org') AS ?site1a) BIND(1 AS ?x) }
  UNION
  { ?site1 schema:about wd:Q6010682 . BIND(STRBEFORE(STR(?site1),'.org') AS ?site1a) BIND(10 AS ?x) }
  UNION
  { ?site1 schema:about wd:Q9703849  . BIND(STRBEFORE(STR(?site1),'.org') AS ?site1a) BIND(100 AS ?x) }
}
GROUP BY ?site1a
ORDER BY ?site1a
} AS %i WHERE {
  include %i
  BIND(STRAFTER(?site1a,'//') AS ?site1b)
  BIND(IF(?s=111,'All three categories',IF(?s=110,'All but Q5645580',IF(?s=101,'All but Q6010682',IF(?s=11,'All but Q9703849',IF(?s=1,'Only Q5645580',IF(?s=10,'Only Q6010682',IF(?s=100,'Only Q9703849',''))))))) AS ?categories)
}
ORDER BY ?site1b
 Try it! --Larske (talk) 13:04, 26 December 2019 (UTC) Thanks; I was thinking of something like the fourth query, but the third query provides the best results. Peter James (talk) 14:51, 26 December 2019 (UTC) I want to list the sitelink to English and German Wikipedia SELECT ?item ?itemLabel ?sitelink_de ?sitelink_en ?ATP_Kennung (GROUP_CONCAT(?sitey;SEPARATOR=' ') AS ?sites) {
  OPTIONAL { ?item wdt:P536 ?ATP_Kennung. }
  ?site schema:about ?item . 
  BIND(STRAFTER(STRBEFORE(STR(?site),'.org'),'//') AS ?sitex)
  BIND(STRBEFORE(?sitex,'.') AS ?sitey)
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en,[AUTO_LANGUAGE]\". }
}
GROUP BY ?item ?itemLabel ?sitelink_de ?sitelink_en ?ATP_Kennung ?sitelinks
 Try it! Hi! I want to list the sitelink to English and to German Wikipedia, but the columns are empty. I only need the plain lemma like \"Rafael Nadal\". What can I do? Doc Taxon (talk) 23:09, 5 January 2020 (UTC)?article doesn't work: SELECT ?item ?itemLabel ?sitelink_en ?sitelink_de ?ATP_Kennung (GROUP_CONCAT(?sitey;SEPARATOR=' ') AS ?sites) {
  OPTIONAL { ?item wdt:P536 ?ATP_Kennung. }
  ?article schema:about ?item ; schema:isPartOf <https://en.wikipedia.org/> ;  schema:name ?sitelink_en .
  ?article schema:about ?item ; schema:isPartOf <https://de.wikipedia.org/> ;  schema:name ?sitelink_de .
  ?site schema:about ?item . 
  BIND(STRAFTER(STRBEFORE(STR(?site),'.org'),'//') AS ?sitex)
  BIND(STRBEFORE(?sitex,'.') AS ?sitey)
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en,[AUTO_LANGUAGE]\". }
}
GROUP BY ?item ?itemLabel ?sitelink_en ?sitelink_de ?ATP_Kennung ?sitelinks
 Try it! Doc Taxon (talk) 23:19, 5 January 2020 (UTC) @Doc Taxon: This, I think SELECT ?item ?itemLabel ?sitelink_de ?sitelink_en ?ATP_Kennung ?sites with {
SELECT ?item ?ATP_Kennung (GROUP_CONCAT(?sitey;SEPARATOR=' ') AS ?sites) where {
  OPTIONAL { ?item wdt:P536 ?ATP_Kennung. }
  ?site schema:about ?item .   
  BIND(STRAFTER(STRBEFORE(STR(?site),'.org'),'//') AS ?sitex)
  BIND(STRBEFORE(?sitex,'.') AS ?sitey)
} GROUP BY ?item ?ATP_Kennung } as %i where
{ include %i
  optional {?sitelink_en ^schema:name ?article_en .
            ?article_en schema:about ?item ;
                     schema:isPartOf <https://en.wikipedia.org/> .}
  optional {?sitelink_de ^schema:name ?article_de .
            ?article_de schema:about ?item ;
                     schema:isPartOf <https://de.wikipedia.org/> .}
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en,[AUTO_LANGUAGE]\". }
}
 Try it! --Tagishsimon (talk) 23:22, 5 January 2020 (UTC) SELECT ?item ?itemLabel ?sitelink_de ?sitelink_en ?ATP_Kennung with {
SELECT ?item ?ATP_Kennung where {
  OPTIONAL { ?item wdt:P536 ?ATP_Kennung. }
  ?site schema:about ?item .   
} GROUP BY ?item ?ATP_Kennung } as %i where
{ include %i
  optional {?sitelink_en ^schema:name ?article_en .
            ?article_en schema:about ?item ;
                     schema:isPartOf <https://en.wikipedia.org/> .}
  optional {?sitelink_de ^schema:name ?article_de .
            ?article_de schema:about ?item ;
                     schema:isPartOf <https://de.wikipedia.org/> .}
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en,[AUTO_LANGUAGE]\". }
}
 Try it! @Tagishsimon: okay, I don't need the list of ?sites and have taken them out. But I get 3829 results, in Wikidata there are 3858 items with P536. Where are the missing ones? Doc Taxon (talk) 23:34, 5 January 2020 (UTC) @Doc Taxon: If you don't need ?sites, it becomes a simpler query. The lost items in the previous report would have been excluded for having no sitelinks, I think. SELECT ?item ?itemLabel ?sitelink_de ?sitelink_en ?ATP_Kennung where {
  ?item wdt:P536 ?ATP_Kennung. 
  optional {?sitelink_en ^schema:name ?article_en .
            ?article_en schema:about ?item ;
                     schema:isPartOf <https://en.wikipedia.org/> .}
  optional {?sitelink_de ^schema:name ?article_de .
            ?article_de schema:about ?item ;
                     schema:isPartOf <https://de.wikipedia.org/> .}
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"de,en,[AUTO_LANGUAGE]\". }
}
 Try it! --Tagishsimon (talk) 23:39, 5 January 2020 (UTC) @Tagishsimon: with this query I get all the 3858 results. Thank you very much, Doc Taxon (talk) 23:44, 5 January 2020 (UTC) @Tagishsimon: is it possible to get also a column of ?itemLabel_de and ?itemLabel_en ? Doc Taxon (talk) 00:22, 6 January 2020 (UTC) @Doc Taxon: Yup. SELECT ?item ?ATP_Kennung ?itemLabel_en ?sitelink_en ?itemLabel_de ?sitelink_de where {
  ?item wdt:P536 ?ATP_Kennung. 
  optional {?sitelink_en ^schema:name ?article_en .
            ?article_en schema:about ?item ;
                     schema:isPartOf <https://en.wikipedia.org/> .}
  optional {?sitelink_de ^schema:name ?article_de .
            ?article_de schema:about ?item ;
                     schema:isPartOf <https://de.wikipedia.org/> .}
  optional {?item rdfs:label ?itemLabel_en. filter(lang(?itemLabel_en)=\"en\")}
  optional {?item rdfs:label ?itemLabel_de. filter(lang(?itemLabel_de)=\"de\")}
}
 Try it! --Tagishsimon (talk) 00:30, 6 January 2020 (UTC) cool, thank you Doc Taxon (talk) 00:34, 6 January 2020 (UTC) Search for multiple (specific) types via wikibase:api \"EntitySearch\" running into 50 record limit? I'm trying to let people search for anything, as long as it's something in the popular culture like an actor, book, movie, sports team, etc. So if I search \"Patriots\" I should get the NFL Team and the movie The Patriot. And I think I've gotten a good start with this: SELECT distinct ?ordinal ?item ?itemLabel ?itemDescription ?image WHERE {
  SERVICE wikibase:mwapi {
    bd:serviceParam wikibase:api \"EntitySearch\";
                    wikibase:endpoint \"www.wikidata.org\";
                    mwapi:search \"Patriots\";
                    mwapi:limit 1000;
                    mwapi:language \"en\" .
    ?item wikibase:apiOutputItem mwapi:item .
    ?ordinal wikibase:apiOrdinal true .
  } 
  ?item wdt:P31/wdt:P279* ?type.
  OPTIONAL{?item wdt:P18 ?image .}
  FILTER( ?type in (wd:Q5, wd:Q17537576, wd:Q12973014))
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
} ORDER BY ASC (?ordinal) LIMIT 100
 Try it! I'm using the EntitySearch service so that I can get the :apiOrdinal value to help sort the results for relevance, and ?type contains the whole list of parent types so that I can just use \"creative work\" and \"sports team\" as catch-alls to filter on. (The FILTER list of Q types is incomplete, I was just starting to get the test going)But, the problem happens when you replace \"Patriots\" with \"Fellowship\". In theory I should get the book and film Fellowship of the Ring. But the only result is for the \"group\" Fellowship of the Ring. If you take off the FILTER, you'll see it only returns 40 records from the EntitySearch for some reason? And if you change it to \"Fellowship of the Ring\" it will correctly find the book and film, so the EntitySearch can find those two. But for some reason it seems like EntitySearch is capping the results to a limit of 40?Any ideas on what's going on? Or an alternative to do this search differently, preferably still with the :apiOrdinal so that the results are sorted by relevance?Thanks! â€“ The preceding unsigned comment was added by Thomas.lumen (talk â€¢ contribs) at 17:16â€Ž, 6. 7. 2019 (UTC).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) Get all items that come under a particular category I'm trying to find all items that can be marked as scientific items. For instance, photosynthesis, ohm's law, etc. Like, photosynthesis belongs to Biological Processes category but when I use the category in the below query I also get \"death\" as an item which is not an intended item. I think I am leaning more towards the concepts that one studies during his school years. Please let me know if there's a specific category that I should aim for? SELECT ?item ?itemLabel WHERE {
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  ?item wdt:P31 wd:Q336.
}
LIMIT 200 â€“ The preceding unsigned comment was added by 204.246.162.35 (talk â€¢ contribs) at 22:35â€Ž, 16 July 2019 (UTC).Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) WikiProject every politician Could you please help to create a query which returns all politicians and the linked persons to them (like sibling, siblings-in-law, family, unusual family relationships etc.)? Can this be done with one query? â€“ The preceding unsigned comment was added by Danail Stoyanov (talk â€¢ contribs) at 13 aoÃ»t 2019 Ã  09:15â€Ž (EST) (UTC).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) WikiData SPARQL query for plants data I am a BE CSE undergrad student currently working on collecting information about plants so I am trying to query about all the plants from the wikidata Query Engine but I couldn't find a way. Any help on this is appreciableÎ» â€“ The preceding unsigned comment was added by 14.139.161.242 (talk â€¢ contribs) at 14 aoÃ»t 2019 Ã  10:06â€Ž (EST) (UTC).Does this help you get started? SELECT ?plant ?plantLabel
WHERE
{
         # any instance or subclass of plant
  ?plant wdt:P31/wdt:P279* wd:Q756
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }
}
 Try it! â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) Query of Species of Trees in different languages I am an artists looking at the representation of species of trees in Wikidata, looking for a list that contains f.ex. Quercus Robur, Acacia_podalyriifolia etc.It is interesting to see that this query gets all individual trees, not species, as a result: SELECT ?item ?itemLabel WHERE { ?item wdt:P31 wd:Q10884. #is instance of tree
 ?item wdt:P17 wd:Q30 . # specific for US, uses other code than 'citizenship' of a place specified non-human } SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }Rather, I'm looking for a list like this: https://en.wikipedia.org/wiki/Category:Trees_of_Africa (without the location property)I would like to see what kind of species of trees are represented in EN, FR, SP, NL. I started something like this, but as 'taxon' is a property with a wide variety of values, it gives me a time out. SELECT ?item ?itemLabel WHERE { ?item wdt:P31 wd:Q16521;
   wdt:P105 wd:Q7432.
 SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". } }On the Wiki Data page of f.ex. Quercus Robur, everything refers to Quercus, which leads me to Genus/Family/Order etc. It would be wonderful to get some help on this. Many thanks in advance! â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) chemical substances used in plastics I would like to retrieve the CAS number of all chemical substances that are used in plastics.This is what I built with the Query Helper, however it returns nothing. Any suggestions? #CAS number of all chemical substances that are used in plastics
   SELECT ?sitelink ?item ?CAS_Registry_Number WHERE {
   ?item wdt:P279 wd:Q79529.
   ?sitelink schema:about ?item.
   SERVICE wikibase:label { bd:serviceParam wikibase:language 
   \"[AUTO_LANGUAGE],en\". }
   ?item wdt:P366 wd:Q60082936.
   OPTIONAL { ?item wdt:P231 ?CAS_Registry_Number. }
}
ORDER BY (?itemLabel)|
 Try it! â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) Trouble with nested town/state entities There is a lighthouse (Q7084940) in Scituate (Q2415936), Massachusetts. Scituate is located within Plymouth County (Q54086). How come asking for lighthouses in Plymouth County doesn't show Scituate light? (other lighthouses do show however) This is the query I'm using:SELECT DISTINCT ?item ?itemLabel ?coords ?image WHERE { ?item wdt:P31 wd:Q39715 ; wdt:P131 wd:Q54086Â ;
             wdt:P625 ?coords OPTIONAL { ?item wdt:P18 ?image } SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],nb,nn,en,fi\" } } ORDER BY ?itemLabelNeverind. I figured it out. Just add an asterisk after P131.â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) Query to get Fotball Players from different countries by divistion and date of birth Hi, I was wondering if that is possible. I have not been able to query anything sports related so I am wondering if it isn't available.â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) DisGeNET and mirbase hello, we are 3 bioinformaticians. We are currently working on the correlation of 2 data bases : disGeNET(URI:http://rdf.disgenet.org/resource) and mirDB.The mirDB is a data base that provide miRNA associated with target gene. disGeNET is a data base that provide gene correlated to their disease. We want to make a query that can relate between the 2 data base and give us the following response : find every miRNA correlated to a gene correlated to a disease(for example:myoptahy). We found in the query example the following query that might be the base of that we want. variants that are associated with renal cell carcinoma (for our example we wantÂ :# miRNA that are associated with myopathy(disease)). SELECT DISTINCT ?reference ?referenceLabel ?pmid WHERE { ?item wdt:P3329 ?civicId ; ?property  ?object .
   ?object prov:wasDerivedFrom ?provenance .
   ?provenance pr:P248 ?reference .
   ?reference wdt:P31 wd:Q13442814Â ;
              wdt:P698 ?pmid . SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\" } }We didn't find the URI of mirDB. is it a problem ? Thank you for your attention. We will be pleased to read your response.â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) Common properties that link politicians Hi,I am trying to get a list of common properties, ideally sorted in descending order, that link two items whose occupation is \"politician.\"These would be things like \"father,\" \"mother,\" \"sibling\" etc.Thank you very much.â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) All eukaryotic proteins Hi!I've been trying to create a query which finds all proteins found in species belonging to the domain \"eukaryote\".I started with this: SELECT ?item ?itemLabel 
WHERE 
{
  ?item wdt:P31 wd:Q8054;
        wdt:P703/?????.
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\". }
}
 Try it! But quickly ran in to the issue that there is no way to find out i a species is a eukaryote or not, hence the question marks.Thanks!â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) Property paths with normalRank I can't seem to be able to combine this answer for returning items with normalRank with property paths.For example, how would I apply it to the following query for retuning all (categories of) cheeses. SELECT ?item ?itemLabel WHERE {
   ?item ((wdt:P31*)/(wdt:P279*)) wd:Q10943;
     rdfs:label ?itemLabel.
   FILTER((LANG(?itemLabel)) = \"en\")
 }
 Try it! The following doesn't seem to work, and neither do any combinations of it, as far as I've discovered. SELECT ?item ?itemLabel WHERE {
  ?item (((p:P31*)/(ps:P31*))/((p:P279*)/(ps:P279*))) wd:Q10943;
    rdfs:label ?itemLabel.
  FILTER((LANG(?itemLabel)) = \"en\")
 }
 Try it! â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Stale request. Random signature with random timestamp archving: Random signature 01:01, 1 January 2020 (UTC) office held by head of government Hello. Many items have located in the administrative territorial entity (P131) -> Paphos District (Q59133). Some of them have also statements with office held by head of government (P1313). Is it possible a query to list all these statements with office held by head of government (P1313)? Xaris333 (talk) 20:00, 6 January 2020 (UTC) @Xaris333: Here's all of them, with a column for P1313. Remove optional{} to make it mandatory that the item has a P1313 ... use filternot exists {} if you want only those with no P131. I forget what the code for your preferred language is, so you might need to fix that in the wikibase:label service. SELECT ?item ?itemLabel ?office ?officeLabel WHERE 
{
  ?item wdt:P131 wd:Q59133 .
  optional {?item wdt:P1313 ?office . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
} order by desc(?officeLabel)
 Try it! hth --Tagishsimon (talk) 20:08, 6 January 2020 (UTC) @Tagishsimon: Hello. Thanks. Is there other way to do this? I want to use the query for Wikidata list, so I want the results to be the Presidents of Communal Councils and not the communities. Xaris333 (talk) 20:24, 6 January 2020 (UTC) @Xaris333: Sure; this, or, at least, this direction. SELECT ?item ?itemLabel ?authority ?authorityLabel WHERE 
{
  ?authority wdt:P131 wd:Q59133 .
  ?authority wdt:P1313 ?item . 
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
} order by desc(?itemLabel)
 Try it! --Tagishsimon (talk) 20:49, 6 January 2020 (UTC) Thanks! Xaris333 (talk) 20:59, 6 January 2020 (UTC) Leading zeros SELECT ?item (CONCAT(?abbr, ':', ?seasonOrd, 'â¨‰', ?episodeOrd) as ?code) WHERE {
  ?item wdt:P31 wd:Q61220733.
  MINUS { ?item wdt:P31 wd:Q21664088. }
  ?item wdt:P179 ?series.
  ?series wdt:P1813 ?abbr.
  ?item wdt:P4908 ?season.
  ?season p:P179 [ pq:P1545 ?seasonOrd ].
  ?item p:P4908 [ pq:P1545 ?episodeOrd ].
}
 Try it! The last number in the ?code (?episodeOrd) field should always have two digits. for instance DS9:1â¨‰5 â†’ DS9:1â¨‰ 0 5. I wasn't able to find a PadLeft funtion. any idea how to do that? --Loominade (talk) 11:58, 6 January 2020 (UTC) @Loominade: You have to roll your own function. SELECT ?item (CONCAT(?abbr, ':', ?seasonOrd, 'â¨‰', ?thing) as ?code) WHERE {
  ?item wdt:P31 wd:Q61220733.
  MINUS { ?item wdt:P31 wd:Q21664088. }
  ?item wdt:P179 ?series.
  ?series wdt:P1813 ?abbr.
  ?item wdt:P4908 ?season.
  ?season p:P179 [ pq:P1545 ?seasonOrd ].
  ?item p:P4908 [ pq:P1545 ?episodeOrd ].
  bind(if(strlen(?episodeOrd)=1,concat(\"0\",?episodeOrd),?episodeOrd) as ?thing)
}
 Try it! --Tagishsimon (talk) 16:06, 6 January 2020 (UTC) that worked, thank you ðŸ˜€--Loominade (talk) 09:11, 7 January 2020 (UTC) Select a point in time and a determination method This query should return only items with a spesific point in time and determination method but it doesn't. There should be only one row per municipality geographical code and one population data. SELECT ?code ?item ?itemLabel ?population ?date
WHERE  { 
    ?item wdt:P3856 ?code .
    ?item p:P1082 [ pq:P459 wd:Q29051383 ] .
    ?item p:P1082 [ pq:P585  ?date ] .
    FILTER (?date  >= \"2020-01-01T00:00:00Z\"^^xsd:dateTime )
    ?item wdt:P1082 ?population .
    FILTER NOT EXISTS { ?item p:P31 [ pq:P582 ?pqend ] }
    FILTER NOT EXISTS { ?item wdt:P582 ?enddt }
    FILTER NOT EXISTS { ?item wdt:P576 ?dissoldt } 
    SERVICE wikibase:label {bd:serviceParam wikibase:language \"en,fr\" .}
 } 
order by ?code
 Try it! --Yanik B 14:09, 9 January 2020 (UTC) @YanikB: Might this be what you seek? In your query, nothing particularly pulled together the three P1082 requirements so as to require that the returned population had an appropriate date & determination method. SELECT ?code ?item ?itemLabel ?population ?date
WHERE  { 
    ?item wdt:P3856 ?code .
    ?item p:P1082 [ ps:P1082 ?population; pq:P459 wd:Q29051383; pq:P585  ?date  ] .
    FILTER (?date  >= \"2020-01-01T00:00:00Z\"^^xsd:dateTime )
    FILTER NOT EXISTS { ?item p:P31 [ pq:P582 ?pqend ] }
    FILTER NOT EXISTS { ?item wdt:P582 ?enddt }
    FILTER NOT EXISTS { ?item wdt:P576 ?dissoldt } 
    SERVICE wikibase:label {bd:serviceParam wikibase:language \"en,fr\" .}
 } 
order by ?code
 Try it! --Tagishsimon (talk) 16:07, 9 January 2020 (UTC) @Tagishsimon: That's exacly it. So nice, thx --Yanik B 16:17, 9 January 2020 (UTC) Change query Hello. Can anyone change this query to find all items that have a reference with reference URL (P854) -> http://www.cystat.gov.cy/mof/cystat/statistics.nsf/All/59681B67FE82FD39C2257AD90053F3FA/$file/POP_CEN_11-POP_PLACE_RESID-EL-171115.xls?OpenElement with also (at the same reference) title (P1476) -> Î‘Ï€Î¿Î³ÏÎ±Ï†Î® Ï€Î»Î·Î¸Ï…ÏƒÎ¼Î¿Ï 2011 (Greek language) and title (P1476) -> Census of population 2011 (English language). SELECT DISTINCT ?item ?itemLabel {
  ?item ?prop ?statement.
  ?statement prov:wasDerivedFrom ?ref.
  ?ref pr:P854 <http://www.cystat.gov.cy/mof/cystat/statistics.nsf/All/59681B67FE82FD39C2257AD90053F3FA/$file/POP_CEN_11-POP_PLACE_RESID-EL-171115.xls?OpenElement>.
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! Xaris333 (talk) 13:58, 11 January 2020 (UTC) @Xaris333: Here is a query proposal: SELECT DISTINCT ?item ?itemLabel {
  ?item ?prop ?statement.
  ?statement prov:wasDerivedFrom ?ref.
  ?ref pr:P854 <http://www.cystat.gov.cy/mof/cystat/statistics.nsf/All/59681B67FE82FD39C2257AD90053F3FA/$file/POP_CEN_11-POP_PLACE_RESID-EL-171115.xls?OpenElement>.
  ?ref pr:P1476 'Î‘Ï€Î¿Î³ÏÎ±Ï†Î® Ï€Î»Î·Î¸Ï…ÏƒÎ¼Î¿Ï 2011'@el .
  ?ref pr:P1476 'Census of population 2011'@en .
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Larske (talk) 15:49, 11 January 2020 (UTC) Thanks!! Xaris333 (talk) 19:30, 11 January 2020 (UTC) Order by number - correctly Given a simple query like SELECT ?item ?itemLabel ?value
{
	?item wdt:P5492 ?value .
	SERVICE wikibase:label { bd:serviceParam wikibase:language \"it,en\"  }    
}
ORDER BY ?value
 Try it! is it possible, in some way, to order the values correctly by a number? At the moment after \"1\" I have \"10015\" ... \"10587\", \"106\", \"10671\" etc. Thank you very much, --EpÃ¬dosis 16:42, 12 January 2020 (UTC) Your values are strings, thus it sorts alphanumerically. It works if you convert to a numeric format for numerical sorting: SELECT ?item ?itemLabel ?value
{
	?item wdt:P5492 ?value .
	SERVICE wikibase:label { bd:serviceParam wikibase:language \"it,en\"  }    
}
ORDER BY xsd:integer(?value)
 Try it! --MisterSynergy (talk) 17:08, 12 January 2020 (UTC) New year challenge Hi and happy New year to everybody !I have tried to combine these 2 queries to get the number and percentage of people dead over 60 year by blood type O, A, B and AB (without the others values), without succÃ¨s. Someone can help ? #LongÃ©vitÃ© en fonction du groupe sanguin
SELECT distinct (count (?item) as ?count) ?groupeLabel WHERE {
SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
   ?item wdt:P1853 ?groupe ;
     }
group by ?groupeLabel
 Try it! #LongÃ©vitÃ© en fonction du groupe sanguin
SELECT distinct (count (?item) as ?count) ?groupeLabel WHERE {
SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
   ?item wdt:P1853 ?groupe ;
         wdt:P569 ?naissance ;
         wdt:P570 ?mort .
        FILTER(?age > 60) .
        BIND((?mort - ?naissance)/365.2425 as ?age )
     }
group by ?groupeLabel
 Try it! Simon Villeneuve (talk) 15:15, 13 January 2020 (UTC)So far, I got this. My problem is that the percentage is calculated with all the blood types. I want a % giving the proportion of each blood type (example : for O group, X % have died at 60, Y % between 70 and 80, etc.) SELECT ?age60 ?age70 ?age80 ?age90 ?items
(CONCAT(str(round(10000 * ?o/?items) / 100), \"%\")  as ?O)
(CONCAT(str(round(10000 * ?a/?items) / 100), \"%\")  as ?A)
(CONCAT(str(round(10000 * ?b/?items) / 100), \"%\")  as ?B)
(CONCAT(str(round(10000 * ?ab/?items) / 100), \"%\")  as ?AB) {
  { 
    SELECT ?age60 ?age70 ?age80 ?age90 (count(?o_items) as ?o) (count(?a_items) as ?a) (count(?b_items) as ?b) (count(?ab_items) as ?ab) (count(?all_items) as ?items) {
      {?o_items wdt:P1853 wd:Q19831451 ; wdt:P569 ?naissance ; wdt:P570 ?mort ; wdt:P31 wd:Q5 } UNION
      {?a_items wdt:P1853 wd:Q19831453 ; wdt:P569 ?naissance ; wdt:P570 ?mort ; wdt:P31 wd:Q5 } UNION
      {?b_items wdt:P1853 wd:Q19831454 ;wdt:P569 ?naissance ; wdt:P570 ?mort ; wdt:P31 wd:Q5 } UNION
      {?ab_items wdt:P1853 wd:Q19831455 ; wdt:P569 ?naissance ; wdt:P570 ?mort ; wdt:P31 wd:Q5 } UNION
      {?all_items wdt:P1853 ?group ; wdt:P569 ?naissance ; wdt:P570 ?mort ; wdt:P31 wd:Q5}
      BIND(round((?mort - ?naissance)/365.2425) as ?age )
      BIND ((60 < ?age && ?age < 70) as ?age60)
      BIND ((70 < ?age && ?age < 80) as ?age70)
      BIND ((80 < ?age && ?age < 90) as ?age80)
      BIND ((90 < ?age && ?age < 100) as ?age90)
  	}
  	GROUP BY ?age60 ?age70 ?age80 ?age90 
  }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"en\"  } 
}
 Try it! Simon Villeneuve (talk) 19:12, 14 January 2020 (UTC) Map: Set individual colors for ordinal scale The following query is counting the numbers of co-authors from a specific author identified by his/her orcid grouped by the co-authors country of citizenship: #defaultView:Map
SELECT  ?coauthor_countryLabel ?shape ?numOfCoAuthors ?layer ?rgb 
WITH {
  SELECT ?author ?searched_author WHERE {
    # Find co-authors
    ?searched_author wdt:P496  \"0000-0002-6778-0887\".
    ?work wdt:P50 ?searched_author, ?author .
  }
  GROUP BY ?author  ?searched_author
} AS %authors
WITH {
  SELECT ?shape ?coauthor_country (COUNT(?author) AS ?numOfCoAuthors) WHERE {
    INCLUDE %authors
           
    # Exclude self-links
    FILTER (?searched_author != ?author)
    
    ?author wdt:P27 ?coauthor_country .
    ?coauthor_country wdt:P3896 ?shape.


  }GROUP BY ?shape ?coauthor_country
} AS %result
WHERE {
  INCLUDE %result
  # Label the results 
  BIND(IF(?numOfCoAuthors <= 2,\"< 2 Authors\",
          IF(?numOfCoAuthors < 5,\"< 5 Authors\",
          IF(?numOfCoAuthors < 10,\"< 10 Authors\",\"> 10 Authors\"))) AS ?layer).
 
  #Set ColorRange for Ordinal Scale
  BIND(IF(?numOfCoAuthors <= 2,\"#fef0d9\",
          IF(?numOfCoAuthors < 5,\"#fdcc8a\",
          IF(?numOfCoAuthors < 10,\"#fc8d59\",\"#d7301f\"))) AS ?rgb).
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  
} ORDER BY DESC(?numOfCoAuthors)
 Try it! As i set up in the end of the query above, i want to define an individual color range, using BIND and IF Functions like to define the Layer-Property. I thought, that it's possible to define the color of points or shaps on wdqs-maps individually. Unfortunately in the query above it won't work. Any ideas? Thanks! --Mfchris84 (talk) 13:23, 14 January 2020 (UTC) @User:Mfchris84: something like #defaultView:Map{layer:?rbg}, I think. --Tagishsimon (talk) 01:37, 15 January 2020 (UTC) @Tagishsimon: that was my idea too, but nothing changed. but - i found the solution: the color-hex-values should be stored in ?rgb without # at the beginning: ?rgb and ?layer variables are fixed variables in the Map-view nd are responsible for re-design the color-set or create the layer. #defaultView:Map
SELECT  ?coauthor_countryLabel ?shape ?numOfCoAuthors ?rgb ?layer
WITH {
  SELECT ?author ?searched_author WHERE {
    # Find co-authors
    ?searched_author wdt:P496  \"0000-0002-6778-0887\".
    ?work wdt:P50 ?searched_author, ?author .
  }
  GROUP BY ?author  ?searched_author
} AS %authors
WITH {
  SELECT ?shape ?coauthor_country (COUNT(?author) AS ?numOfCoAuthors) WHERE {
    INCLUDE %authors
           
    # Exclude self-links
    FILTER (?searched_author != ?author)
    
    ?author wdt:P27 ?coauthor_country .
    ?coauthor_country wdt:P3896 ?shape.


  }GROUP BY ?shape ?coauthor_country
} AS %result
WHERE {
  INCLUDE %result
  # Label the results 
  BIND(IF(?numOfCoAuthors <= 2,\"< 2 Authors\",
          IF(?numOfCoAuthors < 5,\"< 5 Authors\",
          IF(?numOfCoAuthors < 10,\"< 10 Authors\",\"> 10 Authors\"))) AS ?layer).
 
  #Set ColorRange for Ordinal Scale
  BIND(IF(?numOfCoAuthors <= 2,\"fef0d9\",
          IF(?numOfCoAuthors < 5,\"fdcc8a\",
          IF(?numOfCoAuthors < 10,\"fc8d59\",\"d7301f\"))) AS ?rgb).
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
  
} ORDER BY DESC(?numOfCoAuthors)
 Try it! Liste des codes Wikidata des communes franÃ§aises Bonjour et bonne annÃ©e tous, La requÃªte suivante est sensÃ©e lister lâ€™ensemble les codes Wikidata des communes franÃ§aises au 01/01/2019. SELECT ?insee (REPLACE(STR(?item),\".*Q\",\"Q\") AS ?id)
WHERE {
  BIND ( \"2019-01-02\"^^xsd:dateTime as ?dateQuery )
  ?item p:P31 ?nature.
  { ?nature ps:P31 wd:Q484170. }                           # commune de France
  UNION { ?nature ps:P31 wd:Q2989454. }                    # commune nouvelle
  UNION { ?nature ps:P31 wd:Q22927616. }                   # commune franÃ§aise Ã  statut particulier
  OPTIONAL { ?nature pq:P582 ?dateFin }
  OPTIONAL { ?item wdt:P576 ?dateFin. }                    # date dissolution
  FILTER ( !BOUND(?dateFin)|| ?dateFin > ?dateQuery )
  OPTIONAL { ?nature pq:P580 ?dateDÃ©but }
  OPTIONAL { ?item wdt:P571 ?dateDÃ©but. }                  # date crÃ©ation
  FILTER ( !BOUND(?dateDÃ©but) || ?dateDÃ©but < ?dateQuery )
  FILTER ( !STRSTARTS(?insee, \"98\") )                      # - PolynÃ©sie
  FILTER ( !STRSTARTS(?insee, \"975\") )                     # - Saint-Pierre et Miquelon
  ?item wdt:P374 ?insee.
}
ORDER BY (?insee)
 Try it! Le pb est que jâ€™obtiens le message dâ€™erreur suivant Â« Limite du temps de requÃªte atteinte Â».Je me dis quâ€™en limitant la liste aux communes dâ€™une rÃ©gion, avec un filtre, le temps doit Ãªtre moins long. Quelquâ€™un pourrait-il complÃ©ter le code pour restreindre la requÃªte Ã  par exemple lâ€™Occitanie : P31 Q3674 (rÃ©gion) Q18678265 (Occitanie).Merci par avanceIn english : Hi. I have a problem with the above request. I get the following error message \"Request time limit reached\".I tell myself that by limiting the list to the municipalities of a region, with a filter, the time should be shorter. Could someone complete the code to restrict the query to, for example, Occitania: P31 Q3674 (region) Q18678265 (Occitanie)Thanks. Roland45 (talk) 08:40, 14 January 2020 (UTC) Bonjour Ã  tous. Finalement, j'ai rÃ©solu le pb, la premiÃ¨re ligne de la requÃªte comportait une erreur. en remplaÃ§ant SELECT ?insee (REPLACE(STR(?item),\".*Q\",\"Q\") AS ?id par simplement SELECT ?insee ?item, cela fonctionne Roland45 (talk) 15:28, 15 January 2020 (UTC) Wikidata:Database reports/items with P569 greater than P570 Can someone look at this report and query and find out why it stopped running when the decade changed? --RAN (talk) 15:55, 14 January 2020 (UTC) The query simply timed out. I tweaked it a bit and the report got an update, but it is still close to the timeout limit. â€”MisterSynergy (talk) 19:47, 14 January 2020 (UTC) Is there something we can do to prevent the timeout? --RAN (talk) 01:20, 15 January 2020 (UTC) Not really. Internally the query processes almost 2 million items from which it filters the few problematic ones. With the current timeout limit of 60 seconds it will always be a challenge to get this done in time, no matter which tweak we use to make the query more efficient. â€”MisterSynergy (talk) 07:38, 15 January 2020 (UTC) @MisterSynergy: How about if we modify the query to only look at new entries and entries that have been modified since it was last run? I corrected the about 1K historical errors, so we should only have errors in new entries, and where people add errors on purpose, or where bots conflate people with similar names and add in a nonsensical additional birth or death date. RAN (talk) 00:40, 17 January 2020 (UTC) Unfortunately we cannot query following that strategy, as WDQS has basically no access to item histories; it only sees the status quo of an item. The only way is to collect all ~2M items that have P569 and P570 and P31:Q5 and then filter those few which are probably incorrect. â€”MisterSynergy (talk) 00:49, 17 January 2020 (UTC) all items with P536 or P597 and not P4544 Hi! How can I find all items having P536 OR P597 AND NOT P4544? Thank you for any help. Doc Taxon (talk) 03:27, 15 January 2020 (UTC) SELECT DISTINCT ?item WHERE { ?item wdt:P536|wdt:P597 [] . MINUS { ?item wdt:P4544 [] } }
 Try it! â€”MisterSynergy (talk) 12:44, 15 January 2020 (UTC) Unwanted instances This request returns instances that doesn't belong to the specified instance (local administrative entity of quebec (Q81063407)). It should return only one instance for each 1280 items. SELECT DISTINCT ?item ?code ?itemLabel ?statutLabel ?population
WHERE { {  
       SELECT DISTINCT ?item ?code ?itemLabel ?statutLabel ?population
       WHERE {
         ?item wdt:P31/wdt:P279* wd:Q81063407 .
         ?item wdt:P31 ?statut .
         ?item p:P31 ?statut_statement .
         FILTER NOT EXISTS { ?statut_statement pq:P582 ?dtend . }
         ?item wdt:P3856 ?code . 
         ?item p:P1082 [ ps:P1082 ?population; pq:P459 wd:Q29051383; pq:P585  ?date  ] .
         FILTER (?date  >= \"2019-01-01T00:00:00Z\"^^xsd:dateTime )
         FILTER NOT EXISTS { ?item wdt:P582 ?enddt . }
         FILTER NOT EXISTS { ?item wdt:P576 ?dissoldt . } 
         SERVICE wikibase:label { bd:serviceParam wikibase:language 'en,fr'. } .
     } }
 }
 Try it! This, probably. Some items have several P31s each of which are P279s of wd:Q81063407. SELECT DISTINCT ?item ?code ?itemLabel (group_concat(?statutL;separator=\"; \") as ?statutLabel) ?population
       WHERE {
         ?item wdt:P31/wdt:P279* wd:Q81063407 .
         ?item wdt:P31 ?statut .
         ?item p:P31 ?statut_statement .
         FILTER NOT EXISTS { ?statut_statement pq:P582 ?dtend . }
         ?item wdt:P3856 ?code . 
         ?item p:P1082 [ ps:P1082 ?population; pq:P459 wd:Q29051383; pq:P585  ?date  ] .
         FILTER (?date  >= \"2019-01-01T00:00:00Z\"^^xsd:dateTime )
         FILTER NOT EXISTS { ?item wdt:P582 ?enddt . }
         FILTER NOT EXISTS { ?item wdt:P576 ?dissoldt . } 
         SERVICE wikibase:label { bd:serviceParam wikibase:language 'en,fr'. 
                                 ?item rdfs:label ?itemLabel .
                                 ?statut rdfs:label ?statutL . } 
 } group by ?item ?code ?itemLabel ?population
 Try it! --Tagishsimon (talk) 12:26, 16 January 2020 (UTC) @Tagishsimon: Nice try but there are statutL with values that doesnt belong to local administrative entity of quebec (Q81063407), big city (Q1549591) for example. This request almost do the job except for enclave (Q171441) ??? SELECT DISTINCT ?item ?code ?itemLabel ?statutLabel ?population
  WHERE {
         ?item wdt:P31/wdt:P279* wd:Q81063407 .
         ?item wdt:P31 ?statut .
         ?item p:P31 ?statut_statement .
         FILTER NOT EXISTS { ?statut_statement pq:P582 ?dtend . }
         ?item wdt:P3856 ?code . 
         ?item p:P1082 [ ps:P1082 ?population; pq:P459 wd:Q29051383; pq:P585  ?date  ] .
         FILTER (?date  >= \"2019-01-01T00:00:00Z\"^^xsd:dateTime )
         FILTER NOT EXISTS { ?item wdt:P582 ?enddt . }
         FILTER NOT EXISTS { ?item wdt:P576 ?dissoldt . } 
         FILTER NOT EXISTS { ?item wdt:P31 wd:Q902814 } 
         FILTER NOT EXISTS { ?item wdt:P31 wd:Q1637706 } 
         FILTER NOT EXISTS { ?item wdt:P31 wd:Q188443 } 
         FILTER NOT EXISTS { ?item wdt:P31 wd:Q200250 } 
         FILTER NOT EXISTS { ?item wdt:P31 wd:Q1665321 } 
#         FILTER NOT EXISTS { ?item wdt:P31 wd:Q171441 } 
         FILTER NOT EXISTS { ?item wdt:P31 wd:Q1549591 }
         FILTER NOT EXISTS { ?item wdt:P31 wd:Q3518817 }
         SERVICE wikibase:label { bd:serviceParam wikibase:language 'en,fr'. } .
 }
 Try it! --Yanik B 14:04, 16 January 2020 (UTC) @YanikB: Yeah, sorry; I didn't look hard at what was going on. Now I do, too many P31 statements. How's this? SELECT DISTINCT ?item ?code ?itemLabel ?statutLabel ?population
  WHERE {
         ?item p:P31 ?statut_statement .
         ?statut_statement ps:P31 ?statut .
         ?statut wdt:P279* wd:Q81063407 .   
         FILTER NOT EXISTS { ?statut_statement pq:P582 ?dtend . }
         ?item wdt:P3856 ?code . 
         ?item p:P1082 [ ps:P1082 ?population; pq:P459 wd:Q29051383; pq:P585  ?date  ] .
         FILTER (?date  >= \"2019-01-01T00:00:00Z\"^^xsd:dateTime )
         FILTER NOT EXISTS { ?item wdt:P582 ?enddt . }
         FILTER NOT EXISTS { ?item wdt:P576 ?dissoldt . } 
         SERVICE wikibase:label { bd:serviceParam wikibase:language 'en,fr'. } .
 }
 Try it! --Tagishsimon (talk) 22:31, 16 January 2020 (UTC) @Tagishsimon: Very elegant, that's what I was looking for. Your're the best. ^.^ --Yanik B 01:48, 17 January 2020 (UTC) Group URLs in one cell Hi all! I have a doubt. Given this query, SELECT ?person ?personLabel (URI(CONCAT(\"http://www.treccani.it/enciclopedia/\",?ei,\"_(Enciclopedia-Italiana)\")) AS ?eiUrl)
WHERE {
  ?person wdt:P4223 ?ei . 
  ?person wdt:P31 wd:Q5 .
  { ?person wdt:P27 wd:Q38 . } UNION { ?person wdt:P27 wd:Q172579 . }
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"it,en\" . }
}
 Try it! because of the fact that Treccani's Enciclopedia Italiana ID (P4223) has often more than one value, I would appreciate very much the possibility of making each person appear only in one line, with different eiUrls grouped in one cell, separated each other by \", \". Eg. wd:Q13888 - Luchino Visconti - <http://www.treccani.it/enciclopedia/visconti-di-modrone-luchino_(Enciclopedia-Italiana)>, <http://www.treccani.it/enciclopedia/visconti-di-modrone-luchino_res-1732c2c9-87e7-11dc-8e9d-0016357eee51_(Enciclopedia-Italiana)>. Is it possible? Thank you all, --EpÃ¬dosis 11:30, 14 January 2020 (UTC) You can only have non-clickable URLs concatenated in one field, as e.g. here. There is no possibility to concatenate clickable URLs into one field in the WDQS interface; youâ€™d need to do some postprocessing with a script to achieve that. â€”MisterSynergy (talk) 13:30, 14 January 2020 (UTC) @MisterSynergy: I understand, thank you! --EpÃ¬dosis 13:32, 14 January 2020 (UTC) @EpÃ¬dosis: you can make it clickable in Listeria using [ ]. --- Jura 14:20, 20 January 2020 (UTC) All norwegians born before 1802 I would like to have a Query listening all country of citizenship (P27) Norway (Q20) born before 1802 but not dead before 1802 and their residence (P551). I would also like to have residence (P551) With point in time (P585) with References. Breg Pmt (talk) 21:20, 15 January 2020 (UTC) There doesn't seem to be any entries with all that information so I put the residence data in an OPTIONAL clause. Popperipopp (talk) 11:08, 16 January 2020 (UTC) SELECT ?person ?personLabel ?residence ?residenceLabel ?residencePointInTime ?reference
WHERE {
  ?person wdt:P31 wd:Q5 .
  ?person wdt:P27 wd:Q20 .
  ?person wdt:P569 ?birth .
  ?person wdt:P570 ?death .
  
  OPTIONAL {
    ?person p:P551 ?residenceStatement .
    ?residenceStatement ps:P551 ?residence .
    
    OPTIONAL { ?residenceStatement pq:P585 ?residencePointInTime . }
    OPTIONAL { ?residenceStatement prov:wasDerivedFrom ?reference . }
  }
  
  FILTER (?birth < \"1802-01-01\"^^xsd:dateTime && ?death >= \"1802-01-01\"^^xsd:dateTime)
  
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! I fixed ending } and changed \"<\" to \">=\" for ?death. That actually gives 1 person which all requirements: Baltazar Mathias Keilhau (Q893022) --Dipsacus fullonum (talk) 13:47, 20 January 2020 (UTC) Dams Hello. I want to find all items that have country (P17)->Cyprus (Q229) and a value with instance of (P31) which has instance of (P31)->dam (Q12323). Xaris333 (talk) 18:30, 16 January 2020 (UTC) @Xaris333: Take a good hard look at how close your question is to the SPARQL you seek. What's stopping you :) --Tagishsimon (talk) 22:35, 16 January 2020 (UTC) SELECT ?item ?itemLabel 
WHERE 
{
  ?item wdt:P17 wd:Q229 . 
  ?item wdt:P31 wd:Q12323 .
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Tagishsimon (talk) 22:35, 16 January 2020 (UTC) @Tagishsimon: Hello. Is not the query I need. I want to find all items that have country (P17)->Cyprus (Q229) and a value with instance of (P31) and that value (that item) must have instance of (P31)->dam (Q12323).For example, Germasogeia Dam (Q22985821) is not in the list you create. Germasogeia Dam (Q22985821) -> instance of (P31) -> earth-fill dam (Q47499777) and earth-fill dam (Q47499777) -> instance of (P31) -> dam (Q12323) Dipotamos Dam (Q62619235) -> instance of (P31) -> rock-fill dam (Q11350418) and rock-fill dam (Q11350418) -> instance of (P31) -> dam (Q12323) Both Germasogeia Dam (Q22985821) and Dipotamos Dam (Q62619235) must be in the list for example. Xaris333 (talk) 06:16, 17 January 2020 (UTC) @Xaris333: I see. You need to be careful what you ask for: if you ask for P31=Q12323, that is what you'll be given. If, instead, you want items that are a subclass of Q12323 - let us say, that have a P31 which is a wdt:P279* of Q12323 - then that is what you'll be given: SELECT ?item ?itemLabel 
WHERE 
{
  ?item wdt:P17 wd:Q229 . 
  ?item wdt:P31/wdt:P279* wd:Q12323 .
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Tagishsimon (talk) 12:28, 17 January 2020 (UTC)  @Xaris333, Tagishsimon: Just a comment. The modified query will give you the result you asked for, but not because earth-fill dam (Q47499777) is instance of (P31) dam (Q12323). You get it because earth-fill dam (Q47499777) is subclass of (P279) embankment dam (Q1244922) which is subclass of (P279) dam (Q12323). Same goes for rock-fill dam (Q11350418). I think it is \"structurally wrong\" to set earth-fill dam (Q47499777) and rock-fill dam (Q11350418) as instance of (P31) dam (Q12323) as they both represent types/classes of dams and not instances/individuals of dams. Also dome dam (Q56631985) and arch-gravity dam (Q357679), representing other types of dams, have incorrectly been given the property instance of (P31). --Larske (talk) 15:04, 17 January 2020 (UTC) I agree. How can we add the information what type of dam is according to the structure or material? Please read w:en:Dam#Types of dams. Xaris333 (talk) 10:07, 18 January 2020 (UTC) If you want to introduce a \"dam by material\" class object which is subclass of (P279) dam (Q12323), you can do it similar to bridge by material (Q17645478), where you have both wooden bridge (Q428759) and metal bridge (Q3397572) being subclass of (P279) bridge by material (Q17645478) and steel bridge (Q12042110) is a subclass of (P279) metal bridge (Q3397572). Another way, if you don't want to introduce more subclasses is to use the property made from material (P186) for the individual dam objects. Both ways are possible but the SPARQL question you need to select e.g. all \"steel dams\" are different: In the first case you ask for ?dam wdt:P31 wd:\"steel dam\" . (where \"steel dam\" is the new class object) You also have to change the P31 property for all individual \"steel dams\" from the more generic dam (Q12323) to the more specific new class object \"steel dam\". In the other case you ask for ?dam wdt:P31/P279* wd:Q12323; wdt:P186 wd:Q11427 . You also have to make sure that all individual \"steel dams\" have the made from material (P186) property set to steel (Q11427). If there are several individual \"steel dams\", I think the first of these two alternatives is the better one. --Larske (talk) 10:49, 18 January 2020 (UTC) Don't make a new item for steel dam as there already is steel dam (Q16904906). --Dipsacus fullonum (talk) 13:26, 20 January 2020 (UTC) Feature arcticle Hello. I want to find all articles that have country (P17) -> Greece (Q41) and are featured article in English Wikipedia. Xaris333 (talk) 10:10, 18 January 2020 (UTC) @Xaris333: SELECT ?item ?itemLabel {
  ?item wdt:P17 wd:Q41 .
  ?enwp schema:about ?item; schema:isPartOf <https://en.wikipedia.org/> .
  ?enwp wikibase:badge ?badge .
  FILTER(?badge=wd:Q17437796)  # featured article
#  FILTER(?badge=wd:Q17437798)  # good article
  SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE],en\". }
}
 Try it! --Larske (talk) 11:09, 18 January 2020 (UTC) Thanks. Xaris333 (talk) 11:06, 19 January 2020 (UTC) Famous (not french) people born in France Is there any query that could help me get the famous people (not french) born in France ? Thanks ! â€“ The preceding unsigned comment was added by [[User:|?]] ([[User talk:|talk]] â€¢ contribs).Here is a basic one: Items used: France (Q142) â€¯ , human (Q5) â€¯ Properties used: place of birth (P19) â€¯ , country (P17) â€¯ , country of citizenship (P27) â€¯ , coordinate location (P625) â€¯ , instance of (P31) â€¯ """@en;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Request_a_query/Archive/2020/01>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/> .
