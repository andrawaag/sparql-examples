@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-6cee57146ca85991911e3324e189bcf8> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Synia:venue>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:genid-4e694113159d4e3db4a1a913894a81d863452-wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/>;
  sh:select """PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX target: <http://www.wikidata.org/entity/Q15817015>

SELECT DISTINCT
  ?description
  ?value ?valueUrl

  ?wikidata ?wikidataUrl

  ?scholia ?scholiaUrl
WHERE {
  BIND(target: AS ?work)
  {
    BIND(1 AS ?order)
    BIND(\"Title\" AS ?description)
    ?work wdt:P1476 ?value .
    
    BIND(CONCAT(SUBSTR(STR(target:), 32), \"&nbsp;↗\") AS ?wikidata)
    BIND(CONCAT(\"https://www.wikidata.org/wiki/\", SUBSTR(STR(target:), 32)) AS ?wikidataUrl)

    BIND(\"Scholia&nbsp;↗\" AS ?scholia)
    BIND(CONCAT(\"https://scholia.toolforge.org/venue/\", SUBSTR(STR(target:), 32)) AS ?scholiaUrl)

  }
  UNION
  {
    BIND(2.5 AS ?order)
    BIND(\"Language\" AS ?description)
    ?work wdt:P407 ?iri .
    BIND(SUBSTR(STR(?iri), 32) AS ?q) 
    ?iri rdfs:label ?value_string .
    FILTER (LANG(?value_string) = 'en')
    BIND(STR(?value_string) AS ?value)
    BIND(CONCAT(\"#language/\", ?q) AS ?valueUrl)
  }
  UNION
  {
    SELECT
      (8 AS ?order)
      (\"Topics\" AS ?description)
      (GROUP_CONCAT(?value_; separator=\", \") AS ?value)
      # (CONCAT(\"#topics/\", GROUP_CONCAT(?q; separator=\",\")) AS ?valueUrl)
    {
      BIND(1 AS ?dummy)
      target: wdt:P921 ?iri .
      BIND(SUBSTR(STR(?iri), 32) AS ?q) 
      ?iri rdfs:label ?value_string . 
      FILTER (LANG(?value_string) = 'en')
      BIND(COALESCE(?value_string, ?q) AS ?value_)
    }
    GROUP BY ?dummy
  }
  UNION
  {
    BIND(11 AS ?order)
    BIND(\"Homepage\" AS ?description)
    ?work wdt:P856 ?valueUrl .
    BIND(STR(?valueUrl) AS ?value)
  }
} 
ORDER BY ?order""" .
