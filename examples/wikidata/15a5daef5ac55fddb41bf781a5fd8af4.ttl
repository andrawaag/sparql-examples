@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-15a5daef5ac55fddb41bf781a5fd8af4> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Project_chat/Archive/2021/12>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:genid-4e694113159d4e3db4a1a913894a81d818227-wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/>;
  sh:select """PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX psv: <http://www.wikidata.org/prop/statement/value/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX geo: <http://www.opengis.net/ont/geosparql#>
# Based on a query by James Heald
#defaultView:Map
SELECT ?line (COUNT (?line) AS ?layer)

WHERE
{
  {
  SELECT DISTINCT ?station
  WHERE
  {
    VALUES ?stationtype { wd:Q55678 wd:Q55488 }
    ?station wdt:P17 wd:Q159 . # In Russia
    ?station wdt:P31 / wdt:P279* ?stationtype . # Instances of railway station
  }
}
  ?station wdt:P197 ?next .
  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat1 ;
           p:P625/psv:P625/wikibase:geoLongitude ?lon1 .
  ?next    p:P625/psv:P625/wikibase:geoLatitude ?lat2 ;
           p:P625/psv:P625/wikibase:geoLongitude ?lon2 .
  BIND(STRDT(IF(?lat1 < ?lat2,
                CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')'),
                CONCAT('LINESTRING(', STR(?lon2), ' ', STR(?lat2), ',', STR(?lon1), ' ', STR(?lat1), ')')),
             geo:wktLiteral) AS ?line)
}
GROUP BY ?line""";
  <http://example.org/bigdata_select> """# Based on a query by James Heald
#defaultView:Map
SELECT ?line (COUNT (?line) AS ?layer)
WITH
{
  SELECT DISTINCT ?station
  WHERE
  {
    VALUES ?stationtype { wd:Q55678 wd:Q55488 }
    ?station wdt:P17 wd:Q159 . # In Russia
    ?station wdt:P31 / wdt:P279* ?stationtype . # Instances of railway station
  }
} AS %stations
WHERE
{
  INCLUDE %stations
  ?station wdt:P197 ?next .
  ?station p:P625/psv:P625/wikibase:geoLatitude ?lat1 ;
           p:P625/psv:P625/wikibase:geoLongitude ?lon1 .
  ?next    p:P625/psv:P625/wikibase:geoLatitude ?lat2 ;
           p:P625/psv:P625/wikibase:geoLongitude ?lon2 .
  BIND(STRDT(IF(?lat1 < ?lat2,
                CONCAT('LINESTRING(', STR(?lon1), ' ', STR(?lat1), ',', STR(?lon2), ' ', STR(?lat2), ')'),
                CONCAT('LINESTRING(', STR(?lon2), ' ', STR(?lat2), ',', STR(?lon1), ' ', STR(?lat1), ')')),
             geo:wktLiteral) AS ?line)
}
GROUP BY ?line""" .
