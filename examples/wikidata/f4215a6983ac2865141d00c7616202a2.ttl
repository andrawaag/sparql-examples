@prefix sh: <http://www.w3.org/ns/shacl#> .
@prefix rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> .
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#> .
@prefix dcterms: <http://purl.org/dc/terms/> .
@prefix schema: <https://schema.org/> .

<https://www.wikidata.org/#query-f4215a6983ac2865141d00c7616202a2> a sh:SPARQLExecutable,
    sh:SPARQLSelectExecutable;
  dcterms:isPartOf <https://www.wikidata.org//wiki/Wikidata:Project_chat/Archive/2018/04>;
  dcterms:license <https://creativecommons.org/licenses/by-sa/4.0/>;
  sh:prefixes _:genid-4e694113159d4e3db4a1a913894a81d86266-wikidata_prefixes;
  schema:target <https://query.wikidata.org/sparql/>;
  sh:select """PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX bd: <http://www.bigdata.com/rdf#>
SELECT ?item ?itemLabel ?isni ?isniLink ?gnd ?gndLink ?loc ?locLink

WITH {
  SELECT * WHERE {
    wd:P213 wdt:P1630 ?isniFormatterUrl.
    wd:P227 wdt:P1630 ?gndFormatterUrl.
    wd:P244 wdt:P1630 ?locFormatterUrl.
  }
} AS %urls WHERE {
  {
  SELECT ?item ?itemLabel ?isni ?gnd ?loc WHERE {
    ?item wdt:P31 wd:Q5 .
    ?item wdt:P213 ?isni .
    MINUS {?item wdt:P214 [] .}
    OPTIONAL {?item wdt:P227 ?gnd.}
    OPTIONAL {?item wdt:P244 ?loc.}
    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }
  }
}.
  INCLUDE %urls.
  BIND(IRI(REPLACE(?isniFormatterUrl, \"\\\\$1\", ?isni)) AS ?isniLink)
  BIND(IRI(REPLACE(?gndFormatterUrl, \"\\\\$1\", ?gnd)) AS ?gndLink)
  BIND(IRI(REPLACE(?locFormatterUrl, \"\\\\$1\", ?loc)) AS ?locLink)
}
ORDER BY DESC(?gnd) DESC(?loc)""";
  <http://example.org/bigdata_select> """SELECT ?item ?itemLabel ?isni ?isniLink ?gnd ?gndLink ?loc ?locLink
WITH {
  SELECT ?item ?itemLabel ?isni ?gnd ?loc WHERE {
    ?item wdt:P31 wd:Q5 .
    ?item wdt:P213 ?isni .
    MINUS {?item wdt:P214 [] .}
    OPTIONAL {?item wdt:P227 ?gnd.}
    OPTIONAL {?item wdt:P244 ?loc.}
    SERVICE wikibase:label { bd:serviceParam wikibase:language \"[AUTO_LANGUAGE]\". }
  }
} AS %results
WITH {
  SELECT * WHERE {
    wd:P213 wdt:P1630 ?isniFormatterUrl.
    wd:P227 wdt:P1630 ?gndFormatterUrl.
    wd:P244 wdt:P1630 ?locFormatterUrl.
  }
} AS %urls WHERE {
  INCLUDE %results.
  INCLUDE %urls.
  BIND(IRI(REPLACE(?isniFormatterUrl, \"\\\\$1\", ?isni)) AS ?isniLink)
  BIND(IRI(REPLACE(?gndFormatterUrl, \"\\\\$1\", ?gnd)) AS ?gndLink)
  BIND(IRI(REPLACE(?locFormatterUrl, \"\\\\$1\", ?loc)) AS ?locLink)
}
ORDER BY DESC(?gnd) DESC(?loc)""" .
